//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                                  (C) VESTA 2011                                       //
//        Het doel van het model is om beleidsinformatie te genereren met betrekking    //
//        tot de energievoorziening van de gebouwde omgeving.                           //
//                                                                                      //
//                         Planbureau voor de Leefomgeving                              //
//                               FEBRUARI 2010                                          //
//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//     * In het geval van externe publicatie van onderzoeksresultaten waarbij gebruik   //
//       is gemaakt van de verstrekte gegevens dient de gebruiker deze resultaten aan   //
//       het PBL mede te delen middels de toezending van 2 exemplaren van de betref-    //
//       fende publicatie aan het hoofd van het team ROL van het PBL.                   //
//                                                                                      //
//     * In elke publicatie waarin de gegevens van het PBL in enige vorm zijn opgenomen,//
//       zal de gebruiker de vermelding opnemen: "Bron: Planbureau voor de Leefomgeving"//
//       Indien het een bewerking van de gegevens betreft, dient te worden opgenomen:   //
//       "Gebaseerd op Planbureau voor de Leefomgeving gegevens".                       //
//                                                                                      //
//     * Het PBL is onder geen enkele voorwaarde aansprakelijk tegenover gebruiker voor //
//       enige schade, mede begrepen winstderving, verlies van gegevens, schade         //
//       tengevolge van fouten of onjuistheden in gegevens, schade tengevolge van fouten//
//       of onvolkomenheden in de documentatie of andere incidentele of gevolgschade    //
//       welke voortvloeit uit of in verband staat met gebruik of het niet kunnen ge-   //
//       bruiken van gegevens.                                                          //
//                                                                                      //
//     * Het PBL staat niet in voor de juistheid van de resultaten die worden verkregen //
//       door combinatie van diverse, op zichzelf staande PBL-gegevens onderling,       //
//       of met die uit andere bronnen.                                                 //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//       Deze DMS-file wordt aangeroepen door stam.dms                                  //
//       In deze configuratiefile worden de brondata geconfigureerd.                    //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

container BronData: Using = "Units;Geography", FreeData = "False"
{
	#include <BAG.dms>
	#include <Uitleg.dms>

	unit<uint32> EnergieLabelData_src
	:	StorageName     = "%SourceDataProjDir%/vraag/wonen/definitieve_labels_fixed_date_311214_v2.csv"
	,	StorageType     = "gdal.vect"
	,	StorageReadOnly = "True"
	{
		parameter<string> SQoute:= '\''';

		attribute<string> HLT_unquoted:= trim(replace(HLT,SQoute,''));
		attribute<string> adres_key   := 
			replace(Postcode,SQoute,'') + '_' + replace(HNR,SQoute,'') + 
			(strlen(HLT_unquoted) >0 
				? '_' + HLT_unquoted
				: ''
			);
		attribute<Classifications/energielabel> energielabel_rel:= rlookup(replace(replace(label,SQoute,''),'A','A+'), Classifications/energielabel/Label);
		attribute<uint32>                       datum_numeriek  := uint32(replace(DATUM,SQoute,''));
	}

	unit<uint32> EnergieLabelData:= unique(EnergieLabelData_src/adres_key)
	{
		attribute<string> nummeraanduiding_id := BAG/import/nummeraanduiding/identificatie[rlookup(values, BAG/import/nummeraanduiding/adres_key)];
		attribute<string> VBO_ID    := rjoin(nummeraanduiding_id, BAG/import/vbo/H_ADRES, BAG/import/vbo/VBO_ID), FreeData = "False";
		
		attribute<uint32>                       datum_laatste        := max(EnergieLabelData_src/datum_numeriek, rlookup(EnergieLabelData_src/adres_key, values));
		attribute<Classifications/energielabel> energielabel_rel     := rjoin(values + '_' + string(datum_laatste), EnergieLabelData_src/adres_key + '_' + string(EnergieLabelData_src/datum_numeriek), EnergieLabelData_src/energielabel_rel), FreeData = "False";
	}

	container EnergieLabelData_pc6
	{
		parameter<String> FileName := '';
		parameter<String> File: StorageName = "%sourceDataProjDir%/vraag/wonen/pc6_energielabel.csv", StorageType = "str", StorageReadOnly = "True";
		parameter<string>  Header := ReadLines(File, void, 0);
		unit<uint32> RowSet := Range(UInt32, 0, StrCount(File, '\n')-1)
		{
			attribute<String> Lines := ReadLines(File, ., Header/ReadPos);
			
		}
		
		attribute<String> PC6(RowSet) := ReadElems(RowSet/Lines, String, const(0, RowSet));
		
		attribute<uint32> aantal_app(RowSet) := ReadElems(RowSet/Lines, UInt32, PC6/ReadPos);
		attribute<uint32> aantal_ap (RowSet) := ReadElems(RowSet/Lines, UInt32, aantal_app/ReadPos);
		attribute<uint32> aantal_a  (RowSet) := ReadElems(RowSet/Lines, UInt32, aantal_ap/ReadPos );
		attribute<uint32> aantal_b  (RowSet) := ReadElems(RowSet/Lines, UInt32, aantal_a/ReadPos  );
		attribute<uint32> aantal_c  (RowSet) := ReadElems(RowSet/Lines, UInt32, aantal_b/ReadPos  );
		attribute<uint32> aantal_d  (RowSet) := ReadElems(RowSet/Lines, UInt32, aantal_c/ReadPos  );
		attribute<uint32> aantal_e  (RowSet) := ReadElems(RowSet/Lines, UInt32, aantal_d/ReadPos  );
		attribute<uint32> aantal_f  (RowSet) := ReadElems(RowSet/Lines, UInt32, aantal_e/ReadPos  );
		attribute<uint32> aantal_g  (RowSet) := ReadElems(RowSet/Lines, UInt32, aantal_f/ReadPos  );
		attribute<uint32> totaal    (RowSet) := ReadElems(RowSet/Lines, UInt32, aantal_g/ReadPos  );
	}

	#include <Bebouwing.dms>
	#include <Sloop.dms>
	#include <GGSectorAfBij.dms>
	#include <GLTBVerdeling.dms>
	
	container energie: Descr = "Blok 2", FreeData = "False"
	{
		
		container vraag
		{
			container ModelBagWoning := 
				CalculationSchemes/EnergieKengetallen/import(
					'20160707_Woningen_BAG.csv', 
					'WoningType_str', 
					Classifications/WoningtypeGeoHoogte, Classifications/BouwJaarBag, Units/nrWoningen, Units/nrWoningen
				)
			{
				container results
				{
					container Verbeteringen
					{
						container S_H_T := GebouwTempl(ModelObject, GJ_Yr_per_Eenheid, EUR_per_eenheid,  Functioneel/Ruimteverwarming, const(0.0[GJ_Yr_Per_Eenheid], ModelObject), 'A');
						container S_H_B := GebouwTempl(ModelObject, GJ_Yr_per_Eenheid, EUR_per_eenheid,  Functioneel/Ruimteverwarming, const(0.0[GJ_Yr_Per_Eenheid], ModelObject), 'B');
						container S_H_AP:= GebouwTempl(ModelObject, GJ_Yr_per_Eenheid, EUR_per_eenheid,  Functioneel/Ruimteverwarming, const(0.0[GJ_Yr_Per_Eenheid], ModelObject), 'C');
						container S_T_B := GebouwTempl(ModelObject, GJ_Yr_per_Eenheid, EUR_per_eenheid, S_H_T/verbruik, S_H_T/Comfort_verbetering_tov_h, 'D');
						container S_T_AP:= GebouwTempl(ModelObject, GJ_Yr_per_Eenheid, EUR_per_eenheid, S_H_T/verbruik, S_H_T/Comfort_verbetering_tov_h, 'E');
						container S_B_AP:= GebouwTempl(ModelObject, GJ_Yr_per_Eenheid, EUR_per_eenheid, S_H_B/verbruik, S_H_B/Comfort_verbetering_tov_h, 'F');
					}
				}
			}
			container ModelWoningNieuwbouw :=
				CalculationSchemes/EnergieKengetallen/import(
					'20160525_Woningen_Nieuwbouw_BAG.csv',
					'WoningType_str',
					Classifications/woningtype, Classifications/BouwJaar, Units/nrWoningen, Units/nrWoningen
				)
			{
				container results {
					container Verbeteringen
					{
						container S_X_X := GebouwTempl(ModelObject, GJ_Yr_per_Eenheid, EUR_per_eenheid,  Functioneel/Ruimteverwarming, const(0.0[GJ_Yr_Per_Eenheid], ModelObject), 'X');
						
						container S_H_T := S_X_X;
						container S_H_B := S_X_X;
						container S_H_AP:= GebouwTempl(ModelObject, GJ_Yr_per_Eenheid, EUR_per_eenheid,  Functioneel/Ruimteverwarming, const(0.0[GJ_Yr_Per_Eenheid], ModelObject), 'C');
						container S_T_B := S_X_X;
						container S_T_AP:= S_X_X;
						container S_B_AP:= S_H_AP;
					}
				}
			}
			container ModelBagUtil :=
				CalculationSchemes/EnergieKengetallen/import(
					'20160706_Utiliteiten_BAG.csv',
					'UtiliteitType_str', 
					Classifications/UtiliteitBAG,Classifications/BouwjaarUtilBag,  Units/m2, Units/m2
				)
			{
				container results {
					container Verbeteringen
					{
						container S_X_X := GebouwTempl(ModelObject, GJ_Yr_per_Eenheid, EUR_per_eenheid, Functioneel/Ruimteverwarming, const(0.0[GJ_Yr_Per_Eenheid], ModelObject), 'X');
						
						container S_H_T := S_X_X;
						container S_H_B := GebouwTempl(ModelObject, GJ_Yr_per_Eenheid, EUR_per_eenheid, Functioneel/Ruimteverwarming, const(0.0[GJ_Yr_Per_Eenheid], ModelObject), 'B');
						container S_H_AP:= GebouwTempl(ModelObject, GJ_Yr_per_Eenheid, EUR_per_eenheid, Functioneel/Ruimteverwarming, const(0.0[GJ_Yr_Per_Eenheid], ModelObject), 'C');
						container S_T_B := S_X_X;
						container S_T_AP:= S_X_X;
						container S_B_AP:= GebouwTempl(ModelObject, GJ_Yr_per_Eenheid, EUR_per_eenheid, S_H_B/verbruik, S_H_B/Comfort_verbetering_tov_h, 'F');
					}
				}
			}
			
			container ModelUtilNieuwbouw := 
				CalculationSchemes/EnergieKengetallen/import(
					'20160525_Utiliteiten_Nieuwbouw_BAG.csv', 
					'UtiliteitType_str', 
					Classifications/UitlegUtil, Classifications/BouwJaar,  Units/m2, Units/m2
				)
			{
				container results {
					container Verbeteringen
					{
						container S_X_X := GebouwTempl(ModelObject, GJ_Yr_per_Eenheid, EUR_per_eenheid, Functioneel/Ruimteverwarming, const(0.0[GJ_Yr_Per_Eenheid], ModelObject), 'X');
						
						container S_H_T := S_X_X;
						container S_H_B := S_X_X;
						container S_H_AP:= S_X_X;
						container S_T_B := S_X_X;
						container S_T_AP:= S_X_X;
						container S_B_AP:= S_X_X;
					}
				}
			}

			container ModelGlTb2010 :=  
				CalculationSchemes/EnergieKengetallen/import(
					'20160706_Glastuinbouw_BAG.csv', 
					'UtiliteitType_str',
					Classifications/GlTb, Classifications/bouwjaar, Units/m2, Units/m2
				)
			{
				container results {
					container Verbeteringen
					{
						container S_X_X := GebouwTempl(ModelObject, GJ_Yr_per_Eenheid, EUR_per_eenheid, Functioneel/Ruimteverwarming, const(0.0[GJ_Yr_Per_Eenheid], ModelObject), 'X');
						
						container S_H_T := S_X_X;
						container S_H_B := S_X_X;
						container S_H_AP:= S_X_X;
						container S_T_B := S_X_X;
						container S_T_AP:= S_X_X;
						container S_B_AP:= S_X_X;
					}
				}
			}
			container ModelGlTbNieuwbouw :=
				CalculationSchemes/EnergieKengetallen/import(
					'20160525_Glastuinbouw_Nieuwbouw_BAG.csv',
					'UtiliteitType_str',
					Classifications/GlTb, classifications/bouwjaar, Units/m2, Units/m2
				)
			{
				
				container results {
					container Verbeteringen
					{
						container S_X_X := GebouwTempl(ModelObject, GJ_Yr_per_Eenheid, EUR_per_eenheid, Functioneel/Ruimteverwarming, const(0.0[GJ_Yr_Per_Eenheid], ModelObject), 'X');
						
						container S_H_T := S_X_X;
						container S_H_B := S_X_X;
						container S_H_AP:= S_X_X;
						container S_T_B := S_X_X;
						container S_T_AP:= S_X_X;
						container S_B_AP:= S_X_X;
					}
				}
			}
			Template GebouwTempl
			{
				unit<UInt32>  ModelObject;
				unit<Float64> GJ_Yr_per_Eenheid;
				unit<Float64> EUR_per_eenheid;
				attribute<GJ_Yr_per_Eenheid> verbruik_prev(ModelObject); // alleen ruimteverwarming
				attribute<GJ_Yr_per_Eenheid> comfort_prev (ModelObject);
				parameter<String> VerbeterCode;

				// =================== ReboundFactor
				// verbetering van 100 kuub-> 85 kuub met een ReboundFactor van x 
				// geeft een besparing van 15 kuub en 
				// een comfortverbetering ter waarde van 15 kuub * ( 1/ (1-x) - 1); 
				// totaal voordeel is dus 15 kuub  / (1-x).
				// waarvan fractie x de comfortverbetering die gelijk gesteld is aan de Rebound en 
				// fractie (1-x) de feitelijke besparing is.
				attribute<Ratio> ReboundFactor_stap(ModelObject) :=
					=(VerbeterCode!='X') 
					? 'MakeDefined(value(table/RB_FAC_'+VerbeterCode+', Ratio), value(0.0, Ratio))' 
					: 'const(0 / 0, ModelObject, Ratio)';
				
				// de volgende kenmerken betreffen ook aleen RuimteVerwarming
				attribute<GJ_Yr_per_Eenheid> verbruik(ModelObject) := 
					=(VerbeterCode!='X') 
					? 'MakeDefined(value(table/MV'+VerbeterCode+'_EG, GJ_Yr_per_Eenheid), Functioneel/Ruimteverwarming)' 
					: 'const(0 / 0, ModelObject, GJ_Yr_per_Eenheid)';

				attribute<GJ_Yr_per_Eenheid> energie_verbetering_stap(ModelObject) :=  verbruik - verbruik_prev;
				attribute<GJ_Yr_per_Eenheid> totale_verbetering_stap (ModelObject) :=  energie_verbetering_stap / (1.0 - ReboundFactor_stap);
				attribute<GJ_Yr_per_Eenheid> comfort_verbetering_stap (ModelObject) :=  totale_verbetering_stap - energie_verbetering_stap;
				
				
				attribute<Ratio> efficiency (ModelObject) := Functioneel/Ruimteverwarming / verbruik;
				attribute<Ratio> ComfortFactor_stap(ModelObject) := 1.0 / (1.0 - ReboundFactor_stap) - 1.0;
				
				attribute<GJ_Yr_per_Eenheid> energie_verbetering_tov_h(ModelObject) :=  Functioneel/Ruimteverwarming - verbruik;
				attribute<GJ_Yr_per_Eenheid> comfort_verbetering_tov_h(ModelObject) :=  comfort_prev + comfort_verbetering_stap;
				attribute<GJ_Yr_per_Eenheid> totale_verbetering_tov_h (ModelObject) :=  energie_verbetering_tov_h + comfort_verbetering_tov_h;
					
				attribute<EUR_per_eenheid> kosten_min (ModelObject) := 
					=(VerbeterCode!='X') 
					? 'MakeDefined(value(table/MV'+VerbeterCode+'_EG_EUR_MIN, EUR_per_eenheid), value(0.0, EUR_per_eenheid))' 
					: 'const(0 / 0, ModelObject, EUR_per_eenheid)';
				attribute<EUR_per_eenheid> kosten_max (ModelObject) := 
					=(VerbeterCode!='X') 
					? 'MakeDefined(value(table/MV'+VerbeterCode+'_EG_EUR_MAX, EUR_per_eenheid), value(0.0, EUR_per_eenheid))' 
					: 'const(0 / 0, ModelObject, EUR_per_eenheid)';
			}
		}
		
		// Kosten (investeringen etc.) voor warmtelevering:
		// a	Woningen (per woningtype, per leeftijdcategorie).
		// b	Utiliteit (per m2, per aard utiliteit).
		// c	Glastuinbouw (per hectare, per type )
		// d	Onderdelen distributie warmte.
		// e	Transport warmte (prijs per km rechte afstand; gedifferentieerd naar capaciteit; + 'omwegfactor')
		container prijzen: Expr = "CalculationSchemes/EnergiePrijzen/All(Parameters/prijzenBron, log(1.00)[_Yr])", Descr = "zie FO $3.5";
	}
	container WarmteBronnen
	{
		container prijzen := energie/prijzen
		{
			unit<uint32> aardgas_tabel := AardGas/Table;
			attribute<aardgas_tabel> aardgas_tabel_rel(classifications/zichtjaar) := rlookup(classifications/zichtjaar/jaar, UInt16(aardgas_tabel/jaar));
			attribute<EUR_GJ> Gas_GJ             (classifications/zichtjaar) := prijzen/AardGas/results/GrootGebruik/eindgebruik_ex[aardgas_tabel_rel], Descr = "M10";
			
			unit<uint32> elek_tabel := Elektriciteit/Table;
			attribute<elek_tabel> elek_tabel_rel(classifications/zichtjaar) := rlookup(classifications/zichtjaar/jaar, UInt16(elek_tabel/jaar));
			attribute<EUR_GJ> ElekEnGros_GJ     (classifications/zichtjaar) := prijzen/Elektriciteit/results/GrootGebruik/CommodityPrice[elek_tabel_rel];
			attribute<EUR_GJ> ElekKMG_GJ        (classifications/zichtjaar) := prijzen/Elektriciteit/results/KleinMiddelGebruik/eindgebruik_ex[elek_tabel_rel];
			
			unit<uint32> overig_tabel := Overig/Table;
			container    overig_results := Overig/Results;
			attribute<overig_tabel> overig_tabel_rel(classifications/zichtjaar) := rlookup(classifications/zichtjaar/jaar, overig_results/jaar);
			attribute<EUR_GJ> BioMassa_GJ           (classifications/zichtjaar) := overig_results/BioMassa    [overig_tabel_rel];
			attribute<EUR_GJ> Kolen_GJ              (classifications/zichtjaar) := overig_results/Kolen_Excl  [overig_tabel_rel];
			attribute<EUR_GJ> KolenCO2_GJ           (classifications/zichtjaar) := overig_results/Kolen_CO2   [overig_tabel_rel];
		}
		container MetaData
		{
			unit<uint32> RekenType: nrofrows = 7
			{
				attribute<String> Naam: [ 'STEG', 'Kolen', 'Gasmotor', 'Industrie', 'AVI', 'BMC', 'BioWKK' ];
				attribute<String> Label := Naam, DialogType = "LabelString";
			}
			
			unit<uint32>  TypeInfoData
			:	StorageName = "='%datadir%/'+Parameters/prijzenbron+'_TypeInfo.csv'" 
			,	StorageType = "gdal.vect"
			,	StorageReadOnly = "True"
			,	SyncMode    = "None"
			{
				attribute<String > Type;
				attribute<String > Label := Type, DialogType = "LabelString";
				attribute<String > K_kW_min;   // [EUR_kW]
				attribute<String > K_kW_max;   // [EUR_kW]
				attribute<String> MWth_min;   // [MWth]
				attribute<String> JaarKosten; // [_Yr]
				
				attribute<TypeInfoList> TypeInfoList_rel := rlookup(Type, TypeInfoList/Naam);
				attribute<RekenType> RekenType_rel := TypeInfoList/RekenType_rel[TypeInfoList_rel];

				attribute<EUR_GJ>  K_GJ10 := ='union_data(RekenType, '+AsItemList('RekenTypes/'+RekenType/Naam+'/K_GJ[classifications/zichtjaar/J2010]')+')[RekenType_rel]';
 				attribute<EUR_GJ>  K_GJ20 := ='union_data(RekenType, '+AsItemList('RekenTypes/'+RekenType/Naam+'/K_GJ[classifications/zichtjaar/J2020]')+')[RekenType_rel]';
 				attribute<EUR_GJ>  K_GJ30 := ='union_data(RekenType, '+AsItemList('RekenTypes/'+RekenType/Naam+'/K_GJ[classifications/zichtjaar/J2030]')+')[RekenType_rel]';
 				attribute<EUR_GJ>  K_GJ40 := ='union_data(RekenType, '+AsItemList('RekenTypes/'+RekenType/Naam+'/K_GJ[classifications/zichtjaar/J2040]')+')[RekenType_rel]';
 				attribute<EUR_GJ>  K_GJ50 := ='union_data(RekenType, '+AsItemList('RekenTypes/'+RekenType/Naam+'/K_GJ[classifications/zichtjaar/J2050]')+')[RekenType_rel]';
			}
			unit<int32> TypeInfoList: nrofrows = 15
			{
				attribute<String> Naam: [
					"STEG", "Gasturbine", "Conventioneel",
					"Kolen", "KVSTEG",
					"Gasmotor", "WijkWKK",
					"Industrie", "Raffinaderij", "Kern", "Geothermie",
					"AVI", "BMC", "BioWKK", "Onbekend"
				];
				attribute<String> Label := Naam, DialogType = "LabelString";
				
				attribute<RekenType> RekenType_rel: [ 
					0, 0, 0, 
					1, 1, 
					2, 2,
					3, 3, 3, 3,
					4, 5, 6, null
				];
				container V := for_each_nedv(Naam, 'value('+String(ID(.))+', ..)', void, .);
			}
			container RekenTypes {
				container STEG
				{
					// obv (0.20/0.30) * M10[EUR/GJ];
					// obj M10 = 1000[MJ/GJ]*L10/ 31.65 [MJ/m3], 
					// obv L10 = gasprijs in [EUR/m3] commodity incl CO2, SDE-heffing, energiebel, excl BTW, geen distributie en transport.
					attribute<EUR_GJ> K_GJ  (classifications/zichtjaar) := (0.20/0.30) * prijzen/Gas_GJ;
				}
				container Kolen
				{
					// obv (0.20/0.30) * P10[EUR/GJ]
					// obv P10 = O10+0,105[ton/GJ]*B10 
					// obv O10= Kolenprijs = 2.20 [EUR/GJ] CONSTANT!
					// en B10[EUR/ton]=CO2-prijs die varieert met de tijd, 
					attribute<EUR_GJ> P   (classifications/zichtjaar) := prijzen/Kolen_GJ + prijzen/KolenCO2_GJ;
					attribute<EUR_GJ> K_GJ(classifications/zichtjaar) := (0.20/0.30) * P;
				}
				container Gasmotor
				{
					// M10/0.45 - Q10*1 
					// obv Q10[EUR/GJ] = elektriciteitsprijs en gros
					// (Gasprijs/(totaalrendement WKK (0.9)*Percentage warmteoutput WKK (0.75)) - (Elektriciteitsprijs (per GJ) * ((totaalrendement WKK (0.9)* Percentage elektriciteits-output WKK (0.25) )/(totaalrendement WKK (0.9)* Percentage warmte-output WKK (0.75) )))
					parameter<Float64> WarmteAandeel:= 0.75;
					parameter<Float64> Rendement    := 0.90;
					parameter<Float64>  ElektriciteitAandeel := 1.0 - WarmteAandeel;
					parameter<Float64> Aardgas_Factor := 1.0 / (Rendement * WarmteAandeel);
					parameter<Float64> Elektriciteit_Factor := - ElektriciteitAandeel / WarmteAandeel;
					attribute<EUR_GJ> K_GJ(classifications/zichtjaar) :=Aardgas_Factor * prijzen/Gas_GJ + Elektriciteit_Factor * prijzen/ElekEnGros_GJ; // en gros prijs als opbrengst, dit is excl distributiekosten
					// attribute<EUR_GJ> K_GJ(classifications/zichtjaar) := (prijzen/Gas_GJ/(0.9*0.75))-(prijzen/ElekEnGros_GJ*((0.9*0.25)/(0.9*0.75)))
				}
				container Industrie
				{
					// obv 0.0000025 [GJ/GJ] * R10[EUR/GJ]
					// obv R10 = S10[EUR/kWh] * 1000000[kJ/GJ] / 3600[sec/h]
					attribute<EUR_GJ> K_GJ(classifications/zichtjaar) := 0.0000025 [GJ / GJ] * prijzen/ElekKMG_GJ;
				}
				container AVI
				{
					// 0.18 * Q10[EUR/GJ] 
					// obv Q10 = 0.058[EUR/kWh] * 1000/3.6 [kWh/GJ] die varieert met de tijd.
					attribute<EUR_GJ> K_GJ(classifications/zichtjaar): Expr = "0.18 * prijzen/ElekEnGros_GJ"; // en gros prijs als kosten, is deze prijs nu excl of incl distributiekosten en heffingen?
				}
				container BMC
				{
					// obv (0.20 / 0.30) * N10[EUR/GJ] obv N10 = 122.4 / 16 CONSTANT ! 
					attribute<EUR_GJ> K_GJ(classifications/zichtjaar) := (0.20 / 0.30) * prijzen/BioMassa_GJ;
				}
				
				container BioWKK
				{
					// obv (0.20 / 0.30) * N10[EUR/GJ] obv N10 = 122.4 / 16 CONSTANT ! 
					parameter<Float64> WarmteAandeel:= 0.75;
					parameter<Float64> Rendement    := 0.80;
					parameter<Float64> ElektriciteitAandeel := 1.0 - WarmteAandeel;
					parameter<Float64> Warmte_Factor := 1.0 / (Rendement * WarmteAandeel);
					parameter<Float64> Elektriciteit_Factor := - ElektriciteitAandeel / WarmteAandeel;
					parameter<Float64> Aardgas_Factor := 0.0;
					attribute<EUR_GJ> K_GJ(classifications/zichtjaar) := Warmte_Factor *  prijzen/BioMassa_GJ + Elektriciteit_Factor * prijzen/ElekEnGros_GJ; // en gros prijs als opbrengst, dit is excl distributiekosten
				}
			}
		}
		container RestWarmte: Descr = "zie FO $3.3", FreeData = "False"
		{
			unit<uint32> bron: 
				StorageName     = "%sourceDataProjDir%/aanbod/puntbronnen/puntbronnen_20160407_in_gebruik_PBL.dbf",
				StorageReadOnly = "True",
				DialogType      = "Map",
				DialogData      = "P",
				Source          = "ECN, Marit van Hout, 5 juni 2015 per email
				                   aanlevering Y:\\data\\Vesta\\v2\\config\\MAIS\\aanlevering\\Steven_070416\\06-04 vergelijk_puntbronnen_180713_PBL-Lijst met centrales.xlsx
				                   bewerking: Y:\\data\\Vesta\\v2\\config\\MAIS\\bewerk\\puntbronnen_070416\\06-04 vergelijk_puntbronnen_180713_PBL-Lijst met centrales.xlsx tabblad GIS",
				SyncMode        = "None"
			{
				attribute<rdc_meter> P: StorageName = "%sourceDataProjDir%/aanbod/puntbronnen/puntbronnen_20160407_in_gebruik_PBL.shp", StorageReadOnly = "True";

				attribute<String>    Naam;
				
				attribute<String>  Type;
				attribute<MetaData/TypeInfoData> TypeInfo_rel     := rlookup(Type, MetaData/TypeInfoData/Type);
				attribute<MetaData/TypeInfoList> TypeInfoList_rel := lookup(TypeInfo_rel, MetaData/TypeInfoData/TypeInfoList_rel);
				
				attribute<Jaar>    ingebruik;
				attribute<Jaar>    uitgebruik;
				
				attribute<MW>      MWth_max;
				attribute<MW>      MWth_min := value(MetaData/TypeInfoData/MWth_min[TypeInfo_rel],MW);
				attribute<EUR_kW>  K_kW_min := value(MetaData/TypeInfoData/K_kW_min[TypeInfo_rel], EUR / kW);
				attribute<EUR_kW>  K_kW_max := value(MetaData/TypeInfoData/K_kW_max[TypeInfo_rel], EUR / kW);
				
 				attribute<_Yr>  Rj_Onderhoud := value(MetaData/TypeInfoData/JaarKosten[ TypeInfo_rel ], _Yr);
 				attribute<EUR_GJ>  K_GJ10 : = MetaData/TypeInfoData/K_GJ10[TypeInfo_rel];
 				attribute<EUR_GJ>  K_GJ20 : = MetaData/TypeInfoData/K_GJ20[TypeInfo_rel];
 				attribute<EUR_GJ>  K_GJ30 : = MetaData/TypeInfoData/K_GJ30[TypeInfo_rel];
 				attribute<EUR_GJ>  K_GJ40 : = MetaData/TypeInfoData/K_GJ40[TypeInfo_rel];
 				attribute<EUR_GJ>  K_GJ50 : = MetaData/TypeInfoData/K_GJ50[TypeInfo_rel];
				attribute<kg_GJ>   CO2;
				attribute<String>  Eigenaar;
				attribute<String>  Brandstof;

				// aspects				
				attribute<String>    Label:           Expr = "Naam",                                   DialogType = "LabelText";
				attribute<Float64>   SymbolWorldSize: Expr = "Sqrt(MWth_max * value(100000, 1 / MW))", DialogType = "SymbolWorldSize";
				parameter<int16>     SymbolIndex:     Expr = "int16(44)",                              DialogType = "SymbolIndex";
			}
			container BronSelectieTempl: IsTemplate = "True"
			{
				parameter<units/Jaar> Jaar;
				unit<uint32> WarmteBronnen := Bron;
				
				unit<UInt32> BronElementen := SubSet(WarmteBronnen/ingebruik <= Jaar)
				{
					attribute<String>     Label := WarmteBronnen/Label[nr_OrgEntity], DialogType = "LabelText";
					attribute<MetaData/TypeInfoList> TypeInfoList_rel := WarmteBronnen/TypeInfoList_rel[nr_OrgEntity];
		
					attribute<rdc_meter>  Point:        Expr = "WarmteBronnen/P         [nr_OrgEntity]";
					attribute<MW>         MWTh_min:     Expr = "WarmteBronnen/MWth_min  [nr_OrgEntity]";
					attribute<EUR_kW>     K_kW_min:     Expr = "WarmteBronnen/K_kW_min  [nr_OrgEntity]";
					attribute<EUR_kW>     K_kW_max:     Expr = "WarmteBronnen/K_kW_max  [nr_OrgEntity]";
					attribute<_Yr>        Rj_Onderhoud: Expr = "WarmteBronnen/Rj_Onderhoud[nr_OrgEntity]";
					attribute<MW>         MWth_max:     Expr = "WarmteBronnen/MWth_max  [nr_OrgEntity]";
					attribute<EUR_GJ>     K_GJ:         Expr = 
						"(Jaar < value(2020, Units/Jaar) ? WarmteBronnen/K_GJ10 :  "
						" Jaar < value(2030, Units/Jaar) ? WarmteBronnen/K_GJ20 :  "
						" Jaar < value(2040, Units/Jaar) ? WarmteBronnen/K_GJ30 :  "
						" Jaar < value(2050, Units/Jaar) ? WarmteBronnen/K_GJ40 :  "
						"               WarmteBronnen/K_GJ50 )  "
						"[nr_OrgEntity]";
					attribute<KG_GJ>      CO2_GJ := WarmteBronnen/CO2  [nr_OrgEntity];
				}
				unit<UInt32> Bronnen := Unique(BronElementen/Point), DialogType = "Map", DialogData = "Point"
				{
					attribute<String>     Label:        Expr = "AsItemList(BronElementen/Label, BronElementen_rel)", DialogType = "LabelText";
		
					attribute<.> BronElementen_rel(BronElementen): Expr = "rlookup(BronElementen/Point, Values)";
					attribute<MetaData/TypeInfoList> TypeInfoList_rel := modus_weighted(BronElementen/TypeInfoList_rel, BronElementen/MWth_max, BronElementen_rel);
					
					attribute<rdc_meter>  Point:        Expr = "Values";
					attribute<EUR_kW>     K_kW_min:     Expr = "sum(BronElementen/K_kW_min * BronElementen/MWth_max, BronElementen_rel) / sum(BronElementen/MWth_max, BronElementen_rel)";
					attribute<EUR_kW>     K_kW_max:     Expr = "sum(BronElementen/K_kW_max * BronElementen/MWth_max, BronElementen_rel) / sum(BronElementen/MWth_max, BronElementen_rel)";
					attribute<MW>         MWth_max:     Expr = "sum(BronElementen/MWth_max, BronElementen_rel)";
					attribute<MW>         MWth_min:     Expr = "sum(BronElementen/MWth_min, BronElementen_rel)";
					attribute<_Yr>        Rj_Onderhoud: Expr = "sum(BronElementen/Rj_Onderhoud * BronElementen/MWth_max, BronElementen_rel) / sum(BronElementen/MWth_max, BronElementen_rel)";
					attribute<EUR_GJ>     K_GJ:         Expr = "sum(BronElementen/K_GJ         * BronElementen/MWth_max, BronElementen_rel) / sum(BronElementen/MWth_max, BronElementen_rel)";
					attribute<KG_GJ >     CO2_GJ:       Expr = "sum(BronElementen/CO2_GJ       * BronElementen/MWth_max, BronElementen_rel) / sum(BronElementen/MWth_max, BronElementen_rel)";
					attribute<m>          cumulDist:    Expr = "const(0[m], .)";
					attribute<MW>         cumulP:       Expr = "const(0[MW], .)";
					attribute<Bronnen>    Bronnen_rel:  Expr = "ID(.)";
					
					attribute<EUR_kW>    Ki_kW_min := K_kW_min;
					attribute<EUR_kW>    Ki_kW_max := K_kW_max;
				}
			}
			container BronSelecties := for_each_ne(Classifications/zichtjaar/name, 'BronSelectieTempl('+String(Classifications/zichtjaar/Jaar)+'[units/jaar])');
		}
		template TypeInfoTempl
		{
			parameter<String> InstallatieTypeName;

			parameter<MetaData/TypeInfoData> TypeInfo_rel     := rlookup(InstallatieTypeName, MetaData/TypeInfoData/Type);
			parameter<MetaData/TypeInfoList> TypeInfoList_rel := MetaData/TypeInfoData/TypeInfoList_rel[TypeInfo_rel];
			parameter<MetaData/RekenType>    RekenType_rel    := MetaData/TypeInfoData/RekenType_rel   [TypeInfo_rel];
			parameter<MW>      MWth_min := value(MetaData/TypeInfoData/MWth_min[ TypeInfo_rel ], MW);
			parameter<EUR_kW > K_kW_min := value(MetaData/TypeInfoData/K_kW_min[ TypeInfo_rel ], EUR_kW);
			parameter<EUR_kW > K_kW_max := value(MetaData/TypeInfoData/K_kW_max[ TypeInfo_rel ], EUR_kW);
			parameter<_Yr>     Rj_Onderhoud := value(MetaData/TypeInfoData/JaarKosten[ TypeInfo_rel ], _Yr);
			parameter<EUR_GJ > K_GJ10   := value(MetaData/TypeInfoData/K_GJ10[ TypeInfo_rel ], EUR_GJ);
			parameter<EUR_GJ > K_GJ20   := value(MetaData/TypeInfoData/K_GJ20[ TypeInfo_rel ], EUR_GJ);
			parameter<EUR_GJ > K_GJ30   := value(MetaData/TypeInfoData/K_GJ30[ TypeInfo_rel ], EUR_GJ);
			parameter<EUR_GJ > K_GJ40   := value(MetaData/TypeInfoData/K_GJ40[ TypeInfo_rel ], EUR_GJ);
			parameter<EUR_GJ > K_GJ50   := value(MetaData/TypeInfoData/K_GJ50[ TypeInfo_rel ], EUR_GJ);
			
			container RekenType := ='MetaData/RekenTypes/'+MetaData/RekenType/Naam[RekenType_rel];
		}
		container GeoThermie: Descr = "zie FO $3.4"
		{
			container import
			{
				unit<UInt32> ContourSet: 
					StorageName      = "%sourceDataProjDir%/aanbod/GeoThermie/GT_GTB_zone_1_2.dbf",
					StorageReadOnly = "True",
					url             = "%sourceDataProjDir%/aanbod/GeoThermie/GT_GTB_zone_1_2.txt",
					DialogType      = "Map",
					DialogData      = "Polygon"
				{
					attribute<rdc_meter> Polygon(poly): StorageName = "%sourceDataProjDir%/aanbod/GeoThermie/GT_GTB_zone_1_2.shp", StorageReadOnly = "True";
	//				attribute<string>    GEOTHERMIE;
					attribute<Int32>    ID;
				}
			}
			container TypeInfo := TypeInfoTempl('Geothermie');
			
			unit<UInt32> ContourSet :=  Subset(import/ContourSet/ID != 0i), FreeData = "False"
			{
				attribute<rdc_meter>  Polygon(poly): Expr = "import/ContourSet/Polygon[nr_OrgEntity]";
				attribute<ContourSet> Grid     (Geography/rdc_100lu) := poly2grid(Polygon, Geography/rdc_100lu), FreeData = "True";
				unit<UInt32> PointSet: Expr = "SubSet(IsDefined(Grid))", FreeData = "False"
				{
					attribute<rdc_meter> Point: Expr = "rdc_100lu/point[nr_OrgEntity]";
				}
			}
		}
		container WKO: Descr = "zie FO $3.4"
		{
			unit<UInt32> ContourSet: 
				StorageName      = "%sourceDataProjDir%/Aanbod/WKO/OS_WARM_900_4560_GJ_hectare_jaar.dbf",
				StorageReadOnly = "True",
				url             = "%sourceDataProjDir%/Aanbod/WKO/OS_WARM_900_4560_GJ_hectare_jaar.txt",
				DialogType      = "Map",
				DialogData      = "Polygon"
			{
				attribute<rdc_meter> Polygon(poly): StorageName = "%sourceDataProjDir%/Aanbod/WKO/OS_WARM_900_4560_GJ_hectare_jaar.shp", StorageReadOnly = "True";
				attribute<string>    WKO;
				attribute<Bool>      Geschikt: Expr = "WKO == 'geschikt' || WKO == 'zeer geschikt'";
			}
			unit<uint32> GeschikteContour := subset(ContourSet/Geschikt), FreeData = "False"
			{
				container impl : FreeData = "True"
				{
					attribute<rdc_meter> geometry  (poly, GeschikteContour) := ContourSet/Polygon[nr_OrgEntity];
/*					
					attribute<rdc_dm> geometry_dm  (poly, GeschikteContour) := geometry[rdc_dm];
					attribute<rdc_dm> expanded_dm  (poly, GeschikteContour) := polygon_I4HV(geometry_dm, 1000[dm]);
					attribute<rdc_dm> contracted_dm(poly, GeschikteContour) := polygon_D4HV(expanded_dm, 1000[dm]);
*/
				}
//				attribute<rdc_meter> simplified_geometry(poly) := impl/contracted_dm[rdc_meter];
				attribute<ContourSet> Grid(Geography/rdc_100lu) := poly2grid(impl/geometry, Geography/rdc_100lu), FreeData = "False";
			}
		}
		container WijkWKK: Descr = "zie FO $3.4"
		{
			container TypeInfo := TypeInfoTempl('WijkWKK');
		}
		container BioWKK: Descr = "zie FO $3.4"
		{
			container TypeInfo := TypeInfoTempl('BioWKK');
		}
		container Gas: Descr = "Gasleidingennet beheersgebied Stedin"
		{
			unit<UInt32> Leiding: 
				StorageName      = "%sourceDataProjDir%/Aanbod/Gas/Leidingen.dbf",
				StorageReadOnly = "True",
				DialogType      = "Map",
				DialogData      = "Leidingen",
				url             = "%sourceDataProjDir%/Aanbod/Gas/Gasvervangingsdata _ Stedin Zakelijk.htm",
				Source          = "download van website op 29 maart 2017: https://www.stedin.net/zakelijk/open-data/gasvervangingsdata/#gasvervangingsbestand"
								  "conversie van Polygon ZM-bestand naar normaal Polygon-bestand in ArcGIS 10.3 mbv Copy features", 
				SyncMode        = "All"
				{
					attribute<rdc_meter> Leidingen(arc): StorageName = "%sourceDataProjDir%/Aanbod/Gas/Leidingen.shp";
					attribute<string> Status;
					attribute<Classifications/StatusKl> Status_rel := rlookup(Status, Classifications/StatusKl/label);
				}
		}
	}

	container VraagReductiePrijzen: Descr = "zie FO $3.6"
	{
	}
	container HuidigeNetten: Descr = "zie FO $3.8"
	{
	}
	container DraagvlagIndicatoren: Descr = "zie FO $4.1"
	{
		
	}
	
	container FysiekeConstanten
	{
		parameter<GJ_m3> GasEnergie := 31.65[ MJ / m3 ] / 1000[ MJ  / GJ ]; // ondergrens
//		parameter<GJ_m3> GasEnergie := 35.17[ MJ / m3 ] / 1000[ MJ  / GJ ]; // bovengrens, rekening houdend met eficientere verbranding (o.a. condensatiewarmte van waterdamp)
	}
	
	
	#include<grondsoort.dms>
	container klimaat
	{
		container Jaar_1976_2005
		{
			attribute<float32> TovDeBilt (Geography/rdc_100lu): StorageName = "%sourceDataProjDir%/hulpbestanden/klimaat/1976-2005/klimaat_1976_2005.tif";
		}
		container de_bilt
		{
//			attribute<Ratio> een (Geography/rdc_100lu): StorageName = "%sourceDataProjDir%/hulpbestanden/klimaat/de_bilt/een.tif";
			attribute<float32> een (Geography/rdc_100lu): Expr = "const(1.0, Geography/rdc_100lu, float32)";
		}
		container scenario_knmi_w: 
			Expr = "for_each_ndva(  Classifications/zichtjaar/name "
				   "              , Geography/rdc_100lu"
				   "              , float32"
				   "              , '%sourceDataProjDir%/hulpbestanden/klimaat/scenario_knmi_w/" // dir name
				                  "w_' + Classifications/zichtjaar/label + '.tif')";             // file name
		container scenario_knmi_g_plus: 
			Expr = "for_each_ndva(  Classifications/zichtjaar/name "
				   "              , Geography/rdc_100lu"
				   "              , float32"
				   "              , '%sourceDataProjDir%/hulpbestanden/klimaat/scenario_knmi_g_plus/" // dir name
				                  "g_pl_' + Classifications/zichtjaar/label + '.tif')";               // file name
	}
	container Leercurves
	{
		// Optie 1a, 2: SchilVerbetering
			// LabelCategorieen: {C-up, B-Dn, Verder dan B}
			// jaren: 2010..2050
			// {pessimistisch, Optimistisch}
		unit<UInt32> TabelJaar: nrofrows = 5 
		{
			attribute<units/Jaar> Jaar: [ 2010, 2020, 2030, 2040, 2050 ];
		}
		container Schil: Source = "email?date=22-01-2013&sender=Ruud.vandenWijngaart@pbl.nl"
		{
			container Pessimistisch
			{
				attribute<Percent> Cmin  (TabelJaar): [ 100, 101, 102, 109, 115 ], Source = "email?date=22-01-2013&sender=Ruud.vandenWijngaart@pbl.nl#optie 1a.xlsx#Pessimistisch";
				attribute<Percent> Bpls  (TabelJaar): [ 100, 101, 105, 110, 116 ], Source = "email?date=22-01-2013&sender=Ruud.vandenWijngaart@pbl.nl#optie 1a.xlsx#Pessimistisch";
				attribute<Percent> Verder(TabelJaar): [ 100, 80, 70, 64, 59 ], Source = "Rapport Vesta 2.0 Uitbreidingen en dataverificaties Delft augustus 2013 CE-Delft tabel 29"; // Huidige kosten E 80.000,- per woning
			}
			container Optimistisch
			{
				attribute<Percent> Cmin  (TabelJaar): [ 100, 85, 73, 63, 54 ], Source = "email?date=22-01-2013&sender=Ruud.vandenWijngaart@pbl.nl#optie 1a.xlsx#Optimistisch";
				attribute<Percent> Bpls  (TabelJaar): [ 100, 85, 72, 61, 52 ], Source = "email?date=22-01-2013&sender=Ruud.vandenWijngaart@pbl.nl#optie 1a.xlsx#Optimistisch";
				attribute<Percent> Verder(TabelJaar): [ 100, 72, 58, 49, 43 ], Source = "Rapport Vesta 2.0 Uitbreidingen en dataverificaties Delft augustus 2013 CE-Delft tabel 29"; // Huidige kosten E 60.000,- per woning
			}
		}
		// Optie 1b en 5: gebouwmaatregelen
			//	{HR-ketel, HRe-ketel, Zonneboiler, eWP (l/w), eWP (w/w), Zon-PV, LTV, EWV}  kosten in woningtabel
			// jaren: 2010..2050
			// {pessimistisch, Optimistisch}
		container Installatie
		{
			container Pessimistisch
			{
				//attribute<Percent> HR (TabelJaar): [ 100, 93, 88, 85, 83 ]; // dit wordt niet gebruikt!
				attribute<Percent> mWKK (TabelJaar): [ 100, 80, 70, 64, 59 ], Source = "#optie 1b.xlsx#Pessimistisch"; // in de excel tabel is dit de rij HRe
				attribute<Percent> ZonB (TabelJaar): [ 100, 91, 85, 80, 77 ], Source = "rapport CE-Delft Ruud 180413#optie 1b.xlsx#Pessimistisch";
				attribute<Percent> ZonPV(TabelJaar): [ 100, 52, 37, 32, 27 ], Source = "rapport CE-Delft Ruud 180413#optie 1b.xlsx#Pessimistisch";
				attribute<Percent> eWPlw(TabelJaar): [ 100, 80, 70, 64, 59 ], Source = "rapport CE-Delft Ruud 180413#optie 1b.xlsx#Pessimistisch"; 
				attribute<Percent> eWPww(TabelJaar): [ 100, 88, 80, 74, 70 ], Source = "rapport CE-Delft Ruud 180413#optie 1b.xlsx#Pessimistisch"; // deze wordt niet gebruikt!
				attribute<Percent> hWP  (TabelJaar) := eWPlw;
				attribute<Percent> EWV  (TabelJaar): [ 100, 99, 100, 103, 107 ], Source = "rapport CE-Delft Ruud 180413#optie 5.xlsx#Pessimistisch";
			}
			container Optimistisch
			{
				//attribute<Percent> HR   (TabelJaar): [ 100, 88, 79, 73, 68 ]; // dit wordt niet gebruikt!
				attribute<Percent> mWKK (TabelJaar): [ 100, 72, 58, 49, 43 ], Source = "rapport CE-Delft Ruud 180413#optie 1b.xlsx#Optimistisch"; // in de excel tabel is dit de rij HRe
				attribute<Percent> ZonB (TabelJaar): [ 100, 85, 75, 67, 62 ], Source = "rapport CE-Delft Ruud 180413#optie 1b.xlsx#Optimistisch";
				attribute<Percent> ZonPV(TabelJaar): [ 100, 40, 28, 25, 21 ], Source = "rapport CE-Delft Ruud 180413#optie 1b.xlsx#Optimistisch";
				attribute<Percent> eWPlw(TabelJaar): [ 100, 72, 58, 49, 43 ], Source = "rapport CE-Delft Ruud 180413#optie 1b.xlsx#Optimistisch";
				attribute<Percent> eWPww(TabelJaar): [ 100, 81, 69, 60, 54 ], Source = "rapport CE-Delft Ruud 180413#optie 1b.xlsx#Optimistisch"; // deze wordt niet gebruikt!
				attribute<Percent> hWP  (TabelJaar)  := eWPlw;
				attribute<Percent> EWV  (TabelJaar): [ 100, 87, 77, 70, 64 ], Source = "rapport CE-Delft Ruud 180413#optie 5.xlsx#Optimistisch";
			}
		}
	}
}
