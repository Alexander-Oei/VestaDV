//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                                  (C) VESTA 2018                                      //
//        Het doel van het model is om beleidsinformatie te genereren met betrekking    //
//        tot de energievoorziening van de gebouwde omgeving.                           //
//                                                                                      //
//                         Planbureau voor de Leefomgeving                              //
//                                    JUNI 2018                                         //
//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//     * In het geval van externe publicatie van onderzoeksresultaten waarbij gebruik   //
//       is gemaakt van de verstrekte gegevens dient de gebruiker deze resultaten aan   //
//       het PBL mede te delen middels de toezending van 2 exemplaren van de betref-    //
//       fende publicatie aan het hoofd van het team ROL van het PBL.                   //
//                                                                                      //
//     * In elke publicatie waarin de gegevens van het PBL in enige vorm zijn opgenomen,//
//       zal de gebruiker de vermelding opnemen: "Bron: Planbureau voor de Leefomgeving"//
//       Indien het een bewerking van de gegevens betreft, dient te worden opgenomen:   //
//       "Gebaseerd op Planbureau voor de Leefomgeving gegevens".                       //
//                                                                                      //
//     * Het PBL is onder geen enkele voorwaarde aansprakelijk tegenover gebruiker voor //
//       enige schade, mede begrepen winstderving, verlies van gegevens, schade         //
//       tengevolge van fouten of onjuistheden in gegevens, schade tengevolge van fouten//
//       of onvolkomenheden in de documentatie of andere incidentele of gevolgschade    //
//       welke voortvloeit uit of in verband staat met gebruik of het niet kunnen ge-   //
//       bruiken van gegevens.                                                          //
//                                                                                      //
//     * Het PBL staat niet in voor de juistheid van de resultaten die worden verkregen //
//       door combinatie van diverse, op zichzelf staande PBL-gegevens onderling,       //
//       of met die uit andere bronnen.                                                 //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//       In deze configuratiefile wordt de Geography gedefinieerd:                      //
//       - geografische basis eenheden en grid domeinen voor brondata                   //
//       - regioindelingen (polygonen)                                                  //
//                                                                                      //
// ///////////////////////////////////////////////////////////////////////////////////////

container Geography: Using = "Units", IsHidden = "True"
{
	unit<dpoint> rdc_meter_base : DialogData = "ngr_layer"
	,	Format   = "EPSG:28992"
	,	IsHidden = "True";
	unit<dpoint> rdc_meter     := range(rdc_meter_base, point(300000.0, 0.0), point(625000.0, 280000.0));
	unit<fpoint> rdc_meter_f32 := gridset(rdc_meter, point(1.0, 1.0), point(0.0, 0.0), fpoint)   , IsHidden = "True";
	unit<ipoint> rdc_mm        := gridset(rdc_meter, point(0.001, 0.001), point(0.0,0.0), ipoint), IsHidden = "True";

	#include <ngr_layer.dms>

	container rdc_grids: IsHidden = "True"
	{
		unit<wpoint> m100 :=
			range(
				  gridset(rdc_meter, point( - 100.0, 100.0), point(625000.0, 10000.0), wpoint)
				, point(0w, 0w)
				, point(3250w, 2700w)
			)
			,	Descr = "rdCoords/100m van NW naar SE (2700 cols)"
		{
			attribute<rdc_meter>             point                 := id(.)[rdc_meter] + point(-50.0, 50.0, rdc_meter);
			attribute<RegioIndelingen/buurt> buurt_rel_org         := point_in_polygon(point, RegioIndelingen/Buurt/Geometry);
			attribute<bool>                  buurt_rel_org_defined := IsDefined(buurt_rel_org);

			unit<uint32> NearBuurt := Subset(potential(float32(buurt_rel_org_defined), DistMatrices/impl/pot1000m/potrange/Cilindrical) >= 0.000001f && !buurt_rel_org_defined);

			container connect := connect_info( RegioIndelingen/Buurt/Geometry, point[NearBuurt/nr_OrgEntity]);

			attribute<RegioIndelingen/blok500m> blok500m_rel := RegioIndelingen/blok500m/BaseGrid[ ID(.)[m500] ];
			attribute<RegioIndelingen/blok1km>  blok1km_rel  := RegioIndelingen/blok1km/BaseGrid[ ID(.)[km1] ];
			attribute<RegioIndelingen/blok2km>  blok2km_rel  := RegioIndelingen/blok2km/BaseGrid[ ID(.)[km2 ] ];

			attribute<RegioIndelingen/Buurt> Buurt_rel       := MakeDefined(buurt_rel_org, connect/ArcID[invert(NearBuurt/nr_OrgEntity)]), FreeData = "False";
			attribute<Invoer/SpecifiekeInstellingen/PlanRegio> PlanRegio_rel := Buurt_rel;
		}

		unit<wpoint> m500 := gridset(rdc_grids/m100, point( 5w, 5w, m100), point(0w,0w, m100), wpoint)
		{
			attribute<RegioIndelingen/buurt> buurt_rel := poly2grid(RegioIndelingen/buurt/geometry,   .);
			attribute<rdc_meter>             point     := id(.)[rdc_meter] + point(-250.0, 250.0, rdc_meter);
			attribute<bool>                  IsNL      := IsDefined(buurt_rel);
		}

		unit<wpoint> km1 := gridset(rdc_grids/m100, point(10w, 10w, m100), point(0w,0w, m100), wpoint)
		{
			attribute<RegioIndelingen/buurt> buurt_rel := poly2grid(RegioIndelingen/buurt/geometry,   .);
			attribute<rdc_meter>             point     := id(.)[rdc_meter] + point(-500.0, 500.0, rdc_meter);
			attribute<bool>                  IsNL      := IsDefined(buurt_rel);
		}

		unit<wpoint> km2 := gridset(rdc_grids/m100, point(20w, 20w, m100), point(0w, 0w, m100), wpoint)
		{
			attribute<RegioIndelingen/buurt> buurt_rel := poly2grid(RegioIndelingen/buurt/geometry,   .);
			attribute<rdc_meter>             point     := id(.)[rdc_meter] + point(-1000.0, 1000.0, rdc_meter);
			attribute<bool>                  IsNL      := IsDefined(buurt_rel);
		}
	}

	container RegioIndelingen // Let op, voor planregio's moeten dit uint32 domeinen zijn.
	{
		unit<uint32> NL: nrofrows = 1
		{
			attribute<string> Name      := const('NL', .);
			parameter<string> RegioNaam := 'NL';
		}

		unit<uint32> Provincie := unique(buurt/PROVNR)
		{
			attribute<string> label     := rjoin(values, buurt/PROVNR, buurt/PROVNM);
			attribute<string> Name      := replace_value(Replace(Label, '-', ''), '', 'Elders');
			parameter<string> RegioNaam := 'Provincie';
		}

		unit<uint32> Gemeente := unique(buurt/GEMNR2017)
		{
			attribute<string> name      := 'G'+string(Values);
			attribute<string> label     := rjoin(values, buurt/GEMNR2017, buurt/GEMNM2017);
			parameter<string> RegioNaam := 'Gemeente';
		}

		#include<buurt.dms>

		unit<uint32> PC6:
			StorageName     = "%SourceDataProjDir%/hulpbestanden/pc06_vlakken_simplify/PC6gebieden_2006_KAD_simplify.dbf",
			DialogData      = "geometry",
			DialogType      = "Map",
			StorageReadOnly = "True"
		{
			attribute<rdc_meter> geometry (poly):
				label           = "Postcode 6 gebieden Nederland 2006 obv percelen",
				StorageName     = "%SourceDataProjDir%/hulpbestanden/pc06_vlakken_simplify/PC6gebieden_2006_KAD_simplify.shp",
				StorageReadOnly = "True";

			attribute<string> postcode;
			attribute<string> code := postcode;
		}

		unit<uint32> blok2km := Subset(const(true,rdc_grids/km2)), DialogType = "Map", DialogData = "BaseGrid" // guarantee that all objects are in a plan-region
		{
			attribute<.>         BaseGrid (rdc_grids/km2) := invert(nr_OrgEntity);

			attribute<rdc_meter> point         := rdc_grids/km2/point[nr_OrgEntity];
			attribute<km2>       AreaInKm2     := const(4.0[km2], .);

			// Report Regio relations
			attribute<NL>        NL_rel        := const(0[NL],.);
			attribute<Gemeente>  Gemeente_rel  := RegioIndelingen/buurt/Gemeente_rel [rdc_grids/km2/buurt_rel[nr_OrgEntity]];
			attribute<Provincie> Provincie_rel := RegioIndelingen/buurt/Provincie_rel[rdc_grids/km2/buurt_rel[nr_OrgEntity]];

			attribute<yr_uint16> BioWKK_Start  := const(2020[yr_uint16], .); // jaar waar vanaf BMC in een planregio is toegestaan. gebruik 9999 of ieder getal hoger dan laatste zichtjaar om geheel te verbieden.
		}

		unit<uint32> blok1km := Subset(const(true,rdc_grids/km1)), DialogType = "Map", DialogData = "BaseGrid" // guarantee that all objects are in a plan-region
		{
			attribute<.>         BaseGrid (rdc_grids/km1) := invert(nr_OrgEntity);

			attribute<rdc_meter> point         := rdc_grids/km1/point[nr_OrgEntity];
			attribute<km2>       AreaInKm2     := const(1.0[km2], .);

			// Report Regio relations
			attribute<NL>        NL_rel        := const(0[NL],.);
			attribute<Gemeente>  Gemeente_rel  := RegioIndelingen/buurt/Gemeente_rel [rdc_grids/km1/buurt_rel[nr_OrgEntity]];
			attribute<Provincie> Provincie_rel := RegioIndelingen/buurt/Provincie_rel[rdc_grids/km1/buurt_rel[nr_OrgEntity]];

			attribute<yr_uint16> BioWKK_Start  := const(2020[yr_uint16], .); // jaar waar vanaf BMC in een planregio is toegestaan. gebruik 9999 of ieder getal hoger dan laatste zichtjaar om geheel te verbieden.
		}

		unit<uint32> blok500m := Subset(const(true, rdc_grids/m500)), DialogType = "Map", DialogData = "BaseGrid" // guarantee that all objects are in a plan-region
		{
			attribute<.>         BaseGrid (rdc_grids/m500) := invert(nr_OrgEntity);

			attribute<rdc_meter> point         := rdc_grids/m500/point[nr_OrgEntity];
			attribute<km2>       AreaInKm2     := const(0.25[km2], .);

			// Report Regio relations
			attribute<NL>        NL_rel        := const(0[NL],.);
			attribute<Gemeente>  Gemeente_rel  := RegioIndelingen/Buurt/Gemeente_rel [rdc_grids/m500/Buurt_rel[nr_OrgEntity]];
			attribute<Provincie> Provincie_rel := RegioIndelingen/buurt/Provincie_rel[rdc_grids/m500/Buurt_rel[nr_OrgEntity]];

			attribute<yr_uint16> BioWKK_Start  := const(2020[yr_uint16], .); // jaar waar vanaf BMC in een planregio is toegestaan. gebruik 9999 of ieder getal hoger dan laatste zichtjaar om geheel te verbieden.
		}
	}

	// for potential calculations
	container DistMatrices : IsHidden = "True"
	{
		unit<uint32> Dist2Range;

		template potrange
		{
			// Begin Case Parameters
			parameter<m> gridsize;
			parameter<m> radius_max;
			// End Case Parameters

			parameter<float64> radius   := radius_max / gridsize;
			unit<spoint>       tmpRange := range(spoint, point(-int16(radius), -int16(radius)), point(int16(radius) + 1s, int16(radius) + 1s));
			unit<spoint>       potRange := tmpRange
			{
				attribute<Dist2Range> distMatr      := dist2(point(0s, 0s, tmpRange), Dist2Range);
				attribute<float64>    DecayedWeight := iif(distMatr <= uint32(radius * radius), 1.0 / (float64(distMatr) + 0.08), 0.0);

				attribute<float32>    Reciprocal    := float32(scalesum(Sqrt(DecayedWeight), 1.0 ));
				attribute<float32>    Cilindrical   := float32(scalesum(float64(distMatr <= uint32(radius * radius)), 1.0 ));
			}
		}

		Container impl
		{
			container pot1000m  := potrange(100[m],  1000[m]);
			container pot5000m  := potrange(100[m],  5000[m]);
			container pot10000m := potrange(100[m], 10000[m]);
		}
	}
}