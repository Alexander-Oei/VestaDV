//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2019 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//       In deze configuratiefile worden de brondata geconfigureerd.                    //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

container RuimtelijkeData: Using = "Units;Geography;model", FreeData = "False"
{
	#include <BAG.dms>
	#include <Nieuwbouw.dms>
	#include <Sloop.dms>
	#include <GlasTuinBouw.dms>
	#include <Bebouwing.dms>
	container EnergieLabel
	{
		parameter<string> SQoute := '\''', IsHidden = "True";
		// Voor koppeling aan de BAG
		unit<uint32> nummeraanduiding := RuimtelijkeData/BAG/import/nummeraanduiding;
		unit<uint32> vbo              := RuimtelijkeData/BAG/import/vbo;

		unit<uint32> src
		:	StorageName     = "%SourceDataProjDir%/vraag/wonen/20190101_inputlabels_rvo.csv"
		,	StorageType     = "gdal.vect"
		,	StorageReadOnly = "True"
		,	url             = "%SourceDataProjDir%/vraag/wonen/20190101_inputlabels_rvo_FvdM.txt"
		{
			attribute<string> HLT_unquoted := trim(replace(HLT,SQoute,''));
			attribute<string> adres_key    :=
				replace(Postcode,SQoute,'') + '_' + replace(HNR,SQoute,'') + 
				(strlen(HLT_unquoted) > 0 
					? '_' + HLT_unquoted
					: ''
				);
			//order of replace matters!
			attribute<Classifications/energielabel> energielabel_rel := rlookup(replace(replace(replace(replace(replace(label,SQoute,''),'A++++','eWP'),'A+++','eWP'),'A++','eWP'),'A','A+'), Classifications/energielabel/Label);
			attribute<uint32>                       datum_numeriek   := uint32(replace(DATUM,SQoute,''));
			attribute<voorkomend>                   voorkomend_rel   := rlookup(label, voorkomend/Values);
		}

		unit<uint32> data := unique(src/adres_key)
		{
			attribute<string>                       nummeraanduiding_id := nummeraanduiding/identificatie[rlookup(values, nummeraanduiding/adres_key)];
			attribute<string>                       vbo_id              := rjoin(nummeraanduiding_id, vbo/nummeraanduiding_id, vbo/identificatie), FreeData = "False";
			attribute<uint32>                       datum_laatste       := max(src/datum_numeriek, rlookup(src/adres_key, values));
			attribute<Classifications/energielabel> energielabel_rel    := rjoin(values + '_' + string(datum_laatste), src/adres_key + '_' + string(src/datum_numeriek), src/energielabel_rel), FreeData = "False";
		}

		unit<uint32> voorkomend := unique(src/label), isHidden = "True"
		{
			attribute<uint32> aantal := pcount(src/voorkomend_rel);
		}

		unit<uint32> opgelegd
		:	StorageName     = "%SourceDataProjDir%/vraag/wonen/sprongtabel.csv"
		,	StorageType     = "gdal.vect"
		,	StorageReadOnly = "True"
		{
			container controle
			{
				attribute<bool> heeft_voorkomende_labelsprong_2020  (opgelegd) := strlen(J2020) == 0 || isDefined(rlookup(J2020, Classifications/LabelSprong/code));
				attribute<bool> heeft_voorkomende_labelsprong_2030  (opgelegd) := strlen(J2030) == 0 || isDefined(rlookup(J2030, Classifications/LabelSprong/code));
				attribute<bool> heeft_voorkomende_labelsprong_2040  (opgelegd) := strlen(J2040) == 0 || isDefined(rlookup(J2040, Classifications/LabelSprong/code));
				attribute<bool> heeft_voorkomende_labelsprong_2050  (opgelegd) := strlen(J2050) == 0 || isDefined(rlookup(J2050, Classifications/LabelSprong/code));

				attribute<bool> heeft_voorkomende_vbo_identificatie (opgelegd) := strlen(J2050) == 0 || isDefined(rlookup(identificatie, Invoer/RuimtelijkeData/BAG/vbo_woonfunctie/identificatie));

				parameter<bool> alle_labelsprongen_komen_voor      := all(heeft_voorkomende_labelsprong_2020) && all(heeft_voorkomende_labelsprong_2030) && all(heeft_voorkomende_labelsprong_2040) && all(heeft_voorkomende_labelsprong_2050);
				parameter<bool> alle_vbo_identificaties_komen_voor := all(heeft_voorkomende_vbo_identificatie);
			}
			attribute<string> StartJaar := const('', .);
		}
	}

	#include <WarmteBronnen.dms>

	container infra {
		unit<uint32> netbeheerders_input
		:	StorageName     = "%SourceDataProjDir%/infra/test_netbeheerders_input.csv"
		,	StorageType     = "gdal.vect"
		,	StorageReadOnly = "True"
		{}
		
		container kentallen
		{
			parameter<Eur_m>       verv_LD_net           := 270[Eur_m];
			parameter<Eur_Aansl>   verw_g_aansl_laagb    := 550[Eur_Aansl];
			parameter<Eur_Aansl>   verw_g_aansl_hoogb    := 182[Eur_Aansl];
			parameter<Eur_Aansl>   verw_g_aansl_util     := 1932[Eur_Aansl];
			parameter<Eur_m>       verw_LD_net           := 100[Eur_m];
			parameter<Eur_Aansl>   verzw_e_aansl         := 227[Eur_Aansl];
			parameter<Eur_MSR>     nieuwe_MSR            := 70000[Eur_MSR];
			parameter<Eur_m>       verzw_LS              := 110[Eur_m];
			parameter<kW>          capaciteit_nieuwe_MSR := 630[kw];
		}
		
		unit<uint32> per_buurt := Geography/RegioIndelingen/buurt
		, DialogType = "map"
		, DialogData = "geometry"
		{
			attribute<Geography/rdc_meter> geometry (poly)             := /Geography/RegioIndelingen/buurt/Geometry;
			attribute<string>              BU_CODE                     := /Geography/RegioIndelingen/buurt/BU_CODE;
			attribute<nrAansl>             aant_g_aansl                := rjoin(BU_CODE, netbeheerders_input/BU_CODE, netbeheerders_input/aantal_g_aansl)[nrAansl];
			attribute<nrUtil>              aant_util                   := rjoin(BU_CODE, netbeheerders_input/BU_CODE, netbeheerders_input/aant_util)[nrUtil];
			attribute<Ratio>               fractie_laagb_gasloos       := rjoin(BU_CODE, netbeheerders_input/BU_CODE, netbeheerders_input/fractie_laagb_gasloos)[Ratio];
			attribute<Ratio>               fractie_hoogb_gasloos       := rjoin(BU_CODE, netbeheerders_input/BU_CODE, netbeheerders_input/fractie_hoogb_gasloos)[Ratio];
			attribute<nrUtil>              aantal_util_gasloos         := rjoin(BU_CODE, netbeheerders_input/BU_CODE, netbeheerders_input/aantal_util_gasloos)[nrUtil];
			attribute<m>                   lengte_grogel_LD_net        := rjoin(BU_CODE, netbeheerders_input/BU_CODE, netbeheerders_input/lengte_grogel_LD_net)[m];
			attribute<m>                   lengte_LD_net               := rjoin(BU_CODE, netbeheerders_input/BU_CODE, netbeheerders_input/lengte_LD_net)[m];
			attribute<nrWoningen>          aant_won_elec               := rjoin(BU_CODE, netbeheerders_input/BU_CODE, netbeheerders_input/aant_won_elec)[nrWoningen];
			attribute<Ratio>               fractie_3x25_aansl          := rjoin(BU_CODE, netbeheerders_input/BU_CODE, netbeheerders_input/fractie_3x25_aansl)[Ratio];
			attribute<kw>                  capaciteit_buurt            := rjoin(BU_CODE, netbeheerders_input/BU_CODE, netbeheerders_input/capaciteit_buurt)[kw];
			attribute<nrVBO>               aant_objecten_per_oplossing := rjoin(BU_CODE, netbeheerders_input/BU_CODE, netbeheerders_input/aant_objecten_per_oplossing)[nrVBO];
			attribute<m>                   lengte_LS_buurt             := rjoin(BU_CODE, netbeheerders_input/BU_CODE, netbeheerders_input/lengte_LS_buurt)[m];
			attribute<kw>                  vermogensvraag_huidig       := rjoin(BU_CODE, netbeheerders_input/BU_CODE, netbeheerders_input/vermogensvraag_huidig)[kw];
		}
		
	}
	
	// KNMI klimaatscenario voor daling functionele warmtevraag in de toekomst door hogere gemiddelde buitentemperatuur
	// Beschikbare varianten:
	// "%sourceDataProjDir%/hulpbestanden/klimaat/GL/GL_' + Classifications/zichtjaar/label + '.tif"     ( + 1,0 graden celcius wereldwijd in 2050 )
	// "%sourceDataProjDir%/hulpbestanden/klimaat/GH/GH_' + Classifications/zichtjaar/label + '.tif"     ( + 1,4 graden celcius wereldwijd in 2050 )
	// "%sourceDataProjDir%/hulpbestanden/klimaat/WL/WL_' + Classifications/zichtjaar/label + '.tif"     ( + 2,0 graden celcius wereldwijd in 2050 )
	// "%sourceDataProjDir%/hulpbestanden/klimaat/WH/WH_' + Classifications/zichtjaar/label + '.tif"     ( + 2,3 graden celcius wereldwijd in 2050 )
	
	container Klimaat :=
			for_each_ndva(
			 Classifications/zichtjaar/name
			,Geography/rdc_grids/m100
			,float32
			,'%sourceDataProjDir%/hulpbestanden/klimaat/GH/GH_' + Classifications/zichtjaar/label + '.tif'
			)
			, descr = "Klimaat_scenario_knmi"
			, url   = "%sourceDataProjDir%/hulpbestanden/klimaat/Werkwijze_aanmaak_correctiekaarten_klimaat_obv_KNMI2014_scenarios_tbv_Vesta33.pdf";

	// inlezen kerncijfers wijken en buurten - meerdere jaren om missende waarden te ondervangen
	container CBSWijkEnBuurt: url="%sourceDataProjDir%/CBS/20190212_update_kerncijfers_wijken_buurten_FvdM.txt"
	{
		unit<uint32> j2013:
			StorageName     = "%SourceDataProjDir%/CBS/20180802_kwb-2013.csv",
			StorageType     = "gdal.vect",
			StorageReadOnly = "True";

		unit<uint32> j2014:
			StorageName     = "%SourceDataProjDir%/CBS/20180802_kwb-2014.csv",
			StorageType     = "gdal.vect",
			StorageReadOnly = "True";

		unit<uint32> j2015:
			StorageName     = "%SourceDataProjDir%/CBS/20180802_kwb-2015.csv",
			StorageType     = "gdal.vect",
			StorageReadOnly = "True";
			
		unit<uint32> j2016:
			StorageName     = "%SourceDataProjDir%/CBS/20190118_kwb-2016.csv",
			StorageType     = "gdal.vect",
			StorageReadOnly = "True";

		unit<uint32> j2017:
			StorageName     = "%SourceDataProjDir%/CBS/20190118_kwb-2017.csv",
			StorageType     = "gdal.vect",
			StorageReadOnly = "True";

		unit<uint32> j2018:
			StorageName     = "%SourceDataProjDir%/CBS/20190118_kwb-2018.csv",
			StorageType     = "gdal.vect",
			StorageReadOnly = "True";
	}
}
