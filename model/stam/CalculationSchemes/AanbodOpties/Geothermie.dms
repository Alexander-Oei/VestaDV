//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2019 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

template GeoThermie
{
	// begin case parameters
	container BeginStatus;
	container AanbodKentallen;
	container EnergiePrijzen;
	parameter<units/yr_uint16> Jaar;
	// end case parameters

	unit<uint32> PlanRegio := Invoer/SpecifiekeInstellingen/PlanRegio;
	container TypeInfo     := Kengetallen/WarmteBronnen/GeoThermie/TypeInfo;

	container KostenBaten := CalculationSchemes/KostenBaten(BeginStatus/Bebouwing, invoer/Energieprijzen);

	parameter<kg_GJ> CO2_GJb := 0[kg_GJ];
	parameter<g_GJ>  NOx_GJb := 0[g_GJ];
	parameter<g_GJ>  SO2_GJb := 0[g_GJ];
	parameter<g_GJ>  VOS_GJb := 0[g_GJ];
	parameter<g_GJ>  TS_GJb  := 0[g_GJ];

	unit<uint32> bron := PlanRegio
	{
		unit<uint32>    ContourSet := RuimtelijkeData/WarmteBronnen/GeoThermie/ContourSet;
		unit<uint32>    PointSet   := ContourSet/PointSet;
		attribute<bool> InContour  := IsDefined(ContourSet/grid[point[Geography/rdc_grids/m100]]), FreeData = "False"; // > value(0, ContourSet);

		unit<uint32> OutsideContourSet := Subset(not(InContour))
		{
			attribute<rdc_meter> Usage_point := bron/point[nr_OrgEntity];
			attribute<PointSet>  Connections := connect(PointSet/Point, Usage_Point);
			attribute<rdc_meter> Drill_point := PointSet/Point[Connections];
			attribute<m>         Dist        := value(dist(Usage_Point, Drill_Point), m);
		}
		attribute<m> Dist := InContour ? 0[m] : OutsideContourSet/Dist[invert(OutsideContourSet/nr_OrgEntity)];
	}

	container Kosten
	{
		attribute<Eur> Ki_ow (PlanRegio) := 
			(TypeInfo/Ki_kW_min * Schuiven/KostenMin + TypeInfo/Ki_kW_max * Schuiven/KostenMax)
			* max_elem(KostenBaten/CapaciteitsVraagD, TypeInfo/MWth_min) * 1000[kW / MW]
			* LeerCurves/Curve2
		;
		attribute<Eur_yr> Kji_ow (PlanRegio) := Ki_ow * NCW/ow28/AnnualisationFactor ;

		attribute<Eur> Ki_pt  (PlanRegio) := KostenBaten/KostenD/PrimairNet/LeidingKosten/aK_m * bron/dist;
		attribute<Eur_yr> Kji_pt (PlanRegio) := Ki_pt * NCW/pt28/AnnualisationFactor ;
		attribute<Eur> Ki_Totaal   (PlanRegio) := Ki_ow + ki_pt;

		parameter<Eur_GJ> K_GJ :=
			(Jaar < 2020[units/yr_uint16] 
				? TypeInfo/K_GJ10 
				: Jaar < 2030[units/yr_uint16] 
					? TypeInfo/K_GJ20 
					: Jaar < 2040[units/yr_uint16] 
						? TypeInfo/K_GJ30 
						: Jaar < 2050[units/yr_uint16] 
							? TypeInfo/K_GJ40 
							: TypeInfo/K_GJ50
			);
		parameter<Eur_GJ> Km_GJ := K_GJ;

		attribute<Eur_yr> Kj_pt_Base       (PlanRegio) := Ki_pt * Kengetallen/Onderhoud/Rj_PrimairNet;
		attribute<Eur_yr> Kj_pt_adm 	   (PlanRegio) := Kj_pt_Base * Kengetallen/Onderhoud/R_Admin;
		attribute<Eur_yr> Kj_pt_oh         (PlanRegio) := Kj_pt_Base - Kj_pt_adm;

		attribute<Eur_yr> Kj_ow_Base       (PlanRegio) := Ki_ow * TypeInfo/Rj_Onderhoud;
		attribute<Eur_yr> Kj_ow_adm        (PlanRegio) := Kj_ow_Base * Kengetallen/Onderhoud/R_Admin;
		attribute<Eur_yr> Kj_ow_oh         (PlanRegio) := Kj_ow_Base - Kj_ow_adm;

		attribute<Eur_yr> Kj_WarmteWaarde  (PlanRegio) := K_GJ  * KostenBaten/Vj_WarmtePrimairD;
		attribute<Eur_yr> Km_WarmteWaarde  (PlanRegio) := Km_GJ * KostenBaten/Vj_WarmtePrimairD;

		attribute<Eur_yr> Kj_Totaal        (PlanRegio) := Kj_pt_Base + Kj_ow_Base + Kj_WarmteWaarde;
		attribute<Eur_yr> Km_Totaal        (PlanRegio) := Kj_pt_Base + Kj_ow_Base + Km_WarmteWaarde;
		attribute<Eur_yr> Oj_SDE           (PlanRegio) := KostenBaten/Vj_WarmtePrimairD * SpecifiekeInstellingen/Beleid/EEA/SDE[TypeInfo/TypeInfoList_rel];
	}

	attribute<Eur   > Oi_Totaal            (PlanRegio) := KostenBaten/Oi_Aansluitbijdrage;
	attribute<Eur   > Ki_Totaal            (PlanRegio) := KostenBaten/Ki_wd_bruto + Kosten/Ki_Totaal; // eenmalige bruto maatschappelijke investeringskosten (dus zonder aftrenk van aansluitbijdrage)
	attribute<Eur   > Ki_Netto             (PlanRegio) := Ki_Totaal - NCW/ow28/StartDiscountFactor * Oi_Totaal; // : eenmalige investering minus netto aansluitbijdrage
	attribute<Eur_yr> Km_Totaal            (PlanRegio) := KostenBaten/Km_TotaalD + Kosten/Km_Totaal; // jaarlijkse bruto kosten zonder aftrek van vastrecht en verbruiksvergoeding
	attribute<Eur_yr> Oji_Netto            (PlanRegio) := KostenBaten/Oji_NettoD  - Kosten/Kj_Totaal; // Rentabiliteit: jaarlijkse opbrengst
	attribute<kg_yr>  CO2_Primair          (PlanRegio) := const(0[kg_yr], PlanRegio);
	attribute<kg_yr>  CO2                  (PlanRegio) := KostenBaten/CO2_BijstookD + CO2_Primair;
	attribute<Eur_yr> Ke_Totaal            (PlanRegio) := const(0[Eur_yr], PlanRegio);
	attribute<GJ_yr>  GJe_Totaal           (PlanRegio) := const(0[GJ_yr], PlanRegio);
	attribute<KG_yr>  CO2_e                (PlanRegio) := const(0[KG_yr], PlanRegio);
	attribute<Eur_yr> Oj_Afweging          (PlanRegio) := 
		Oji_Netto + kosten/Oj_SDE
		- Kosten/Ki_ow * NCW/ow28/AnnualisationFactor * ((1.0 - SpecifiekeInstellingen/Beleid/EIA/ow)*(1.0 - SpecifiekeInstellingen/Beleid/EEA/ow))[TypeInfo/TypeInfoList_rel]
		- Kosten/Kji_pt * ((1.0 - SpecifiekeInstellingen/Beleid/EIA/pt)*(1.0 - SpecifiekeInstellingen/Beleid/EEA/pt));
	attribute<bool> RentabiliteitsAfweging (PlanRegio) := Oj_Afweging > 0[Eur_yr];

	attribute<bool> OldGebruik             (PlanRegio) := IsDefined(BeginStatus/PlanRegioWarmteAllocatie_REMOVE);                                  // een optie (deze of andere) was al in gebruik
	attribute<bool> GebruiktOptie          (PlanRegio) := BeginStatus/PlanRegioWarmteAllocatie_REMOVE == Classifications/GebiedsOptie/V/Geothermie; // deze optie was al in gebruik;
	attribute<bool> NewGebruik             (PlanRegio) := RentabiliteitsAfweging && bron/InContour && not(OldGebruik);
	attribute<bool> GebruikOptie           (PlanRegio) := NewGebruik || GebruiktOptie;

	attribute<Eur_yr> Kj_Netto             (PlanRegio) := Ki_Netto * NCW/ow28/AnnualisationFactor - Oji_Netto; // Rentabiliteit: jaarlijkse opbrengsten voor de energieproducent
	
	container Results := BeginStatus
	{
		container BebouwingMutatie :=
			for_each_ne(Classifications/BebouwingsComponent/Name
				, 'PlanRegioOptieResultaat(BeginStatus/Bebouwing/'+Classifications/BebouwingsComponent/Name+', Classifications/GebiedsOptie/V/GeoThermie, .../GebruiktOptie)'
			);
		container Bebouwing :=
			for_each_ne(Classifications/BebouwingsComponent/Name
				, 'BebouwingMutatie/'+Classifications/BebouwingsComponent/Name+'/result'
			);
				
		container Factoren
		{
			parameter<float64> Aardgas       := 0.0;
			parameter<float64> Biogas        := 0.0;
			parameter<float64> Biovast       := 0.0;
			parameter<float64> Ondergrond    := 1.0;
			parameter<float64> Opwekking     := 0.0;
			parameter<float64> Elektriciteit := 1.0 / 30.0;
		}

		attribute<Classifications/GebiedsOptie> PlanRegioWarmteAllocatie_REMOVE (PlanRegio) :=
			../GebruikOptie
				? Classifications/GebiedsOptie/V/GeoThermie
				: BeginStatus/PlanRegioWarmteAllocatie_REMOVE;
		container Geothermie {
			container Flow
			{
				container ge {
					attribute<Eur_yr> Kj_ge_hv        (PlanRegio) := NewGebruik ? KostenBaten/Kj_ge_hv : 0[Eur_yr];
					attribute<Eur_yr> Kji_ge_ov       (PlanRegio) := NewGebruik ? KostenBaten/Kji_ge_ov : 0[Eur_yr];
					attribute<Eur_yr> Kji_ge_pm       (PlanRegio) := NewGebruik ? KostenBaten/Kji_ge_pm: 0[Eur_yr];
					
					attribute<Eur_yr> Kmi_ge_ov       (PlanRegio) := NewGebruik ? KostenBaten/Kmi_ge_ov : 0[Eur_yr];
					attribute<Eur_yr> Kmi_ge_pm       (PlanRegio) := NewGebruik ? KostenBaten/Kmi_ge_pm : 0[Eur_yr];
					attribute<Eur_yr> Om_comfort      (PlanRegio) := const(0[EUR_yr], PlanRegio);
				}
				
				container id {
					attribute<Eur_yr> Kj_id_gas       (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_id_gas_EH    (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_id_gas_CO2   (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_id_elek      (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_id_elek_EH   (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_id_elek_CO2  (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_id_oh        (PlanRegio) := NewGebruik ? KostenBaten/KostenD/Kj_id_oh : 0[Eur_yr];
					attribute<Eur_yr> Kj_id_adm       (PlanRegio) := const(0[EUR_yr], PlanRegio);
					
					attribute<Eur_yr> Kji_id          (PlanRegio) := NewGebruik ? KostenBaten/Kji_id : 0[Eur_yr];
					attribute<Eur_yr> Kmi_id          (PlanRegio) := NewGebruik ? KostenBaten/Kmi_id : 0[Eur_yr];
					
					attribute<Eur_yr> Oj_id_SDE   (PlanRegio) := const(0[EUR_yr], PlanRegio);
				}
				
				container wd {
					attribute<Eur_yr> Kj_wd_gas       (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_wd_gas_EH    (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_wd_gas_CO2   (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Km_wd_gas       (PlanRegio) := const(0[EUR_yr], PlanRegio);
					
					attribute<Eur_yr> Kj_wd_elek      (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_wd_elek_EH   (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_wd_elek_CO2  (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Km_wd_elek      (PlanRegio) := const(0[EUR_yr], PlanRegio);
					
					attribute<Eur_yr> Kj_wd_oh        (PlanRegio) := NewGebruik ? KostenBaten/KostenD/Kj_wd_oh        : 0[Eur_yr];
					attribute<Eur_yr> Kj_wd_adm       (PlanRegio) := NewGebruik ? KostenBaten/KostenD/Kj_wd_adm       : 0[Eur_yr];
					attribute<Eur_yr> Kji_wd          (PlanRegio) := NewGebruik ? KostenBaten/Kji_wd : 0[Eur_yr];
					attribute<Eur_yr> Kmi_wd          (PlanRegio) := NewGebruik ? KostenBaten/Kmi_wd : 0[Eur_yr];
					
					attribute<Eur_yr> Oj_wd_SDE   (PlanRegio) := const(0[EUR_yr], PlanRegio);
				}
				container pt {				
					attribute<Eur_yr> Kj_pt_oh        (PlanRegio) := NewGebruik ? Kosten/Kj_pt_oh        : 0[Eur_yr];
					attribute<Eur_yr> Kj_pt_adm       (PlanRegio) := NewGebruik ? Kosten/Kj_pt_adm       : 0[Eur_yr];
					
					attribute<Eur_yr> Kji_pt          (PlanRegio) := NewGebruik ? Kosten/Kji_pt : 0[Eur_yr];
					attribute<Eur_yr> Kmi_pt          (PlanRegio) := NewGebruik ? Kosten/Ki_pt * NCW/mr28/AnnualisationFactor : 0[Eur_yr];
				}
				container ow {
					attribute<Eur_yr> Kj_ow_gas       (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_ow_gas_EH    (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_ow_gas_CO2   (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Km_ow_gas       (PlanRegio) := const(0[EUR_yr], PlanRegio);
					
					attribute<Eur_yr> Kj_ow_elek      (PlanRegio) := NewGebruik ? Kosten/Kj_WarmteWaarde  : 0[Eur_yr];
					attribute<Eur_yr> Kj_ow_elek_EH   (PlanRegio) := NewGebruik ? Kosten/Kj_WarmteWaarde  : 0[Eur_yr];
					attribute<Eur_yr> Kj_ow_elek_CO2  (PlanRegio) := NewGebruik ? Kosten/Kj_WarmteWaarde  : 0[Eur_yr];
					attribute<Eur_yr> Km_ow_elek      (PlanRegio) := NewGebruik ? Kosten/Km_WarmteWaarde  : 0[Eur_yr];
					
					attribute<Eur_yr> Kj_ow_productie (PlanRegio) := const(0[EUR_yr], PlanRegio);
					
					attribute<Eur_yr> Kj_ow_oh        (PlanRegio) := NewGebruik ? Kosten/Kj_ow_oh        : 0[Eur_yr];
					attribute<Eur_yr> Kj_ow_adm       (PlanRegio) := NewGebruik ? Kosten/Kj_ow_adm       : 0[Eur_yr];
					
					attribute<Eur_yr> Kji_ow          (PlanRegio) := NewGebruik ? Kosten/Kji_ow : 0[Eur_yr];
					attribute<Eur_yr> Kmi_ow          (PlanRegio) := NewGebruik ? Kosten/Ki_ow * NCW/mr28/AnnualisationFactor : 0[Eur_yr];
					
					/// TODO: ook aanpassen in rentabiliteitsafwegingen 
					attribute<Eur_yr> Oj_ow_EEA      (PlanRegio) := NewGebruik
						? (Kj_ow_oh + Kj_ow_adm + Kj_ow_gas + Kj_ow_Elek + Kj_ow_productie) * MakeDefined(SpecifiekeInstellingen/Beleid/EEA/ow[TypeInfo/TypeInfoList_rel], 0.0) 
						: 0[Eur_yr];						
						
					attribute<Eur_yr> Oj_ow_SDE   (PlanRegio) := const(0[EUR_yr], PlanRegio);
				}
				container lv
				{
					attribute<Eur_yr> Oj_lv_verbruik  (PlanRegio) := NewGebruik ? KostenBaten/Opbrengsten/JaarlijksD/Warmtevraag  : 0[Eur_yr];
					attribute<Eur_yr> Oj_lv_vastrecht (PlanRegio) := NewGebruik ? KostenBaten/Opbrengsten/JaarlijksD/Oj_vastrecht : 0[Eur_yr];
					attribute<Eur_yr> Oj_lv_SDE       (PlanRegio) := NewGebruik ? kosten/Oj_SDE    : 0[Eur_yr];
				}
			}

			container Stock
			{
				attribute<Eur> Ki_ow          (PlanRegio) := NewGebruik ? Kosten/Ki_ow : 0[Eur];
				
				attribute<Eur> Oi_ow_EIA      (PlanRegio) := NewGebruik ? Kosten/Ki_ow * SpecifiekeInstellingen/Beleid/EIA/ow[TypeInfo/TypeInfoList_rel] : 0[Eur];
				attribute<Eur> Oj_ow_EEA      (PlanRegio) := NewGebruik ? (Ki_ow - Oi_ow_EIA) * SpecifiekeInstellingen/Beleid/EEA/ow[TypeInfo/TypeInfoList_rel] : 0[Eur];
				attribute<Eur> Ki_pt          (PlanRegio) := NewGebruik ? Kosten/Ki_pt : 0[Eur];
				attribute<Eur> Oi_pt_subsidie (PlanRegio) := NewGebruik ? Kosten/Ki_ow * SpecifiekeInstellingen/Beleid/EIA/pt : 0[Eur];
				
				attribute<Eur> Ki_wd          (PlanRegio) := NewGebruik ? KostenBaten/Ki_wd_Bruto : 0[Eur];
				attribute<Eur> Ki_id          (PlanRegio) := NewGebruik ? KostenBaten/Ki_id_Bruto : 0[Eur];
				attribute<Eur> Ki_ge_ov       (PlanRegio) := NewGebruik ? KostenBaten/Ki_ge_ov : 0[Eur];
				attribute<Eur> Ki_ge_pm       (PlanRegio) := NewGebruik ? KostenBaten/Ki_ge_pm : 0[Eur];
			}
			
			container Verbruik
			{
				attribute<Gj_yr>	V_id_gas		(PlanRegio)		:=	const(0[Gj_yr], PlanRegio);
				attribute<Gj_yr>	V_wd_gas		(PlanRegio)		:=	KostenBaten/KostenD/Bijstook/Vj_Bijstook;
				attribute<Gj_yr>	V_ow_gas		(PlanRegio)		:=	const(0[Gj_yr], PlanRegio);
				
				attribute<Gj_yr>	V_id_elek		(PlanRegio)		:=	const(0[Gj_yr], PlanRegio);
				attribute<Gj_yr>	V_wd_elek		(PlanRegio)		:=	const(0[Gj_yr], PlanRegio);
				attribute<Gj_yr>	V_ow_elek		(PlanRegio)		:=	const(0[Gj_yr], PlanRegio); // geen elektriciteitsverbruik bij LT bronnen TODO: controle met FO
				
				attribute<Gj_yr>	V_id_verlies	(PlanRegio)		:=	const(0[Gj_yr], PlanRegio); // niet specifiek benoemd, alles in WD, TODO: specifiek maken
				attribute<Gj_yr>	V_wd_verlies	(PlanRegio)		:=	const(0[Gj_yr], PlanRegio);
				attribute<Gj_yr>	V_pt_verlies	(PlanRegio)		:=	const(0[Gj_yr], PlanRegio); // geen primair transport bij LT netten
				
				attribute<Gj_yr>	V_warmte		(PlanRegio)		:=	KostenBaten/KostenD/Vj_Warmte;
				attribute<Gj_yr>	V_koude			(PlanRegio)		:=	const(0[Gj_yr], PlanRegio); // geen koude bij RW.
//REMOVE		attribute<Gj_yr>	V_WKO			(PlanRegio)		:=	V_warmte - V_koude;	
			}
			
			container Uitstoot
			{
				attribute<KG_yr>	CO2_id_gas		(PlanRegio)		:=	const(0[KG_yr], PlanRegio); // geen gasverbruik bij LT netten
				attribute<KG_yr>	CO2_wd_gas		(PlanRegio)		:=	const(0[KG_yr], PlanRegio); // geen gasverbruik bij LT netten
				attribute<KG_yr>	CO2_ow_gas		(PlanRegio)		:=	KostenBaten/Vj_WarmtePrimairD * MakeDefined(CO2_GJb, 0[KG_GJ]);
				
				attribute<KG_yr>	CO2_id_elek		(PlanRegio)		:=	Verbruik/V_id_elek * prijzen/Elektriciteit/CO2_GJ;
				attribute<KG_yr>	CO2_wd_elek		(PlanRegio)		:=	Verbruik/V_wd_elek * prijzen/Elektriciteit/CO2_GJ;
				attribute<KG_yr>	CO2_ow_elek		(PlanRegio)		:=	Verbruik/V_ow_elek * prijzen/Elektriciteit/CO2_GJ;				
			}

		}
	}
}
