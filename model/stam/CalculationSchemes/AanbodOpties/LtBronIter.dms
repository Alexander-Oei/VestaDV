//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                                  (C) VESTA 2018                                      //
//        Het doel van het model is om beleidsinformatie te genereren met betrekking    //
//        tot de energievoorziening van de gebouwde omgeving.                           //
//                                                                                      //
//                         Planbureau voor de Leefomgeving                              //
//                                    JUNI 2018                                         //
//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//     * In het geval van externe publicatie van onderzoeksresultaten waarbij gebruik   //
//       is gemaakt van de verstrekte gegevens dient de gebruiker deze resultaten aan   //
//       het PBL mede te delen middels de toezending van 2 exemplaren van de betref-    //
//       fende publicatie aan het hoofd van het team ROL van het PBL.                   //
//                                                                                      //
//     * In elke publicatie waarin de gegevens van het PBL in enige vorm zijn opgenomen,//
//       zal de gebruiker de vermelding opnemen: "Bron: Planbureau voor de Leefomgeving"//
//       Indien het een bewerking van de gegevens betreft, dient te worden opgenomen:   //
//       "Gebaseerd op Planbureau voor de Leefomgeving gegevens".                       //
//                                                                                      //
//     * Het PBL is onder geen enkele voorwaarde aansprakelijk tegenover gebruiker voor //
//       enige schade, mede begrepen winstderving, verlies van gegevens, schade         //
//       tengevolge van fouten of onjuistheden in gegevens, schade tengevolge van fouten//
//       of onvolkomenheden in de documentatie of andere incidentele of gevolgschade    //
//       welke voortvloeit uit of in verband staat met gebruik of het niet kunnen ge-   //
//       bruiken van gegevens.                                                          //
//                                                                                      //
//     * Het PBL staat niet in voor de juistheid van de resultaten die worden verkregen //
//       door combinatie van diverse, op zichzelf staande PBL-gegevens onderling,       //
//       of met die uit andere bronnen.                                                 //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

template LtBronIter
{
	// begin case parameters
	unit <uint32> Candidates;       // the set of bronnen for the current iteration
	unit <uint32> OrgineleBronSet; // the set of all original candidate bronnen
	// end case parameters

	unit <uint32> UniqueCandidates := Unique(Candidates/Point) // dit moet omdat connect_points anders weigert.
	,	DialogType = "Map"
	,	DialogData = "Point"
	{
		attribute<rdc_meter> Point       := Values;
		attribute<string>    Label       := AsItemList(Candidates/Label, RelPartitioning), DialogType = "LabelText";

		attribute<.> RelPartitioning (Candidates)       := rlookup(Candidates/Point, Values);
		attribute<.> Partitioning    (OrgineleBronSet) := RelPartitioning[Candidates/Partitioning];

		attribute<kW>        P_Warmte_primair := sum(Candidates/P_Warmte_primair, RelPartitioning);

		attribute<float64>   n           := sum(Candidates/n, RelPartitioning);
		attribute<float64>   MEAN_x      := sum(Candidates/n * Candidates/MEAN_x, RelPartitioning) / n;
		attribute<float64>   MEAN_y      := sum(Candidates/n * Candidates/MEAN_y, RelPartitioning) / n;
		attribute<float64>   SSD_xx      := sum(Candidates/SSD_xx, RelPartitioning);
		attribute<float64>   SSD_xy      := sum(Candidates/SSD_xy, RelPartitioning);
		attribute<float64>   SSD_yy      := sum(Candidates/SSD_yy, RelPartitioning);
		
		//attribute<Eur_yr> Oj_ABC:= Oj_totaal;

		// Rentabiliteitsafweging:
		// max(Ki_ABC, 400000) <= Oj_Netto * DurationFactor
		// samenvoegen verhoogt rhs additief, lhs minder dan additief als Ki_ABC onder de 400.000 ligt en Ki_Distr niet te veel stijgt, anders ligt het aan de ontwikkeling van Ki_Distr
		// het door samenvoegen behalen van een positieve afweging wordt verondersteld alleen mogelijk te zijn als  de onderdelen nog onder de 400000 opleveren, maar meer dan de ongerestricteerde Ki_ABC.

		attribute<bool> MarginaleAfweging := const(true, .); // Ki_B * NCW/wd28/AnnualisationFactor < AC_Netto;
		attribute<bool> ClusterAfweging   := P_warmte_primair < 10000[kW]; // TODO: check
		
		attribute<bool> SelectAfweging    := MarginaleAfweging && !ClusterAfweging; // cluster is klaar
		attribute<bool> CombiAfweging     := MarginaleAfweging &&  ClusterAfweging; // er moet verder geclusterd worden om aan het minimum te gaan voldoen.
	}

	unit<uint32> Selected := Subset(UniqueCandidates/SelectAfweging), DialogType = "Map", DialogData = "Point"
	{
		attribute<rdc_meter> Point                                := UniqueCandidates/Point[nr_OrgEntity];
		attribute<string>    Label                                := UniqueCandidates/Label[nr_OrgEntity], DialogType = "LabelText";
		
		attribute<kW>        P_warmte_primair    := UniqueCandidates/P_warmte_primair   [nr_OrgEntity];
		
		attribute<.>         Partitioning (OrgineleBronSet) := invert(nr_OrgEntity)[UniqueCandidates/Partitioning];
		
		attribute<float64>   n      := UniqueCandidates/n     [nr_OrgEntity];
		attribute<float64>   SSD_xx := UniqueCandidates/SSD_xx[nr_OrgEntity];
		attribute<float64>   SSD_yy := UniqueCandidates/SSD_yy[nr_OrgEntity];
		attribute<float64>   SSD_xy := UniqueCandidates/SSD_xy[nr_OrgEntity];
	}

	unit<uint32> ClusterSet := Subset(UniqueCandidates/CombiAfweging), DialogType = "Map", DialogData = "Point"
	{
		attribute<rdc_meter>  Point        := UniqueCandidates/Point[nr_OrgEntity];
		attribute<ClusterSet> Neighbour    := connect_neighbour(Point);
		attribute<bool>       InCluster    := ClusterSet/Neighbour[ClusterSet/Neighbour] == id(ClusterSet); // reciprociteit?
		attribute<bool>       CanCluster   := InCluster && ClusterSet/Neighbour > id(ClusterSet);
		attribute<bool>       NotInCluster := Not(InCluster);
	}

	unit <uint32> JoinedClusters := Subset(ClusterSet/CanCluster), DialogType = "Map", DialogData = "Point"
	{
		unit<uint32> Candidates := UniqueCandidates;

		attribute<Candidates> src1    := ClusterSet/nr_OrgEntity[nr_OrgEntity];
		attribute<Candidates> src2    := ClusterSet/nr_OrgEntity[ClusterSet/Neighbour[nr_OrgEntity]];

		attribute<.> Partitioning (OrgineleBronSet) := MakeDefined(invert(src1)[Candidates/Partitioning], invert(src2)[Candidates/Partitioning]);

		attribute<string> Label       := '['+Candidates/Label  [src1] + ';' + Candidates/Label[src2]+']', DialogType = "LabelText";
		attribute<kW>     P_warmte_primair    := Candidates/P_warmte_primair   [src1] + Candidates/P_warmte_primair   [src2];

		attribute<float64> n1         := Candidates/n[src1];
		attribute<float64> n2         := Candidates/n[src2];
		attribute<float64> n          := n1 + n2;
		attribute<float64> MEAN_x     := (n1*Candidates/MEAN_x[src1] + n2*Candidates/MEAN_x[src2]) / n;
		attribute<float64> MEAN_y     := (n1*Candidates/MEAN_y[src1] + n2*Candidates/MEAN_y[src2]) / n;
		attribute<float64> DIFF_x     := Candidates/MEAN_x[src1] - Candidates/MEAN_x[src2];
		attribute<float64> DIFF_y     := Candidates/MEAN_y[src1] - Candidates/MEAN_y[src2];
		attribute<float64> SSD_xx     := Candidates/SSD_xx[src1] + Candidates/SSD_xx[src2] + (n1*n2 / n) * DIFF_x * DIFF_x;
		attribute<float64> SSD_xy     := Candidates/SSD_xy[src1] + Candidates/SSD_xy[src2] + (n1*n2 / n) * DIFF_x * DIFF_y;
		attribute<float64> SSD_yy     := Candidates/SSD_yy[src1] + Candidates/SSD_yy[src2] + (n1*n2 / n) * DIFF_y * DIFF_y;

		attribute<rdc_meter> point    := point(MEAN_y, MEAN_x, rdc_meter);
	}

	unit <uint32> InheritedClusters := Subset(ClusterSet/NotInCluster), DialogType = "Map", DialogData = "Point"
	{
		unit<uint32>          Candidates                           := UniqueCandidates;
		attribute<Candidates> src1                                 := ClusterSet/nr_OrgEntity[nr_OrgEntity];
		attribute<.>          Partitioning (OrgineleBronSet) := invert(src1)[Candidates/Partitioning];
		
		attribute<string>  Label       := Candidates/Label    [src1], DialogType = "LabelText";
		attribute<kW>      P_warmte_primair    := Candidates/P_warmte_primair [src1];

		attribute<float64> n           := Candidates/n     [src1];
		attribute<float64> MEAN_x      := Candidates/MEAN_x[src1];
		attribute<float64> MEAN_y      := Candidates/MEAN_y[src1];
		attribute<float64> SSD_xx      := Candidates/SSD_xx[src1];
		attribute<float64> SSD_xy      := Candidates/SSD_xy[src1];
		attribute<float64> SSD_yy      := Candidates/SSD_yy[src1];

		attribute<rdc_meter> point     := Candidates/point[src1];
	}

	unit <uint32> SelectedClusters := union_unit(InheritedClusters, JoinedClusters), DialogType = "Map", DialogData = "Point"
	{
		attribute<string>  Label       := union_data(., InheritedClusters/Label      , JoinedClusters/Label), DialogType = "LabelText";
		attribute<kW>      P_warmte_primair    := union_data(., InheritedClusters/P_warmte_primair   , JoinedClusters/P_warmte_primair);

		attribute<.>      Partitioning (OrgineleBronSet) := MakeDefined(InheritedClusters/Partitioning, JoinedClusters/Partitioning + #InheritedClusters)[.];

		attribute<float64> n           := union_data(., InheritedClusters/n          , JoinedClusters/n);
		attribute<float64> MEAN_x      := union_data(., InheritedClusters/MEAN_x     , JoinedClusters/MEAN_x);
		attribute<float64> MEAN_y      := union_data(., InheritedClusters/MEAN_y     , JoinedClusters/MEAN_y);
		attribute<float64> SSD_xx      := union_data(., InheritedClusters/SSD_xx     , JoinedClusters/SSD_xx);
		attribute<float64> SSD_xy      := union_data(., InheritedClusters/SSD_xy     , JoinedClusters/SSD_xy);
		attribute<float64> SSD_yy      := union_data(., InheritedClusters/SSD_yy     , JoinedClusters/SSD_yy);

		attribute<rdc_meter> point     := union_data(., InheritedClusters/point      , JoinedClusters/point);
	}
}