//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2019 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

template LtData
{
	// begin case parameters
	container WkoDataComponent;
	parameter<string> ComponentNaam;
	parameter<Celsius> T_sec;
	parameter<bool> IsWKO := true;
	parameter<string> LabelConditie := '';
	// end case parameters

	container				BCdata							:= WkoDataComponent/BCdata;
	container				RuimtelijkeVraagComponent		:= BCdata/RuimtelijkeVraagComponent;
	container				BeginStatusComponent			:= ='BeginStatus/Bebouwing/'+ComponentNaam;
	unit<uint32>			BebouwingsObject				:= BCdata/BebouwingsObject;

	container Gebied
	{
		//beschrijving van gebiedskenmerken
		attribute<float64>	n			(BebouwingsObject)	:= value(BCdata/BebouwingsObject/Gebied/n, float64);
		attribute<float64>	MEAN_x		(BebouwingsObject)	:= BCdata/BebouwingsObject/Gebied/MEAN_x + rnd_uniform(0, BebouwingsObject, Range(float64, -1.0, 1.0));
		attribute<float64>	MEAN_y		(BebouwingsObject)	:= BCdata/BebouwingsObject/Gebied/MEAN_y + rnd_uniform(0, BebouwingsObject, Range(float64, -1.0, 1.0));
		attribute<float64>	SSD_xx		(BebouwingsObject)	:= BCdata/BebouwingsObject/Gebied/SSD_xx;
		attribute<float64>	SSD_xy		(BebouwingsObject)	:= BCdata/BebouwingsObject/Gebied/SSD_xy;
		attribute<float64>	SSD_yy		(BebouwingsObject)	:= BCdata/BebouwingsObject/Gebied/SSD_yy;
	}

	//selectie op basis van eisen potentiecontour en minimaal schillabel, indien van toepassing
	parameter<bool>			UseContour						:= =IsWKO ? '!AanbodKentallen/BuitenContour' : 'false',												Descr = "indien de gebiedsoptie WKO is wordt gebruik gemaakt van een potentiecontour";
	attribute<bool>			InContour	(BebouwingsObject)	:= =UseContour
		? 'IsDefined( RuimtelijkeData/WarmteBronnen/WKO/GeschikteContour/grid[RuimtelijkeVraagComponent/point[Geography/rdc_grids/m100]] )'
		: 'const(true, BebouwingsObject)',																														Descr = "controle of objecten wel of niet binnen de potentiecontour vallen";
	attribute<bool>			Geschikt	(BebouwingsObject)	:= =(UseContour ? 'InContour && ' : '')+ 
		'BcData/energielabel/CurrValue <= Classifications/energielabel/V/LabelE',																				Descr = "controle of het minimum vereiste schillabel aanwezig is";

	//Vraag objectgerelateerd in capaciteit en volume
	attribute<KW>			P_sec				(BebouwingsObject)	:=	WkoDataComponent/P_sec,																	Descr = "gevraagde capaciteit aan het secundaire net";
	attribute<KW>			P_sec_primair		(BebouwingsObject)	:=	P_sec * constanten/AandeelPrimairVermogen,												Descr = "vermogen dat voor rekening van de primaire bron wordt gerekend";
	attribute<KW>			P_sec_hulp			(BebouwingsObject)	:=	P_sec * constanten/AandeelHulpVermogen,													Descr = "vermogen dat voor rekening van de hulpvoorziening wordt gerekend";

	attribute<GJ_Yr>		V_Warmte			(BebouwingsObject)	:=	WkoDataComponent/V_Warmte,																Descr = "jaalijkse volumevraag warmte";
	attribute<GJ_Yr>		V_Koude				(BebouwingsObject)	:=	WkoDataComponent/V_Koude,																Descr = "jaarlijkse volumevraag koude";
	attribute<GJ_Yr>		V_Warmte_primair	(BebouwingsObject)	:=	WkoDataComponent/V_Wsec_netto * constanten/AandeelPrimairVolume,						Descr = "jaalijkse volumevraag warmte";
	attribute<GJ_Yr>		V_Koude_primair		(BebouwingsObject)	:=	WkoDataComponent/V_Ksec_netto * constanten/AandeelPrimairVolume,						Descr = "jaarlijkse volumevraag koude";
	attribute<GJ_Yr>		V_Warmte_hulp		(BebouwingsObject)	:=	WkoDataComponent/V_Wsec_netto * constanten/AandeelHulpVolume,							Descr = "jaalijkse volumevraag warmte";
	attribute<GJ_Yr>		V_Koude_hulp		(BebouwingsObject)	:=	WkoDataComponent/V_Ksec_netto * constanten/AandeelHulpVolume,							Descr = "jaarlijkse volumevraag koude";
	
	attribute<GJ_Yr>		V_elek_ind			(BebouwingsObject)	:=	WkoDataComponent/V_elek_ind,															Descr = "jaarlijkse elektriciteitsvraag gebouwinstallatie";
	attribute<GJ_Yr>		V_elek_ow			(BebouwingsObject)	:=	WkoDataComponent/V_elek_ow,																Descr = "jaarlijkse elektriciteitsvraag collectieve warmtepomp";
	
	//Uitstoot en verbruik
	attribute<GJ_yr>		GJe					(BebouwingsObject)	:=	V_Elek_ind + V_Elek_ow,																	Descr = "elektriciteitsverbruik van gebiedsoptie";
	attribute<GJ_yr>		GJw					(BebouwingsObject)	:=	V_warmte - GJe,																			Descr = "warmte geleverd uit gebiedsoptie";
	attribute<KG_yr>		CO2					(BebouwingsObject)	:=	Prijzen/Elektriciteit/CO2_GJ * GJe,														Descr = "co2-uitstoot door elektriciteitsverbruik gebiedsoptie";
	attribute<Eur_yr>		Kj_productie		(BebouwingsObject)	:=	V_Warmte_primair * constanten/K_GJ_bron,												Descr = "meerkosten van warmteproductie in primaire bron";
	
	
	//Investeringskosten vergoedingen gebouweigenaar
	attribute<Eur>			Ki_ge_pm			(BebouwingsObject)	:=	WkoDataComponent/Kosten/Ki_ge_pm,														Descr = "investeringskosten vergoeding gebouweigenaar projectmanagement";
	attribute<Eur>			Ki_ge_ov			(BebouwingsObject)	:=	WkoDataComponent/Kosten/Ki_ge_ov,														Descr = "investeringskosten vergoeding gebouweigenaar ongerief";
	attribute<Eur>			Oi_ge_EIA			(BebouwingsObject)	:=	WkoDataComponent/Kosten/Oi_ge_EIA,														Descr = "investeringssubsidie vergoeding gebouweigenaar ongerief";
	attribute<Eur_yr>		KL_ge				(BebouwingsObject)	:=	(Ki_ge_pm + Ki_ge_ov - Oi_ge_EIA) * BCdata/NcwRefs/_30/AnnualisationFactor,				Descr = "geannualiseerde investeringskosten vergoedingen gebouweigenaar";
	
	//Investeringskosten inpandige distributie
	attribute<Eur>			Ki_id				(BebouwingsObject)	:=	WkoDataComponent/Kosten/Ki_id,															Descr = "investeringskosten inpandige distributie";
	attribute<Eur>			Oi_id_EIA			(BebouwingsObject)	:=	Ki_id * SpecifiekeInstellingen/Beleid/EIA/id,											Descr = "investeringssubsidie inpandige distributie";
	attribute<Eur_yr>		KL_id				(BebouwingsObject)	:=	(Ki_id - Oi_id_EIA) * BCdata/NcwRefs/_30/AnnualisationFactor,							Descr = "geannualiseerde investeringskosten inpandige distributie";
	
	//Investeringskosten wijkdistributie gebouwgerelateerd
	attribute<Eur>			Ki_wd				(BebouwingsObject)	:=	WkoDataComponent/Kosten/Ki_wd,															Descr = "investeringskosten wijkdistributie gebouwgerelateerd";
	attribute<Eur>			Oi_wd_EIA			(BebouwingsObject)	:=	Ki_wd * SpecifiekeInstellingen/Beleid/EIA/wd,											Descr = "investeringssubsidie inpandige distributie";	
	attribute<Eur_yr>		KL_wd				(BebouwingsObject)	:=	(Ki_wd - Oi_wd_EIA) * NCW/wd28/AnnualisationFactor,										Descr = "geannualiseerde investeringskosten wijkdistributie";
	
	//Investeringskosten opwekking gebouwgerelateerd
	attribute<Eur>			Ki_ow_Uitkoppel		(BebouwingsObject)	:=	P_sec_primair * constanten/Ki_uitkoppel,												Descr = "investeringskosten voor het uitkoppelen van de primaire bron";
	attribute<Eur>			Ki_ow_eWP_col		(BebouwingsObject)	:=	CollectieveWP ? P_sec_hulp * constanten/Ki_eWP_coll : 0 [Eur],							Descr = "investeringskosten voor het installeren van de collectieve elektrische warmtepomp";
	attribute<Eur>			Ki_ow				(BebouwingsObject)	:=	Ki_ow_Uitkoppel + Ki_ow_eWP_col,														Descr = "investeringskosten opwekking gebouwgerelateerd";
	attribute<Eur>			Oi_ow_EIA			(BebouwingsObject)	:=	Ki_ow * SpecifiekeInstellingen/Beleid/EIA/LT,											Descr = "investeringssubsidie opwekking gebouwgerelateerd";
	attribute<Eur_yr>		KL_ow				(BebouwingsObject)	:=	(Ki_ow - Oi_ow_EIA) * NCW/ow28/AnnualisationFactor,										Descr = "geannualiseerde investeringskosten bronzijde opwekking";
	
	//Elektriciteitskosten
	attribute<Eur_yr>		Kj_elek_id_EH		(BebouwingsObject)	:=	WkoDataComponent/Kosten/KEH_elek_ind,													Descr = "eindgebruikerskosten elektriciteit individueel: component energieheffing";
	attribute<Eur_yr>		Kj_elek_id_CO2		(BebouwingsObject)	:=	WkoDataComponent/Kosten/KCO2_elek_ind,													Descr = "eindgebruikerskosten elektriciteit individueel: component CO2 heffing";
	attribute<Eur_yr>		Kj_elek_id			(BebouwingsObject)	:=	WkoDataComponent/Kosten/Kj_elek_ind,													Descr = "eindgebruikerskosten elektriciteit individueel, totaal exclusief btw";
	attribute<Eur_yr>		Km_elek_id			(BebouwingsObject)	:=	WkoDataComponent/Kosten/Km_elek_ind,													Descr = "maatschappelijke kosten elektriciteit individueel";
	
	attribute<Eur_yr>		Kj_elek_ow_EH		(BebouwingsObject)	:=	WkoDataComponent/Kosten/KEH_elek_ow,													Descr = "eindgebruikerskosten elektriciteit collectief: component energieheffing";
	attribute<Eur_yr>		Kj_elek_ow_CO2		(BebouwingsObject)	:=	WkoDataComponent/Kosten/KCO2_elek_ow,													Descr = "eindgebruikerskosten elektriciteit collectief: component CO2 heffing";
	attribute<Eur_yr>		Kj_elek_ow			(BebouwingsObject)	:=	WkoDataComponent/Kosten/Kj_elek_ow,														Descr = "eindgebruikerskosten elektriciteit collectief, totaal exclusief btw";
	attribute<Eur_yr>		Km_elek_ow			(BebouwingsObject)	:=	WkoDataComponent/Kosten/Km_elek_ow,														Descr = "maatschappelijke kosten elektriciteit collectief";

	//Onderhoud en administratie
	attribute<Eur_yr>		Kj_id_onderhoud		(BebouwingsObject)	:=	Ki_id * constanten/OenM/R_id_onderhoud,													Descr = "jaarlijkse onderhoudskosten inpandige distributie";
	attribute<Eur_yr>		Kj_wd_onderhoud		(BebouwingsObject)	:=	Ki_wd * constanten/OenM/R_wd_onderhoud,													Descr = "jaarlijkse onderhoudskosten wijkdistributie";
	attribute<Eur_yr>		Kj_uitkoppel_oh		(BebouwingsObject)	:=	Ki_ow_Uitkoppel * constanten/OenM/R_ow_onderhoud,										Descr = "jaarlijkse onderhoudskosten uitkoppeling primaire bron";
	attribute<Eur_yr>		Kj_col_ewp_oh		(BebouwingsObject)	:=	Ki_ow_eWP_col * constanten/OenM/R_wpColl_onderhoud,										Descr = "jaarlijkse onderhoudskosten collectieve warmtepomp";
	attribute<Eur_yr>		Kj_onderhoud		(BebouwingsObject)	:=	Kj_id_onderhoud + Kj_wd_onderhoud + Kj_uitkoppel_oh + Kj_col_ewp_oh,					Descr = "jaarlijkse onderhoudskosten totaal";
	
	attribute<Eur_yr>		Kj_id_admin			(BebouwingsObject)	:=	Ki_id * constanten/OenM/R_id_admin,														Descr = "jaarlijkse administratiekosten inpandige distributie";
	attribute<Eur_yr>		Kj_wd_admin			(BebouwingsObject)	:=	Ki_wd * constanten/OenM/R_wd_admin,														Descr = "jaarlijkse administratiekosten wijkdistributie";
	attribute<Eur_yr>		Kj_uitkoppel_admin	(BebouwingsObject)	:=	Ki_ow_Uitkoppel * constanten/OenM/R_ow_admin,											Descr = "jaarlijkse administratiekosten uitkoppeling primaire bron";
	attribute<Eur_yr>		Kj_col_ewp_admin	(BebouwingsObject)	:=	Ki_ow_eWP_col * constanten/OenM/R_wpColl_admin,											Descr = "jaarlijkse administratiekosten collectieve warmtepomp";
	attribute<Eur_yr>		Kj_administratie	(BebouwingsObject)	:=	Kj_id_admin + Kj_wd_admin + Kj_uitkoppel_admin + Kj_col_ewp_admin,						Descr = "jaarlijkse administratiekosten totaal";
	
	//Opbrengsten
	attribute<Eur>			Oi_Aansl			(BebouwingsObject)	:=	WkoDataComponent/Opbrengsten/Oi_Aansl,													Descr = "initiele opbrengsten uit aansluitbijdrage";
	attribute<Eur_yr>		Oj_verbruik			(BebouwingsObject)	:=	WkoDataComponent/Opbrengsten/Oj_Warmte + WkoDataComponent/Opbrengsten/Oj_Koude,			Descr = "jaarlijkse opbrengsten uit levering";
	attribute<Eur_yr>		Oj_vastrecht		(BebouwingsObject)	:=	WkoDataComponent/Opbrengsten/Oj_Vastrecht,												Descr = "jaarlijkse opbrengsten uit vastrecht";
	attribute<Eur_yr>		KL_Aansl			(BebouwingsObject)	:=	Oi_Aansl * (NCW/id28/AnnualisationFactor * NCW/id28/StartDiscountFactor),				Descr = "geannualiseerde aansluitbijdrage";
	attribute<Eur_yr>		Om_comfort			(BebouwingsObject)	:=	WkoDataComponent/Opbrengsten/Om_comfort,												Descr = "maatschappelijke opbrengsten door koudelevering";
	attribute<Eur_yr>		Oj_SDE				(BebouwingsObject)	:=	GJw [Gj_yr] * SpecifiekeInstellingen/Beleid/EEA/SDE_LT [Eur_Gj],							Descr = "jaarlijkse opbrengsten uit SDE op lage temperatuur warmte";		
	
	//Netto contributiemarge
	attribute<Eur_yr>		Oj_totaal			(BebouwingsObject)	:=	KL_aansl + Oj_verbruik + Oj_vastrecht + Oj_SDE,											Descr = "totale jaarlijkse opbrengsten";
	attribute<Eur_yr>		KL_totaal			(BebouwingsObject)	:=	KL_id + KL_wd + KL_ow + KL_ge,															Descr = "totale jaarlijkse kapitaallasten";
	attribute<Eur_yr>		Kj_totaal			(BebouwingsObject)	:=	Kj_productie + Kj_elek_id + Kj_elek_ow + Kj_onderhoud + Kj_administratie,				Descr = "totale jaarlijkse kapitaallasten";
	attribute<Eur_yr>		Contributiemarge	(BebouwingsObject)	:=	Oj_totaal - KL_totaal - Kj_totaal,														Descr = "Netto contributiemarge per object";																																		
	
	//selectie van geschikte kandidaten uit alle bebouwingsobjecten op basis van rentabiliteit en geselecteerde criteria
	attribute<bool>		RentabiliteitsAfweging	(BebouwingsObject)	:=	Contributiemarge > 0[Eur_yr],															Descr = "heeft dit bebouwingsobject individueel een positieve aansluitmarge, ja of nee";
	attribute<bool>		MarginaleAfweging		(BebouwingsObject)	:=	geschikt && RentabiliteitsAfweging,														Descr = "is zowel de aansluitmarge positief als het minimale schillabel aanwezig, ja of nee";
	attribute<bool>		WasAllocated			(BebouwingsObject)	:=	IsDefined(BeginStatusComponent/WarmteAllocatie) 
																		|| IsDefined(BeginStatus/AanbodOpties/PlanRegioWarmteAllocatie[BCdata/PlanRegio_rel]),	Descr = "is het gebouw al eerder aangesloten op een andere verwarmingsoptie, ja of nee";
	attribute<bool>		Toegestaan				(BebouwingsObject)	:=	MarginaleAfweging && !WasAllocated,														Descr = "selectie op alle bebouwingsobjecten dioe voldoen aan de gestelde criteria";
	unit<uint32>		AllowedObjects								:=	Subset(Toegestaan)
	{
		attribute<string>	Label									:=	'(' + BCdata/BebouwingsObject/Label[nr_OrgEntity]+')',									Descr = "koppelveld om relatie tussen eerder domein en domein van geselecteerde kandidaten te leggen";
	}
}