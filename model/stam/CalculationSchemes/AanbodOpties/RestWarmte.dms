//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2019 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

template RestWarmte
{
	// begin case parameters
	container BeginStatus;
	container AanbodKentallen;
	container EnergiePrijzen;
	parameter<units/yr_uint16> Jaar;
	// end case parameters

	unit<uint32> PlanRegio := Invoer/SpecifiekeInstellingen/PlanRegio;
	container KostenBaten := CalculationSchemes/KostenBaten(BeginStatus/Bebouwing, invoer/Energieprijzen);

	unit<uint32> Bronnen := ='/Invoer/RuimtelijkeData/WarmteBronnen/RestWarmte/BronSelecties/' + ZichtjaarName + '/Bronnen'
	{
		attribute<Eur_kW   > Ki_kW_Org := (Ki_kW_min * Schuiven/KostenMin + Ki_kW_max * Schuiven/KostenMax);
		attribute<Eur_kW   > Ki_kW     := Ki_kW_org  * LeerCurves/Curve2;
		attribute<Eur_kW_yr> Kj_kW     := Ki_kW * Rj_Onderhoud;
	}

	container RestWarmteStartingState := BeginStatus/RestWarmte
	{
		unit<uint32> BronConnections := ../Bronnen;

		attribute<BronConnections>  CurrBron        (PlanRegio)       := rlookup(CumulatedArcSet/BronPoint, BronConnections/Point)[invert(CumulatedArcSet/PlanRegio_rel)];
		attribute<BronConnections>  CurrConnection  (PlanRegio)       := CurrBron;

		attribute<MW>               MWth_used       (BronConnections) := sum(KostenBaten/CapaciteitsVraagD * float64(GebruikRestwarmte) , CurrBron);
		attribute<MW>               MWth_capaciteit (BronConnections) := Bronnen/MWth_max  - MWth_used;
	}
	attribute<Bool> HasWarmteVraag (PlanRegio) := not(IsDefined(BeginStatus/PlanRegioWarmteAllocatie_REMOVE));

	parameter<uint32> NrIterations := Voorkeuren/NewRestWarmte ? 25 : 1;

	unit<uint32> Iteration := Range(uint32, 0, NrIterations)
	{
		attribute<String> Name          := 'I'+String(id(.));
		attribute<String> PrevStateName := MakeDefined(Iteration/Name[id(Iteration)-1]+'/NextState', 'RestWarmteStartingState');
	}
	
	container Iterations := for_each_ne(Iteration/Name, 'CalculationSchemes/AanbodOpties/RestWarmteIteratie('+Iteration/PrevStateName+', Bronnen, PlanRegio, KostenBaten)');

	container LastIter  := ='Iterations/'+last(Iteration/Name);
	container LastState := LastIter/NextState;

	attribute<MW>       P_Bron  (Bronnen) := sum(KostenBaten/CapaciteitsVraagD * float64(LastState/GebruikRestWarmte) , LastState/CurrBron);
	attribute<float64>  R_Bron  (Bronnen) := P_Bron / Bronnen/MWTh_Max;

	attribute<Bool> GebruikBron (Bronnen) := P_Bron >= Bronnen/MWth_min;

	attribute<Bool> RentabiliteitsAfweging(PlanRegio) := GebruikBron[LastState/CurrBron] && LastState/GebruikRestWarmte
	,	IntegrityCheck = "not(any(LastIter/ExtraGebruikRestWarmte))"; // @@@ Increase NrIterations is this fails.
	
	attribute<Bool> OldGebruik           (PlanRegio) := IsDefined(BeginStatus/PlanRegioWarmteAllocatie_REMOVE); // een optie (deze of andere) was al in gebruik
	attribute<Bool> GebruiktOptie        (PlanRegio) := BeginStatus/PlanRegioWarmteAllocatie_REMOVE == Classifications/GebiedsOptie/V/Restwarmte;   // deze optie was al in gebruik;
	attribute<Bool> NewGebruik           (PlanRegio) := RentabiliteitsAfweging && not(OldGebruik);
	attribute<Bool> GebruikOptieAfweging (PlanRegio) := NewGebruik || GebruiktOptie;
	attribute<Bool> GebruikOptie         (PlanRegio) := = Voorkeuren/OldRestwarmte
		?	'GebruiktOptie'
		:	(Jaar == 2010[units/yr_uint16])
			?	'bool(PlanRegio/RW)' 
			:	'GebruikOptieAfweging'
		;

	attribute<Eur   > Oi_Totaal          (PlanRegio) := KostenBaten/Oi_Aansluitbijdrage;
	attribute<KG_GJ>  CO2_GJb            (PlanRegio) := Bronnen/CO2_GJ[LastIter/NextState/CurrBron];
	attribute<G_GJ>   NOx_GJb            (PlanRegio) := Bronnen/NOx_GJ[LastIter/NextState/CurrBron];
	attribute<G_GJ>   SO2_GJb            (PlanRegio) := Bronnen/SO2_GJ[LastIter/NextState/CurrBron];
	attribute<G_GJ>   VOS_GJb            (PlanRegio) := Bronnen/VOS_GJ[LastIter/NextState/CurrBron];
	attribute<G_GJ>   TS_GJb             (PlanRegio) := Bronnen/TS_GJ [LastIter/NextState/CurrBron];
	attribute<KG_yr>  CO2_Primair        (PlanRegio) := KostenBaten/Vj_WarmtePrimairD * MakeDefined(CO2_GJb, 0[KG_GJ]);
	attribute<KG_yr>  CO2                (PlanRegio) := KostenBaten/CO2_BijstookD + CO2_Primair;
	attribute<GJ_yr>  GJe_Totaal         (PlanRegio) := const(0[GJ_yr], PlanRegio);
//	attribute<KG_yr>  CO2_e              (PlanRegio) := const(0[KG_yr], PlanRegio);

	container Results := BeginStatus, IntegrityCheck = "P_Bron <= Bronnen/MWTh_Max * float64(3.00)"
	{
		container BebouwingMutatie :=
			for_each_ne(Classifications/BebouwingsComponent/Name
				, 'PlanRegioOptieResultaat(BeginStatus/Bebouwing/'+Classifications/BebouwingsComponent/Name+', Classifications/GebiedsOptie/V/RestWarmte, .../GebruiktOptie)'
			);
		container Bebouwing :=
			for_each_ne(Classifications/BebouwingsComponent/Name
				, 'BebouwingMutatie/'+Classifications/BebouwingsComponent/Name+'/result'
			);
				
		container Factoren
		{
			parameter<float64> Aardgas       := 0.0; // TODO
			parameter<float64> Biogas        := 0.0; // TODO
			parameter<float64> Biovast       := 0.0; // TODO
			parameter<float64> Ondergrond    := 0.0;
			parameter<float64> Opwekking     := 1.0;
			parameter<float64> Elektriciteit := 0.0; // TODO
		}
		
		attribute<bool> GebruikRestWarmteMetBron (PlanRegio) := GebruikOptie;
		
		container RestWarmte := MiniStock, FreeData = "False"
		{
			container Flow //:= BeginStatus/Flow
			{
				container ge {
					attribute<Eur_yr> Kj_ge_hv        (PlanRegio) := NewGebruik ? KostenBaten/Kj_ge_hv  : 0[Eur_yr];
					attribute<Eur_yr> Kji_ge_ov       (PlanRegio) := NewGebruik ? KostenBaten/Kji_ge_ov : 0[Eur_yr];
					attribute<Eur_yr> Kji_ge_pm       (PlanRegio) := NewGebruik ? KostenBaten/Kji_ge_pm : 0[Eur_yr];
					
					attribute<Eur_yr> Kmi_ge_ov       (PlanRegio) := NewGebruik ? KostenBaten/Kmi_ge_ov : 0[Eur_yr];
					attribute<Eur_yr> Kmi_ge_pm       (PlanRegio) := NewGebruik ? KostenBaten/Kmi_ge_pm : 0[Eur_yr];
					attribute<Eur_yr> Om_ge_comfort   (PlanRegio) := const(0[EUR_yr], PlanRegio);
					
					attribute<Eur_yr> Oji_ge_EIA      (PlanRegio) := NewGebruik ? Stock/Oi_ge_EIA * NCW/bw28/AnnualisationFactor : 0[Eur_yr];
					attribute<Eur_yr> Omi_ge_EIA      (PlanRegio) := NewGebruik ? Stock/Oi_ge_EIA * NCW/bw28/AnnualisationFactor : 0[Eur_yr];
				}
				
				container id {
					attribute<Eur_yr> Kj_id_gas       (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_id_gas_EH    (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_id_gas_CO2   (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Km_id_gas       (PlanRegio) := const(0[EUR_yr], PlanRegio);
					
					attribute<Eur_yr> Kj_id_elek      (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_id_elek_EH   (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_id_elek_CO2  (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Km_id_elek      (PlanRegio) := const(0[EUR_yr], PlanRegio);
					
					attribute<Eur_yr> Kj_id_oh        (PlanRegio) := NewGebruik ? KostenBaten/KostenD/Kj_id_oh : 0[Eur_yr];
					attribute<Eur_yr> Kj_id_adm       (PlanRegio) := const(0[EUR_yr], PlanRegio);
					
					attribute<Eur_yr> Kji_id          (PlanRegio) := NewGebruik ? KostenBaten/Kji_id : 0[Eur_yr];
					attribute<Eur_yr> Kmi_id          (PlanRegio) := NewGebruik ? KostenBaten/Kmi_id : 0[Eur_yr];
					
					attribute<Eur_yr> Oji_id_EIA      (PlanRegio) := NewGebruik ? Stock/Oi_id_EIA * NCW/id28/AnnualisationFactor : 0[Eur_yr];
					attribute<Eur_yr> Omi_id_EIA      (PlanRegio) := NewGebruik ? Stock/Oi_id_EIA * NCW/mr28/AnnualisationFactor : 0[Eur_yr];
					
					/// TODO: ook aanpassen in rentabiliteitsafwegingen 
					attribute<Eur_yr> Oj_id_SDE       (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Oj_id_EEA       (PlanRegio) := const(0[EUR_yr], PlanRegio);
				}
				
				container wd {
					attribute<Eur_yr> Kj_wd_gas       (PlanRegio) := NewGebruik ? KostenBaten/KostenD/Bijstook/Kj_wd_gas    : 0[Eur_yr];
					attribute<Eur_yr> Kj_wd_gas_EH    (PlanRegio) := NewGebruik ? KostenBaten/KostenD/Bijstook/Kj_wd_gas_EH : 0[Eur_yr];
					attribute<Eur_yr> Kj_wd_gas_CO2   (PlanRegio) := NewGebruik ? KostenBaten/KostenD/Bijstook/Kj_wd_gas_CO2: 0[Eur_yr];
					attribute<Eur_yr> Km_wd_gas       (PlanRegio) := NewGebruik ? KostenBaten/KostenD/Bijstook/Km_wd_gas    : 0[Eur_yr];
					
					attribute<Eur_yr> Kj_wd_elek      (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_wd_elek_EH   (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_wd_elek_CO2  (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Km_wd_elek      (PlanRegio) := const(0[EUR_yr], PlanRegio);
					
					attribute<Eur_yr> Kj_wd_oh        (PlanRegio) := NewGebruik ? KostenBaten/KostenD/Kj_wd_oh  : 0[Eur_yr];
					attribute<Eur_yr> Kj_wd_adm       (PlanRegio) := NewGebruik ? KostenBaten/KostenD/Kj_wd_adm : 0[Eur_yr];
					attribute<Eur_yr> Kji_wd          (PlanRegio) := NewGebruik ? KostenBaten/Kji_wd : 0[Eur_yr];
					attribute<Eur_yr> Kmi_wd          (PlanRegio) := NewGebruik ? KostenBaten/Kmi_wd : 0[Eur_yr];
					
					attribute<Eur_yr> Oji_wd_EIA      (PlanRegio) := NewGebruik ? Stock/Oi_wd_EIA * NCW/wd28/AnnualisationFactor : 0[Eur_yr];
					attribute<Eur_yr> Omi_wd_EIA      (PlanRegio) := NewGebruik ? Stock/Oi_wd_EIA * NCW/mr28/AnnualisationFactor : 0[Eur_yr];
					
					/// TODO: ook aanpassen in rentabiliteitsafwegingen 
					attribute<Eur_yr> Oj_wd_SDE       (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Oj_wd_EEA       (PlanRegio) := const(0[EUR_yr], PlanRegio);
				}
				container pt {
					attribute<Eur_yr> Kj_pt_oh        (PlanRegio) := NewGebruik ? lastiter/Assignment/Kj_pt_oh        : 0[Eur_yr];
					attribute<Eur_yr> Kj_pt_adm       (PlanRegio) := NewGebruik ? lastiter/Assignment/Kj_pt_adm       : 0[Eur_yr];
					
					attribute<Eur_yr> Kji_pt          (PlanRegio) := NewGebruik ? lastiter/Assignment/Kji_pt : 0[Eur_yr];
					attribute<Eur_yr> Kmi_pt          (PlanRegio) := NewGebruik ? lastiter/Assignment/Ki_pt * NCW/mr28/AnnualisationFactor : 0[Eur_yr];
					
					attribute<Eur_yr> Oji_pt_EIA      (PlanRegio) := NewGebruik ? lastIter/Assignment/Oji_pt_EIA : 0[Eur_yr];
					attribute<Eur_yr> Omi_pt_EIA      (PlanRegio) := NewGebruik ? Stock/Oi_pt_EIA * NCW/mr28/AnnualisationFactor : 0[Eur_yr];
					
					attribute<Eur_yr> Oj_pt_EEA       (PlanRegio) := NewGebruik  ? lastIter/Assignment/Oj_pt_EEA : 0[Eur_yr];						
				}
				container ow {
				// TODO: gebruik koppelfactoren 
					attribute<Eur_yr> Kj_ow_gas       (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_ow_gas_EH    (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_ow_gas_CO2   (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Km_ow_gas       (PlanRegio) := const(0[EUR_yr], PlanRegio);
				// TODO: gebruik koppelfactoren 
					attribute<Eur_yr> Kj_ow_elek      (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_ow_elek_EH   (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Kj_ow_elek_CO2  (PlanRegio) := const(0[EUR_yr], PlanRegio);
					attribute<Eur_yr> Km_ow_elek      (PlanRegio) := const(0[EUR_yr], PlanRegio);
					
					attribute<Eur_yr> Kj_ow_productie (PlanRegio) := NewGebruik ? lastiter/Assignment/Kj_warmtewaarde : 0[Eur_yr];
					
					attribute<Eur_yr> Kj_ow_oh        (PlanRegio) := NewGebruik ? lastiter/Assignment/Kj_ow_oh        : 0[Eur_yr];
					attribute<Eur_yr> Kj_ow_adm       (PlanRegio) := NewGebruik ? lastiter/Assignment/Kj_ow_adm       : 0[Eur_yr];
					
					attribute<Eur_yr> Kji_ow          (PlanRegio) := NewGebruik ? lastiter/Assignment/Kji_ow : 0[Eur_yr];
					attribute<Eur_yr> Kmi_ow          (PlanRegio) := NewGebruik ? lastiter/Assignment/Ki_ow * NCW/mr28/AnnualisationFactor : 0[Eur_yr];
					
					attribute<Eur_yr> Oji_ow_EIA      (PlanRegio) := NewGebruik ? lastIter/Assignment/Oji_ow_EIA : 0[Eur_yr];
					attribute<Eur_yr> Omi_ow_EIA      (PlanRegio) := NewGebruik ? Stock/Oi_ow_EIA * NCW/mr28/AnnualisationFactor : 0[Eur_yr];
					
					/// TODO: ook aanpassen in rentabiliteitsafwegingen 
					attribute<Eur_yr> Oj_ow_EEA       (PlanRegio) := NewGebruik
						? (Kj_ow_oh + Kj_ow_adm + Kj_ow_gas + Kj_ow_Elek + Kj_ow_productie) * MakeDefined(SpecifiekeInstellingen/Beleid/EEA/ow[Bronnen/TypeInfoList_rel[lastState/CurrBron]], 0.0) 
						: 0[Eur_yr];
						
					attribute<Eur_yr> Oj_ow_SDE   (PlanRegio) := const(0[EUR_yr], PlanRegio);
				}
				container lv
				{
					attribute<Eur_yr> Oji_lv_Aansl    (PlanRegio) := Stock/Oi_lv_Aansl * NCW/ow28/AnnualisationFactor;
					attribute<Eur_yr> Omi_lv_Aansl    (PlanRegio) := Stock/Oi_lv_Aansl * NCW/mr28/AnnualisationFactor;
					
					attribute<Eur_yr> Oj_lv_verbruik  (PlanRegio) := NewGebruik ? KostenBaten/Opbrengsten/JaarlijksD/Warmtevraag  : 0[Eur_yr];
					attribute<Eur_yr> Oj_lv_vastrecht (PlanRegio) := NewGebruik ? KostenBaten/Opbrengsten/JaarlijksD/Oj_vastrecht : 0[Eur_yr];
					attribute<Eur_yr> Oj_lv_SDE       (PlanRegio) := NewGebruik ? lastiter/Assignment/Oj_SDE  : 0[Eur_yr];
				}
			}

			container Stock
			{
				attribute<Eur> Oi_lv_Aansl    (PlanRegio) := NewGebruik ? KostenBaten/Opbrengsten/EenmaligD/Oi_Totaal : 0[Eur];
				
				attribute<Eur> Ki_ge_ov       (PlanRegio) := NewGebruik ? KostenBaten/Ki_ge_ov : 0[Eur];
				attribute<Eur> Ki_ge_pm       (PlanRegio) := NewGebruik ? KostenBaten/Ki_ge_pm : 0[Eur];
				attribute<Eur> Oi_ge_EIA      (PlanRegio) := NewGebruik ? KostenBaten/Oi_ge_subsidie: 0[Eur];
				
				attribute<Eur> Ki_id          (PlanRegio) := NewGebruik ? KostenBaten/Ki_id_Bruto : 0[Eur];
				attribute<Eur> Oi_id_EIA      (PlanRegio) := NewGebruik ? KostenBaten/Ki_id_Bruto * SpecifiekeInstellingen/Beleid/EIA/id : 0[Eur];
				
				attribute<Eur> Ki_wd          (PlanRegio) := NewGebruik ? KostenBaten/Ki_wd_Bruto : 0[Eur];
				attribute<Eur> Oi_wd_EIA      (PlanRegio) := NewGebruik ? KostenBaten/Ki_wd_Bruto * SpecifiekeInstellingen/Beleid/EIA/wd : 0[Eur];
				
				attribute<Eur> Ki_pt          (PlanRegio) := NewGebruik ? lastiter/Assignment/Ki_pt : 0[Eur];
				attribute<Eur> Oi_pt_EIA      (PlanRegio) := NewGebruik ? lastiter/Assignment/Oi_pt_EIA : 0[Eur];
				
				attribute<Eur> Ki_ow          (PlanRegio) := NewGebruik ? lastiter/Assignment/Ki_ow : 0[Eur];
				attribute<Eur> Oi_ow_EIA      (PlanRegio) := NewGebruik ? lastiter/Assignment/Oi_ow_EIA : 0[Eur];
			}
			
			container Verbruik
			{
				attribute<Gj_yr>	V_id_gas		(PlanRegio)		:=	const(0[Gj_yr], PlanRegio);
				attribute<Gj_yr>	V_wd_gas		(PlanRegio)		:=	KostenBaten/KostenD/Bijstook/Vj_Bijstook;
				attribute<Gj_yr>	V_ow_gas		(PlanRegio)		:=	const(0[Gj_yr], PlanRegio);
				
				attribute<Gj_yr>	V_id_elek		(PlanRegio)		:=	const(0[Gj_yr], PlanRegio);
				attribute<Gj_yr>	V_wd_elek		(PlanRegio)		:=	const(0[Gj_yr], PlanRegio);
				attribute<Gj_yr>	V_ow_elek		(PlanRegio)		:=	const(0[Gj_yr], PlanRegio); // geen elektriciteitsverbruik bij LT bronnen TODO: controle met FO
				
				attribute<Gj_yr>	V_id_verlies	(PlanRegio)		:=	const(0[Gj_yr], PlanRegio); // niet specifiek benoemd, alles in WD, TODO: specifiek maken
				attribute<Gj_yr>	V_wd_verlies	(PlanRegio)		:=	const(0[Gj_yr], PlanRegio);
				attribute<Gj_yr>	V_pt_verlies	(PlanRegio)		:=	const(0[Gj_yr], PlanRegio); // geen primair transport bij LT netten
				
				attribute<Gj_yr>	V_warmte		(PlanRegio)		:=	KostenBaten/KostenD/Vj_Warmte;
				attribute<Gj_yr>	V_koude			(PlanRegio)		:=	const(0[Gj_yr], PlanRegio); // geen koude bij RW.
//REMOVE		attribute<Gj_yr>	V_WKO			(PlanRegio)		:=	V_warmte - V_koude;	
			}
			
			container Uitstoot
			{
				attribute<KG_yr>	CO2_id_gas		(PlanRegio)		:=	const(0[KG_yr], PlanRegio); // geen gasverbruik bij LT netten
				attribute<KG_yr>	CO2_wd_gas		(PlanRegio)		:=	const(0[KG_yr], PlanRegio); // geen gasverbruik bij LT netten
				attribute<KG_yr>	CO2_ow_gas		(PlanRegio)		:=	KostenBaten/Vj_WarmtePrimairD * MakeDefined(CO2_GJb, 0[KG_GJ]);
				
				attribute<KG_yr>	CO2_id_elek		(PlanRegio)		:=	Verbruik/V_id_elek * prijzen/Elektriciteit/CO2_GJ;
				attribute<KG_yr>	CO2_wd_elek		(PlanRegio)		:=	Verbruik/V_wd_elek * prijzen/Elektriciteit/CO2_GJ;
				attribute<KG_yr>	CO2_ow_elek		(PlanRegio)		:=	Verbruik/V_ow_elek * prijzen/Elektriciteit/CO2_GJ;				
			}
			
			attribute<bool> GebruikRestWarmte      (PlanRegio) := GebruikRestWarmteMetBron;
			attribute<bool> ExtraGebruikRestWarmte (PlanRegio) := GebruikOptie && !RestWarmteStartingState/GebruikRestWarmte;

			unit<uint32> StartArcSet         := RestWarmteStartingState/CumulatedArcSet;
			unit<uint32> LastCumulatedArcSet := LastState/CumulatedArcSet;

			unit<uint32> ExtraArcSet := SubSet(ExtraGebruikRestWarmte)
			{
				attribute<PlanRegio>           PlanRegio_rel           := nr_OrgEntity;
				attribute<LastCumulatedArcSet> LastCumulatedArcSet_rel := rlookup(PlanRegio_rel, LastCumulatedArcSet/PlanRegio_rel);
			}

			unit<uint32> CumulatedArcSet := union_unit(StartArcSet, ExtraArcSet), DialogType = "Map", DialogData = "CumulatedArcs"
			{
				attribute<rdc_meter> BronPoint     := union_data(., StartArcSet/BronPoint, LastCumulatedArcSet/BronPoint[ExtraArcSet/LastCumulatedArcSet_rel]), DisableStorage = "True";
				attribute<PlanRegio> PlanRegio_rel := union_data(., StartArcSet/PlanRegio_rel, ExtraArcSet/PlanRegio_rel);

				parameter<bool> HasArcs := true, DisableStorage = "True";
			}

			attribute<rdc_meter> CumulatedArcs (CumulatedArcSet, arc) := union_data(CumulatedArcSet, RestWarmteStartingState/CumulatedArcs, LastState/CumulatedArcs[ExtraArcSet/LastCumulatedArcSet_rel])
			{
				parameter<uint32> PenColor := rgb(0,0,0), DialogType = "PenColor";
				parameter<int16>  PenWidth := 2s        , DialogType = "PenWidth";
			}
		}
		attribute<bool>                         WasAllocated             (PlanRegio) := BeginStatus/PlanRegioWarmteAllocatie_REMOVE == Classifications/GebiedsOptie/V/RestWarmte;
		attribute<bool>                         Alloc_OK                 (PlanRegio) := GebruikRestWarmteMetBron || !WasAllocated; // it never stops.
		attribute<Classifications/GebiedsOptie> PlanRegioWarmteAllocatie_REMOVE (PlanRegio) := 
			GebruikRestWarmteMetBron
				? Classifications/GebiedsOptie/V/RestWarmte
				: BeginStatus/PlanRegioWarmteAllocatie_REMOVE
		,	IntegrityCheck = "Alloc_OK";
	}
}
