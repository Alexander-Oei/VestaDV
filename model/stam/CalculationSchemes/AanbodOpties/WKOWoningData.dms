//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2019 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

template WKOWoningData
{
	// begin case parameters
	container BCdata;              // := BebouwingsComponenten/Woning;
	parameter<Eur_GJ> WarmtePrijs; // := BeginStatus/KostenBaten/PlanRegioKaarten/Woning/WarmtePrijs;
	unit<uint8> Type;
	container KostenBase;
	parameter<bool> KoudeBeschikbaar;
	
	// end case parameters

	unit<uint32> BebouwingsObject := BCdata/BebouwingsObject;

	attribute<bool>       IsAppt(BebouwingsObject) := Type/IsMeergezins[BCdata/RuimtelijkeVraagComponent/BebouwingsType];

	attribute<NrWoningen> nr_won(BebouwingsObject) := BCdata/WarmteVraag/deelnemers/nrAansluitingen * 1[NrWoningen / NrAansl], Descr = "Aantal woningen"; //+ Vraag/NieuwbouwWoning/Totaal/Warmte
	attribute<GJ_yr> V_RV       (BebouwingsObject) := BCdata/WarmteVraag/deelnemers/Verbruik_RV, Source = "FO v7a F 54"; // @@@ is Thermische Vraag
	attribute<GJ_yr> V_WW       (BebouwingsObject) := BCdata/WarmteVraag/deelnemers/Verbruik_WW, Source = "FO v7a F 54"; // @@@ is Thermische Vraag
	attribute<GJ_yr> V_Warmte   (BebouwingsObject) := V_RV + V_WW;
	attribute<GJ_yr> V_Koude    (BebouwingsObject) := =KoudeBeschikbaar ?'7.2[GJ_yr / nrWoningen] * nr_won' : 'const(0[GJ_Yr], BebouwingsObject)', Source = "FO v7a F 54"; // @@@ is Thermische Vraag
//	attribute<GJ_yr> F_Elek     (BebouwingsObject) := (V_Warmte + V_Koude), Source = "FO v7a F 54"; // @@@ is Thermische Vraag
	
	attribute<float64>    SPF_ind_rv  (BebouwingsObject) := 
		BcData/energielabel/CurrValue <= /Classifications/energielabel/V/Aplus  ? constanten/SPF_ind_A_rv :
		BcData/energielabel/CurrValue <= /Classifications/energielabel/V/LabelB ? constanten/SPF_ind_B_rv :
		BcData/energielabel/CurrValue <= /Classifications/energielabel/V/LabelE ? constanten/SPF_ind_CE_rv : 1.0, Descr = "Efficientie van de individuele warmtepomp voor eigen ruimteverwarming";
		
	attribute<float64>    AEO_ind_rv  (BebouwingsObject) := 
		BcData/energielabel/CurrValue <= /Classifications/energielabel/V/Aplus  ? constanten/AEO_ind_A_rv :
		BcData/energielabel/CurrValue <= /Classifications/energielabel/V/LabelB ? constanten/AEO_ind_B_rv :
		BcData/energielabel/CurrValue <= /Classifications/energielabel/V/LabelE ? constanten/AEO_ind_CE_rv : 1.0, Descr = "Aandeel eigen opwekking, afhankelijk van schillabel";
	
	//volumevraag gebouwzijde
	attribute<GJ_yr>	V_RV_ind		(BebouwingsObject)	:= V_RV * AEO_ind_rv,						Descr = "Aandeel van de eigen volumevraag naar ruimteverwarming dat in het gebouw wordt opgewekt";
	attribute<GJ_yr>	V_WW_ind		(BebouwingsObject)	:= V_WW * constanten/AEO_ind_ww,			Descr = "Aandeel van de eigen volumevraag naar warm tapwater dat in het gebouw wordt opgewekt";
	attribute<GJ_yr>	V_Koude_ind		(BebouwingsObject)	:= V_Koude * constanten/AEO_ind_K,			Descr = "Aandeel van de eigen volumevraag naar koude dat in het gebouw wordt opgewekt";
	
	attribute<GJ_yr>	V_Elek_RW_ind	(BebouwingsObject)	:= V_RV_ind / SPF_ind_rv,					Descr = "Elektriciteitsverbruik voor eigen opwekking tbv ruimteverwarming";
	attribute<GJ_yr>	V_Elek_WW_ind	(BebouwingsObject)	:= V_WW_ind / constanten/SPF_ind_ww,		Descr = "Elektriciteitsverbruik voor eigen opwekking tbv warm tapwater";
	attribute<GJ_yr>	V_ElekW_ind		(BebouwingsObject)	:= V_Elek_RW_ind + V_Elek_WW_ind,			Descr = "Elektriciteitsverbruik voor eigen opwekking tbv zowel ruimteverwarming als warm tapwater";
	attribute<GJ_yr>	V_ElekK_ind		(BebouwingsObject)	:= V_Koude_ind / constanten/SPF_ind_k,		Descr = "Elektriciteitsverbruik voor eigen opwekking tbv koude"; // tbv WKO: TODO: Koude specifieke SPF checken en evt toevoegen.
	
	attribute<GJ_yr>	V_RV_sec		(BebouwingsObject)	:= V_RV - V_RV_ind,							Descr = "Aandeel van de eigen volumevraag naar ruimteverwarming geleverd uit het secundaire net";
	attribute<GJ_yr>	V_WW_sec		(BebouwingsObject)	:= V_WW - V_WW_ind,							Descr = "Aandeel van de eigen volumevraag naar warm tapwater geleverd uit het secundaire net";
	attribute<GJ_yr>	V_Warmte_sec	(BebouwingsObject)	:= V_RV_sec + V_WW_sec,						Descr = "Aandeel van de eigen volumevraag naar warmte uit het secundaire";
	attribute<GJ_yr>	V_Koude_sec		(BebouwingsObject)	:= V_Koude - V_Koude_ind,					Descr = "Aandeel van de eigen volumevraag naar koude uit het secundaire net";
	
	//volumevraag jaarlijks aan het secundaire net (inclusief verrekening leidingverlies)
	attribute<GJ_yr>	V_Wsec_netto	(BebouwingsObject)	:= V_Warmte_sec / (1d - constanten/Leidingverlies),		Descr ="Warmte te leveren aan het secundaire net met verrekening leidingverlies";
	attribute<GJ_yr>	V_Ksec_netto	(BebouwingsObject)	:= V_Koude_sec / (1d - constanten/Leidingverlies),		Descr ="Koude te leveren aan het secundaire net met verrekening leidingverlies";
	
	//gelijktijdige capaciteitsvraag aan het secundaire net
	attribute<kW>		P_Warmte_sec	(BebouwingsObject)	:= constanten/GTF_woning_w * (IsAppt ? constanten/ASW_wa : constanten/ASW_wc) * nr_won; // TODO: afhankelijk van label maken. en gelijktijdigheidsfactor aanpassen indien ASW scherper gemodelleerd worden.
	attribute<kW>		P_Koude_sec		(BebouwingsObject)	:= KoudeBeschikbaar ? (constanten/GTF_woning_k * (IsAppt ? constanten/ASW_ka : constanten/ASW_kc) * nr_won) : 0[kW];
	attribute<kW>		P_sec			(BebouwingsObject)	:= max_elem(P_Warmte_sec, P_Koude_sec),		Descr = "capaciteitsvraag aan secundair net";
	
	//elektriciteitsverbruik
	attribute<GJ_yr>	V_ElekW_ow		(BebouwingsObject)	:= V_Wsec_netto / constanten/SPF_coll_w,	Descr = "Elektriciteitsverbruik van de collectieve warmtepomp voor warmteproductie";
	attribute<GJ_yr>	V_ElekK_ow		(BebouwingsObject)	:= V_Ksec_netto / constanten/SPF_coll_k,	Descr = "Elektriciteitsverbruik van de collectieve warmtepomp voor koudeproductie";
	attribute<GJ_yr>	V_Elek_ind		(BebouwingsObject)	:= V_ElekW_ind + V_ElekK_ind,				Descr = "Totaal elektriciteitsverbruik door afnemer";
	attribute<GJ_yr>	V_Elek_ow		(BebouwingsObject)	:= V_ElekW_ow + V_ElekK_ow,					Descr = "Totaal elektriciteitsverbruik door opwekker";

	attribute<GJ_yr> V_Warmte_ow		(BebouwingsObject)	:= V_Wsec_netto - V_ElekW_ow; // warmte geleverd aan centrale eWP door opwekker tbv warmtelevering aan woning
	attribute<GJ_yr> V_Koude_ow		(BebouwingsObject)	:= V_Ksec_netto  + V_ElekK_ow; // warmte onttrokken aan cenrale eWP door opwekker tbv koude levering aan woning
	attribute<GJ_yr> V_WKO		(BebouwingsObject) := V_Warmte_ow - V_Koude_ow; // netto aan de bodem of andere leverancier onttrokken warmte.

	attribute<classifications/gebruiksgrootteklasse> ind_gebruiksgrootteklasse_rel(BebouwingsObject) := classify(V_ElekW_ind, Prijzen_elec/ClassBreak);
	parameter<classifications/gebruiksgrootteklasse> ow_gebruiksgrootteklasse_rel := last(ID(Prijzen_elec));

	container Kosten
	{
		//Investeringskosten binnen gebouw
		attribute<Eur_woning>	Ki_inpandig			(BebouwingsObject)	:= IsAppt ? constanten/Ki_inpandig : 0 [Eur_woning];
		attribute<Eur_woning>	Ki_afleverset		(BebouwingsObject)	:= const(constanten/Ki_afleverset, BebouwingsObject);
		attribute<Eur>			Ki_wv				(BebouwingsObject)	:= (Ki_inpandig + Ki_afleverset) * nr_won;
		attribute<float64>		Pand_aandeel		(BebouwingsObject)	:= IsAppt ? (Schuiven/VerketelingMax * BebouwingsObject/pand_aandeel +  Schuiven/VerketelingMin) : 1.0;
		attribute<Eur>			Ki_eWP				(BebouwingsObject)	:= BcData/Sprongen/VerbeterKosten_i/eWP * nr_won * Pand_aandeel;
		attribute<Eur>			Ki_EigenOpwekking	(BebouwingsObject)	:= 
			(BcData/energielabel/CurrValue <= Classifications/energielabel/V/Aplus  ) ? ((T_sec <= 55[Celsius]) ? constanten/Ki_Booster : 0.0[eur]) :
			(BcData/energielabel/CurrValue <= Classifications/energielabel/V/LabelB ) ? ((T_sec <= 35[Celsius]) ? Ki_eWP : (T_sec <= 55[Celsius]) ? constanten/Ki_booster : 0.0[eur]) :
			(BcData/energielabel/CurrValue <= Classifications/energielabel/V/LabelE ) ? ((T_sec <= 55[Celsius]) ? Ki_eWP : 0.0[eur]) 
			: 1000000.0[eur];
		attribute<Eur>			Ki_LTAS				(BebouwingsObject)	:= BCdata/Sprongen/VerbeterKosten_i/LTAS * nr_won;			
		attribute<Eur>			Ki_id				(BebouwingsObject)	:= Ki_LTAS + Ki_wv + Ki_EigenOpwekking,	Descr = "Investeringskosten systeem bij woningen en appartementen, F 54..56";
		
		//Investeringskosten vergoedingen gebouweigenaar
		attribute<Eur>			Ki_ge_pm			(BebouwingsObject)	:= KostenBase/Ki_ge_pm_o,				Descr = "Investeringskosten vergoeding projectmanagement aan gebouweigenaar";
		attribute<Eur>			Ki_ge_ov			(BebouwingsObject)	:= KostenBase/Ki_ge_ov_o,				Descr = "Investeringskosten vergoeding ongerief aan gebouweigenaar";
		attribute<Eur>			Ki_ge				(BebouwingsObject)	:= Ki_ge_ov + Ki_ge_pm,					Descr = "Investeringskosten vergoedingen aan gebouweigenaar";

		//investeringskosten wijkdistributie gebouwgerelateerd, zonder distributieleidingen
		attribute<Eur>			Ki_wd_OS			(BebouwingsObject)	:= constanten/Ki_OnderStation [Eur_KW] * P_sec [KW], Descr = "Investeringskosten onderstations";
		attribute<Eur>			Ki_wd_aansluiting	(BebouwingsObject)	:= (IsAppt ? constanten/Ki_aansl_meergezins : constanten/Ki_aansl_grondgeb) * nr_won, Descr = "Investeringskosten aansluitleidingen";
		attribute<Eur>			Ki_wd				(BebouwingsObject)	:= Ki_wd_OS + Ki_wd_aansluiting,		Descr = "Investeringskosten wijkdistributie exclusief distributieleidingen";
		
		//Jaarlijkse kosten elektriciteitsverbruik
		attribute<Eur_yr>		Kj_elek_ind			(BebouwingsObject)	:= V_Elek_ind * Prijzen_elec/KGJ_eindgebruik_excl[ind_gebruiksgrootteklasse_rel],	Source = "FO v7a p 82 F 57",	Descr = "jaarlijkse elektriciteitskosten individueel eindgebruiker";
		attribute<Eur_yr>		Km_elek_ind			(BebouwingsObject)	:= V_Elek_ind * Prijzen_elec/KGJ_maatschappelijk [ind_gebruiksgrootteklasse_rel],	Source = "FO C6 Tabel 6 WKO",	Descr = "jaarlijkse elektriciteitskosten individueel maatschappelijk";
		attribute<Eur_yr>		Kj_elek_ow			(BebouwingsObject)	:= V_Elek_ow * Prijzen_elec/KGJ_eindgebruik_excl[ow_gebruiksgrootteklasse_rel],		Source = "FO v7a p 82 F 57",	Descr = "jaarlijkse elektriciteitskosten collectief eindgebruiker";
		attribute<Eur_yr>		Km_elek_ow			(BebouwingsObject)	:= V_Elek_ow * Prijzen_elec/KGJ_maatschappelijk [ow_gebruiksgrootteklasse_rel],		Source = "FO C6 Tabel 6 WKO",	Descr = "jaarlijkse elektriciteitskosten collectief maatschappelijk";	
		attribute<Eur_yr>		Kj_elek				(BebouwingsObject)	:= Kj_elek_ind + Kj_elek_ow,			Descr = "jaarlijkse kosten elektriciteitsverbruik eindgebruiker";
		attribute<Eur_yr>		Km_elek				(BebouwingsObject)	:= Km_elek_ind + Km_elek_ow,			Descr = "jaarlijkse kosten elektriciteitsverbruik maatschappelijk";
		
		//energieheffing en co2prijs
		attribute<Eur_yr>		KEH_elek_ind		(BebouwingsObject)	:= V_Elek_ind * Prijzen_elec/KGJ_EnergieHeffing[ind_gebruiksgrootteklasse_rel],	Descr = "energieheffing over elektriciteitsverbruik individueel";
		attribute<Eur_yr>		KCO2_elek_ind		(BebouwingsObject)	:= V_Elek_ind * Prijzen_elec/KGJ_CO2Heffing[ind_gebruiksgrootteklasse_rel],		Descr = "co2heffing over elektriciteitsverbruik individueel";
		attribute<Eur_yr>		KEH_elek_ow			(BebouwingsObject)	:= V_Elek_ow * Prijzen_elec/KGJ_EnergieHeffing[ow_gebruiksgrootteklasse_rel],	Descr = "energieheffing over elektriciteitsverbruik collectief";
		attribute<Eur_yr>		KCO2_elek_ow		(BebouwingsObject)	:= V_Elek_ow * Prijzen_elec/KGJ_CO2Heffing[ow_gebruiksgrootteklasse_rel],		Descr = "co2heffing over elektriciteitsverbruik collectief";
		attribute<Eur_yr>		KEH_elek			(BebouwingsObject)	:= KEH_elek_ind  + KEH_elek_ow,			Descr = "totaal energieheffing over elektriciteitsverbruik";
		attribute<Eur_yr>		KCO2_elek			(BebouwingsObject)	:= KCO2_elek_ind + KCO2_elek_ow,		Descr = "totaal co2heffing over elektriciteitsverbruik";		
	}

	container Opbrengsten
	{
		attribute<Eur_yr> Warmte           (BebouwingsObject) := V_Warmte * WarmtePrijs - Kosten/Kj_elek_ind, Source = "F 66 jo F 8";
		attribute<Eur_yr> koude            (BebouwingsObject) := 250 [ Eur_yr / NrWoningen] * nr_won, Source = "F 63";

		attribute<Eur>    AansluitBijdrage (BebouwingsObject) := BCdata/Opbrengsten/AansluitBijdrageD;
		attribute<Eur_yr> Vastrecht        (BebouwingsObject) := BCdata/Opbrengsten/VastrechtBijdrageD, Source = "F 60+64 jo F 6";
		attribute<Eur_yr> Om_comfort       (BebouwingsObject) := Koude;
	}
}