//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                                  (C) VESTA 2018                                      //
//        Het doel van het model is om beleidsinformatie te genereren met betrekking    //
//        tot de energievoorziening van de gebouwde omgeving.                           //
//                                                                                      //
//                         Planbureau voor de Leefomgeving                              //
//                                    JUNI 2018                                         //
//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//     * In het geval van externe publicatie van onderzoeksresultaten waarbij gebruik   //
//       is gemaakt van de verstrekte gegevens dient de gebruiker deze resultaten aan   //
//       het PBL mede te delen middels de toezending van 2 exemplaren van de betref-    //
//       fende publicatie aan het hoofd van het team ROL van het PBL.                   //
//                                                                                      //
//     * In elke publicatie waarin de gegevens van het PBL in enige vorm zijn opgenomen,//
//       zal de gebruiker de vermelding opnemen: "Bron: Planbureau voor de Leefomgeving"//
//       Indien het een bewerking van de gegevens betreft, dient te worden opgenomen:   //
//       "Gebaseerd op Planbureau voor de Leefomgeving gegevens".                       //
//                                                                                      //
//     * Het PBL is onder geen enkele voorwaarde aansprakelijk tegenover gebruiker voor //
//       enige schade, mede begrepen winstderving, verlies van gegevens, schade         //
//       tengevolge van fouten of onjuistheden in gegevens, schade tengevolge van fouten//
//       of onvolkomenheden in de documentatie of andere incidentele of gevolgschade    //
//       welke voortvloeit uit of in verband staat met gebruik of het niet kunnen ge-   //
//       bruiken van gegevens.                                                          //
//                                                                                      //
//     * Het PBL staat niet in voor de juistheid van de resultaten die worden verkregen //
//       door combinatie van diverse, op zichzelf staande PBL-gegevens onderling,       //
//       of met die uit andere bronnen.                                                 //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

template EnergieKengetallenImport
{
	parameter<string> FileName;
	parameter<string> ModelType_str_name;

	unit<uint8> model_type;
	unit<uint8> bouwjaarindeling;

	unit<float64> model_eenheid;
	unit<float64> koppel_eenheid;

// attribute<bool> Actief(domein) := const(true, domein);

	unit<float64> GJ_yr_per_eenheid := (units/GJ / Units/Yr) / model_eenheid;
	unit<float64> kW_per_eenheid    := units/kW      / model_eenheid; // capaciteit
	unit<float64> kWh_per_eenheid   := units/kWh     / model_eenheid;
	unit<float64> Eur_per_eenheid   := units/Eur     / model_eenheid;
	unit<float64> m2_per_eenheid    := units/m2      / model_eenheid;
	unit<float64> MU_per_JU         := model_eenheid / koppel_eenheid;

	unit<uint32> Table
	:	StorageName     = "='%EnergieDataProjDir%/'+ FileName"
	,	StorageType     = "gdal.vect"
	,	StorageReadOnly = "True"
	,	SyncMode        = "All"
	,	IsHidden        = "True"
	{
		attribute<string> BouwJaar_str;
		attribute<string> ModelType_str := =ModelType_str_name;

		attribute<string> Label     := trim(ModelType_str) +  ', ' + trim(BouwJaar_str), DialogType = "LabelText";
		parameter<string> LabelList := lowercase(AsItemList(Label));
	}

	unit<uint32> TableColumns := SubItem_PropValues(table, 'name');
	container Results : FreeData = "False"
	{
		unit<uint32>  ModelObject := Table;
		unit<float64> ModelUnit   := model_eenheid;
		unit<float64> JoinedUnit  := koppel_eenheid;
		
		attribute<MU_per_JU> nrMUperJU(ModelObject) := const(1.0[ModelUnit / JoinedUnit], Table);
		
		attribute<bouwjaarindeling> BouwJaar       (ModelObject) := rlookup(trim(Table/BouwJaar_str ), bouwjaarindeling/excel_code);
		attribute<model_type>       ModelType      (ModelObject) := rlookup(trim(Table/ModelType_str), model_type/Excel_code);
		attribute<uint8>            ModelObjectKey (ModelObject) := ModelType *  uint8(NrOfRows(bouwjaarindeling)) + BouwJaar;

		container Functioneel
		{
			attribute<GJ_yr_per_eenheid> RuimteVerwarming      (ModelObject) := value(table/V_RV,       GJ_yr_per_eenheid);
			attribute<GJ_yr_M2>          Pompenergie_M2        (ModelObject) :=
				= IsDefined(rlookup('V_P_ELEK', TableColumns/name))
					? 'MakeDefined(value(table/V_P_ELEK,  Ratio), 0.0[GJ_yr_M2])'
					: 'const(0.0[GJ_yr_M2], ModelObject)';
			attribute<GJ_yr_per_eenheid> Pompenergie_min       (ModelObject) := Pompenergie_M2 * area/min;
			attribute<GJ_yr_per_eenheid> Pompenergie_max       (ModelObject) := Pompenergie_M2 * area/max;
			attribute<GJ_yr_per_eenheid> Koude                 (ModelObject) := value(table/V_KOU,      GJ_yr_per_eenheid);
			attribute<GJ_yr_per_eenheid> WarmWater             (ModelObject) := value(table/V_WW,       GJ_yr_per_eenheid);
			attribute<GJ_yr_per_eenheid> ElektrischeApparatuur (ModelObject) := value(table/V_APP_ELEK, GJ_yr_per_eenheid);
		}
		container Efficiency
		{
			attribute<Classifications/EnergieLabel> EnergieLabel(ModelObject) :=
				results/energie/Label == 'A'
					? Classifications/EnergieLabel/V/LabelB
					: rlookup(results/energie/Label, Classifications/EnergieLabel/Label);

			attribute<Ratio> Gebouw            (ModelObject) := value(table/EG, Ratio);

			container RuimteVerwarming
			{
				attribute<Ratio> AardGas       (ModelObject) := MakeDefined(value(table/E_RV_Gas,  Ratio), value(1.0, Ratio));
			}
			container Koude
			{
				attribute<Ratio> Elektriciteit (ModelObject) := MakeDefined(value(table/E_KOU_ELEK, Ratio), 1.0[Ratio]), Descr = "Efficiency Airco";
			}
			container WarmWater
			{
				attribute<Ratio> AardGas       (ModelObject) := MakeDefined(value(table/E_WW_GAS,  Ratio), 1.0[Ratio]);
			}
			container Apparatuur
			{
				attribute<Ratio> Elektriciteit (ModelObject):= MakeDefined(value(table/E_APP_Elek, Ratio), value(1.0, Ratio));
			}
		}
		container AansluitCapaciteit
		{
			attribute<kW_per_eenheid> Warmte(ModelObject) := value(table/AC_WARM, kW_per_eenheid);
		}
		container Energie
		{
			attribute<float64> Index (ModelObject) := float64(table/EnergieIndex);
			attribute<string>  Label (ModelObject) := string(table/EnergieLabel);
		}
		container Verbeteringen
		{
			container RuimteVerwarming
			{
				attribute<Ratio> efficiency (ModelObject) := MakeDefined(table/MV_RV[Ratio], 1.07[Ratio]);
			}
			container LTAS
			{
				attribute<Eur_per_eenheid> kosten_min(ModelObject) := = IsDefined(rlookup('K_LTAS_MIN', TableColumns/name)) ? 'value(table/K_LTAS_MIN, Eur_per_eenheid)' : 'const(0[Eur_per_eenheid], ModelObject)';
				attribute<Eur_per_eenheid> kosten_max(ModelObject) := = IsDefined(rlookup('K_LTAS_MAX', TableColumns/name)) ? 'value(table/K_LTAS_MAX, Eur_per_eenheid)' : 'const(0[Eur_per_eenheid], ModelObject)';
			}
		}
		container area
		{
			attribute<m2_per_eenheid> min (ModelObject) := = IsDefined(rlookup('VL_OPP_WON_MIN', TableColumns/name)) ? 'value(table/VL_OPP_WON_MIN, m2_per_eenheid)' : 'const(1.0, ModelObject)';
			attribute<m2_per_eenheid> max (ModelObject) := = IsDefined(rlookup('VL_OPP_WON_MAX', TableColumns/name)) ? 'value(table/VL_OPP_WON_MAX, m2_per_eenheid)' : 'const(1.0, ModelObject)';
			attribute<m2_per_eenheid> gem (ModelObject) := 0.5 * (min+max);
		}
	}
}