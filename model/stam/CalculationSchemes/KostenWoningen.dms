//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                                  (C) VESTA 2018                                      //
//        Het doel van het model is om beleidsinformatie te genereren met betrekking    //
//        tot de energievoorziening van de gebouwde omgeving.                           //
//                                                                                      //
//                         Planbureau voor de Leefomgeving                              //
//                                    JUNI 2018                                         //
//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//     * In het geval van externe publicatie van onderzoeksresultaten waarbij gebruik   //
//       is gemaakt van de verstrekte gegevens dient de gebruiker deze resultaten aan   //
//       het PBL mede te delen middels de toezending van 2 exemplaren van de betref-    //
//       fende publicatie aan het hoofd van het team ROL van het PBL.                   //
//                                                                                      //
//     * In elke publicatie waarin de gegevens van het PBL in enige vorm zijn opgenomen,//
//       zal de gebruiker de vermelding opnemen: "Bron: Planbureau voor de Leefomgeving"//
//       Indien het een bewerking van de gegevens betreft, dient te worden opgenomen:   //
//       "Gebaseerd op Planbureau voor de Leefomgeving gegevens".                       //
//                                                                                      //
//     * Het PBL is onder geen enkele voorwaarde aansprakelijk tegenover gebruiker voor //
//       enige schade, mede begrepen winstderving, verlies van gegevens, schade         //
//       tengevolge van fouten of onjuistheden in gegevens, schade tengevolge van fouten//
//       of onvolkomenheden in de documentatie of andere incidentele of gevolgschade    //
//       welke voortvloeit uit of in verband staat met gebruik of het niet kunnen ge-   //
//       bruiken van gegevens.                                                          //
//                                                                                      //
//     * Het PBL staat niet in voor de juistheid van de resultaten die worden verkregen //
//       door combinatie van diverse, op zichzelf staande PBL-gegevens onderling,       //
//       of met die uit andere bronnen.                                                 //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

template KostenWoningen
{
	// begin case parameter
	container       BCdata;
	unit<uint32>    PlanRegio;
	parameter<bool> IsNieuwbouw;
	// end case parameters

	unit<uint32>    WoonObject := BCdata/BebouwingsObject;

	attribute<bool>      IsMeergezins (WoonObject) := BCdata/BebouwingsTypeDomein/IsMeergezins[BCdata/BebouwingsType];
	attribute<Eur_Aansl> Ki_wd_w      (WoonObject) := IsMeergezins ? Kaansluiten_wd_gestappeld : Kaansluiten_wd_grondgebonden;

	parameter<Eur_Aansl> Oi_vermeden_gasketel := Ki_HR_ketel * NCW/bw15/AnnualisationFactor * NCW/bw30/DurationFactor, Source = "FO F84";

	// eenmalige kosten, zie FO tabellen 5..7
	attribute<Eur_Aansl> Ki_id_fa (WoonObject) := IsMeergezins ? Schuiven/VerketelingMin * 1800[Eur_Aansl] + Schuiven/VerketelingMax * 5300[Eur_Aansl] :  0[Eur_Aansl]; // fysieke aansluitmaatregelen  // ?????
	parameter<Eur_Aansl> Ki_id_va              := 0[Eur_Aansl] * float64(!IsNieuwbouw);

	// kosten voor projectmanagement en door huurder ervaren en door eigenaar vergoedde ongerief
	attribute<Eur_Aansl> Ki_ge_ov_w   (WoonObject) := =IsNieuwbouw ? 'const(0[Eur_Aansl], WoonObject)' : 'IsMeergezins ? Schuiven/VerketelingMin *                    0[Eur_Aansl] + Schuiven/VerketelingMax * Ongeriefsvergoeding             : Ongeriefsvergoeding';             // woon-ongerief
	attribute<Eur_Aansl> Ki_ge_pm_w   (WoonObject) := =IsNieuwbouw ? 'const(0[Eur_Aansl], WoonObject)' : 'IsMeergezins ? Schuiven/VerketelingMin * Projectmanagementvergoeding_min + Schuiven/VerketelingMax * Projectmanagementvergoeding_max : Projectmanagementvergoeding_max'; // project management
	attribute<Eur>       Ki_ge_ov_o   (WoonObject) := =IsNieuwbouw ? 'const(0[Eur], WoonObject)'       : 'NrDeelnemers * Ki_ge_ov_w';
	attribute<Eur>       Ki_ge_pm_o   (WoonObject) := =IsNieuwbouw ? 'const(0[Eur], WoonObject)'       : 'NrDeelnemers * Ki_ge_pm_w';

	// huurverlaging, alleen gebruikt in Payments en als kostenfactor
	attribute<Eur_yr_Aansl> Kj_hv_w   (WoonObject) := =IsNieuwbouw ? 'const(0[Eur_yr_Aansl], WoonObject)' : '(IsMeergezins ? Schuiven/VerketelingMax : 1.0) * 160[Eur_yr_Aansl] * Schuiven/HuurVerlagingBijGebiedsOptie';// ?????
	attribute<Eur_yr>       Kj_hv_o   (WoonObject) := =IsNieuwbouw ? 'const(0[Eur_yr      ], WoonObject)' : 'Kj_hv_w* NrDeelnemers';

	attribute<nrAansl> NrDeelnemers   (WoonObject) := BCdata/WarmteVraag/deelnemers/nrAansluitingen    * float64(!IsDefined(BCdata/WarmteAllocatie));
	attribute<kW>      Capaciteit     (WoonObject) := BCdata/WarmteVraag/deelnemers/AansluitCapaciteit * float64(!IsDefined(BCdata/WarmteAllocatie));

	attribute<Eur>     Ki_ge_bruto    (WoonObject) := Ki_ge_ov_o + Ki_ge_pm_o - Oi_vermeden_gasketel * NrDeelnemers;
	attribute<Eur>     Oi_ge_subsidie (WoonObject) := Ki_ge_ov_o * SpecifiekeInstellingen/Beleid/EIA/E_bw/Rs_OngeriefsVergoeding + Ki_ge_pm_o * SpecifiekeInstellingen/Beleid/EIA/E_bw/Rs_ProjectManagement;
	attribute<Eur>     Ki_ge_netto    (WoonObject) := Ki_ge_bruto - Oi_ge_subsidie;
	attribute<Eur_yr>  Kj_ge_base     (WoonObject) := Ki_ge_netto * NCW/bw28/AnnualisationFactor + Kj_hv_o;
	attribute<Eur_yr>  Oe_ge_subsidie (WoonObject) := Kj_ge_base * SpecifiekeInstellingen/Beleid/EEA/ge;
	attribute<Eur_yr>  Oj_ge_subsidie (WoonObject) := Oi_ge_subsidie *NCW/bw28/AnnualisationFactor + Oe_ge_subsidie;
	attribute<Eur_yr>  Kj_ge_netto    (WoonObject) := Kj_ge_base - Oe_ge_subsidie;

	container Onderstation
	{
		attribute<kW> GevraagdeCapaciteit                (PlanRegio) := woning_os_factor * sum(Capaciteit, BCdata/PlanRegio_rel);
		attribute<kW> GevraagdeCapaciteit_blokverwarming (PlanRegio) := float64(!IsNieuwbouw) * 
			woning_os_factor * sum(Capaciteit * (IsMeergezins ? Schuiven/VerketelingMin : 0.0), BCdata/PlanRegio_rel);

		attribute<float64>  n0    (PlanRegio) := GevraagdeCapaciteit / Vermogen_OS, Source = "FO v7a F 23";
		attribute<float64>  n2    (PlanRegio) := GevraagdeCapaciteit_blokverwarming / Vermogen_OS, Descr = "F 28";
		attribute<float64>  n1    (PlanRegio) := n0 - n2;
		attribute<Eur>      Ki_wd (PlanRegio) := n0 * Kosten_OS + n2 *KetelVergoeding, Descr = "F 27";
	}

	attribute<Eur> Ki_wd_sec     (PlanRegio) := sum(Ki_wd_w*NrDeelnemers, BCdata/PlanRegio_rel);
	attribute<Eur> Ki_wd         (PlanRegio) := Ki_wd_sec + Onderstation/Ki_wd;
	attribute<Eur> Ki_id         (PlanRegio) := sum((Ki_id_fa + Ki_id_va) * NrDeelnemers, BCdata/PlanRegio_rel);
	attribute<Eur> Ki_ge_ov      (PlanRegio) := sum(Ki_ge_ov_w * NrDeelnemers, BCdata/PlanRegio_rel) * float64(!IsNieuwbouw);
	attribute<Eur> Ki_ge_pm      (PlanRegio) := sum(Ki_ge_pm_w * NrDeelnemers, BCdata/PlanRegio_rel) * float64(!IsNieuwbouw);

	attribute<Eur_yr> Kj_hv      (PlanRegio) :=  =IsNieuwbouw?'const(0[Eur_yr      ], PlanRegio)':'sum(Kj_hv_o, BCdata/PlanRegio_rel)'; // huurverlaging, alleen gebruikt in flow en als kostenfactor

	attribute<Eur_yr> Kj_wd_base (PlanRegio) := Ki_wd_sec * Kengetallen/Onderhoud/Rj_wd + Onderstation/Ki_wd * Kengetallen/Onderhoud/Rj_Onderstation;
	attribute<Eur_yr> Kj_id_base (PlanRegio) := Ki_id * Kengetallen/Onderhoud/Rj_id;
}