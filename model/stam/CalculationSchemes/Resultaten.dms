//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2019 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

template Resultaten
{
	// begin case parameters
	parameter<String> RekenstapName;
	parameter<String> ZichtjaarName;
	// optional case parameter

	container TussenResultaten := ='/TussenResultaten/' + RekenstapName;
	unit<uint32> PlanRegio     := Invoer/SpecifiekeInstellingen/PlanRegio;

	container Prijzen               := TussenResultaten/Prijzen;
	container BebouwingsComponenten := TussenResultaten/BebouwingsComponenten;
	container NCW                   := TussenResultaten/NCW;
	container GebiedsAllocatie      := TussenResultaten/GebiedsAllocatie;
	container StateNaAllocatie      := TussenResultaten/StateNaAllocatie;
	container Voorkeuren            := TussenResultaten/Voorkeuren;

	container Allocatie             := TussenResultaten/Allocatie;
	parameter<Classifications/zichtjaar> ZichtJaar_rel  := rlookup(ZichtJaarName, Classifications/zichtjaar/name);
	parameter<units/yr_uint16>           ZichtJaar_jaar := Classifications/zichtjaar/jaar[zichtjaar_rel];

	container RestWamte
	{
		unit<uint32> CumulatedArcSet := GebiedsAllocatie/RestWarmte/Iterations/I0/NextState/CumulatedArcSet, StorageName = "=FolderInfo/OutputFolder+'/RestWarmte.dbf'" //REMOVE , DialogType = "Map", DialogData = "CumulatedArcs"
		{
			attribute<float64>   BronPointX    := MakeDefined(PointCol(BronPoint), 280000.0 / 2.0);
			attribute<float64>   BronPointY    := MakeDefined(PointRow(BronPoint), (300000.0 + 325000.0) / 2.0);
			attribute<PlanRegio> PlanRegio_rel := GebiedsAllocatie/RestWarmte/Iterations/I0/NextState/CumulatedArcSet/PlanRegio_rel;
		}
	}

	#include <Grid100m.dms>

	container CO2: Source = "FO v7a C5"
	{
		container gebouwen
		{
			container Elektriciteit := for_each_nedv(Classifications/BebouwingsComponent/Name, 'sum(Allocatie/'+Classifications/BebouwingsComponent/Name+'/CO2/Elektriciteit, BebouwingsComponenten/'+Classifications/BebouwingsComponent/Name+'/PlanRegio_rel)', PlanRegio, KG_yr)
			{
				attribute<KG_yr> Totaal(PlanRegio) := ='add('+AsItemList(Classifications/BebouwingsComponent/Name)+')';
			}

			container AardGas := for_each_nedv(Classifications/BebouwingsComponent/Name, 'sum(Allocatie/'+Classifications/BebouwingsComponent/Name+'/CO2/AardGas, BebouwingsComponenten/'+Classifications/BebouwingsComponent/Name+'/PlanRegio_rel)', PlanRegio, KG_yr)
			{
				attribute<KG_yr> Totaal(PlanRegio) := ='add('+AsItemList(Classifications/BebouwingsComponent/Name)+')';
			}

			attribute<KG_yr> Totaal(PlanRegio) := Elektriciteit/Totaal + AardGas/Totaal;
		}
		container impl: IsHidden = "True"
		{
			attribute<String> BronExpr (Classifications/AanbodOptie) := 		 
				replace(Classifications/AanbodOptie/IsPlanRegioOptie 
					? 'StateNaAllocatie/PlanRegioWarmteAllocatie == Classifications/AanbodOptie/V/@AO@ ? GebiedsAllocatie/@AO@/CO2   : 0.0[KG_yr]'
					: 'StateNaAllocatie/@AO@/CO2', 
				'@AO@', Classifications/AanbodOptie/name
				);
		}

		container CentraleWarmteBronnen := for_each_nedv(Voorkeuren/WarmteOpties/Name, impl/BronExpr[Voorkeuren/WarmteOpties/AanbodOptie], PlanRegio, KG_yr)
		{
			attribute<KG_yr> Totaal(PlanRegio) := =iif(#Voorkeuren/WarmteOpties=0, 'const(0, PlanRegio, KG_yr)', 'add('+AsItemList(Voorkeuren/WarmteOpties/Name)+')');
		}

		attribute<KG_yr> Totaal(PlanRegio) := Gebouwen/Totaal + CentraleWarmteBronnen/Totaal;
	}

	container Metervraag: Source = "Rapportagebehoefte cijfers per buurt --> Geodan 15 maart 2018"
	{
		container gebouwen
		{
			container GebiedsOptie := for_each_nedv(Classifications/BebouwingsComponent/Name, 'sum(Allocatie/'+Classifications/BebouwingsComponent/Name+'/MeterVraag/GebiedsOptie, BebouwingsComponenten/'+Classifications/BebouwingsComponent/Name+'/PlanRegio_rel)', PlanRegio, GJ_yr)
			{
				attribute<GJ_yr> Totaal (PlanRegio) := ='add('+AsItemList(Classifications/BebouwingsComponent/Name)+')';
			}

			container Elektriciteit := for_each_nedv(Classifications/BebouwingsComponent/Name, 'sum(Allocatie/'+Classifications/BebouwingsComponent/Name+'/MeterVraag/Elektriciteit, BebouwingsComponenten/'+Classifications/BebouwingsComponent/Name+'/PlanRegio_rel)', PlanRegio, GJ_yr)
			{
				attribute<GJ_yr> Totaal (PlanRegio) := ='add('+AsItemList(Classifications/BebouwingsComponent/Name)+')';
			}

			container AardGas := for_each_nedv(Classifications/BebouwingsComponent/Name, 'sum(Allocatie/'+Classifications/BebouwingsComponent/Name+'/MeterVraag/AardGas, BebouwingsComponenten/'+Classifications/BebouwingsComponent/Name+'/PlanRegio_rel)', PlanRegio, GJ_yr)
			{
				attribute<GJ_yr> Totaal (PlanRegio) := ='add('+AsItemList(Classifications/BebouwingsComponent/Name)+')';
			}
		}
	}

	#include<Maatschappelijk.dms>

	container Eindgebruiker :=
			for_each_ne(Classifications/BebouwingsComponent/name,
				replace(
					'Rapportage/EindgebruikerComponent(
						 Allocatie/@BC@
						,NCW/@NC@15
						,NCW/@NC@30
						,NCW/@NC@50
						,sum(Allocatie/@BC@/AardGas/Vastrecht,              BebouwingsComponenten/@BC@/PlanRegio_rel)
						,sum(Allocatie/@BC@/AardGas/AansluitBijdrage,       BebouwingsComponenten/@BC@/PlanRegio_rel)
						,sum(Allocatie/@BC@/Elektriciteit/Vastrecht,        BebouwingsComponenten/@BC@/PlanRegio_rel)
						,sum(Allocatie/@BC@/Elektriciteit/AansluitBijdrage, BebouwingsComponenten/@BC@/PlanRegio_rel)
						,StateNaAllocatie/KostenBaten/PlanRegioKaarten/@BC@
					)'
				,	'@BC@', Classifications/BebouwingsComponent/name
				,	'@NC@', Classifications/BebouwingsComponent/NcwRefBase
				)
			)
		,	Source = "FO v7a C6";

	container EnergieProducent
	{
		// *** Investeringen zijn niet over 30 jaar genomen maar over de voor de betreffende aanbodtechnologie geldende gebruiksperiode en tegen het voor de EP geldende rentevoet
		container impl: IsHidden = "True"
		{
			attribute<String> FlowExpr (Classifications/AanbodOptie) := union_data(Classifications/AanbodOptie,
				'(GebiedsAllocatie/RestWarmte/BeginStatus/KostenBaten/Oj_wd - StateNaAllocatie/Flow/Kj_Netto) * float64(StateNaAllocatie/PlanRegioWarmteAllocatie == Classifications/AanbodOptie/V/RestWarmte)',
				'(GebiedsAllocatie/GeoThermie/BeginStatus/KostenBaten/Oj_wd - StateNaAllocatie/Flow/Kj_Netto) * float64(StateNaAllocatie/PlanRegioWarmteAllocatie == Classifications/AanbodOptie/V/GeoThermie)',
				'(GebiedsAllocatie/WijkWKK/BeginStatus/KostenBaten/Oj_wd    - StateNaAllocatie/Flow/Kj_Netto) * float64(StateNaAllocatie/PlanRegioWarmteAllocatie == Classifications/AanbodOptie/V/WijkWKK)',
				'(GebiedsAllocatie/BioWKK/BeginStatus/KostenBaten/Oj_wd     - StateNaAllocatie/Flow/Kj_Netto) * float64(StateNaAllocatie/PlanRegioWarmteAllocatie == Classifications/AanbodOptie/V/BioWKK)',
				'StateNaAllocatie/WKO/Oj_totaal - StateNaAllocatie/WKO/Kj_Netto'
			);

			attribute<String> StockExpr(Classifications/AanbodOptie) :=
				'StateNaAllocatie/Stock/Ki_Totaal* float64(StateNaAllocatie/PlanRegioWarmteAllocatie == Classifications/AanbodOptie/V/'+Classifications/AanbodOptie/Name+')';
		}

		container CashFlow := for_each_nedv(Voorkeuren/WarmteOpties/Name
			,	impl/FlowExpr[Voorkeuren/WarmteOpties/AanbodOptie]
			,	PlanRegio, Eur_yr) // overrule the following defaults
		{
			attribute<Eur_yr> RestWarmte (PlanRegio):= const(0[Eur_yr], PlanRegio);
			attribute<Eur_yr> GeoThermie (PlanRegio):= const(0[Eur_yr], PlanRegio);
			attribute<Eur_yr> WijkWKK    (PlanRegio):= const(0[Eur_yr], PlanRegio);
			attribute<Eur_yr> WKO        (PlanRegio):= const(0[Eur_yr], PlanRegio);
			attribute<Eur_yr> BioWKK     (PlanRegio):= const(0[Eur_yr], PlanRegio);
			attribute<Eur_yr> Kj_Totaal  (PlanRegio):= RestWarmte + GeoThermie + WijkWKK + WKO + BioWKK;
		}

		attribute<Eur_yr> Kj_Totaal (PlanRegio):= CashFlow/Kj_Totaal;

		container Stock := for_each_nedv(Voorkeuren/WarmteOpties/Name
			,	impl/StockExpr[Voorkeuren/WarmteOpties/AanbodOptie]
			,	PlanRegio, Eur) // overrule the following defaults
		{
			attribute<Eur> RestWarmte (PlanRegio) := const(0[Eur], PlanRegio);
			attribute<Eur> GeoThermie (PlanRegio) := const(0[Eur], PlanRegio);
			attribute<Eur> WijkWKK    (PlanRegio) := const(0[Eur], PlanRegio);
			attribute<Eur> WKO        (PlanRegio) := const(0[Eur], PlanRegio);
			attribute<Eur> BioWKK     (PlanRegio) := const(0[Eur], PlanRegio);
			attribute<Eur> Ki_Totaal  (PlanRegio) := RestWarmte + GeoThermie + WijkWKK + WKO + BioWKK;
		}
		attribute<Eur> Ki_Totaal      (PlanRegio) := Stock/Ki_Totaal; // totale investeringen zonder aftrek van de te ontvangen aansluitbijdrage.
	}

	container netwerken
	{
		attribute<bool> heeft_gas (PlanRegio) := TussenResultaten/Allocatie/heeft_gas;

		attribute<nrWoningen> nrWoonUnits     (Planregio) := ='add('+AsItemList(replace('sum(BebouwingsComponenten/@BC@/BebouwingsObject/nrWoonUnits_i, BebouwingsComponenten/@BC@/PlanRegio_rel)', '@BC@',Classifications/BebouwingsComponent/name) )+')';
		attribute<nrWoningen> nrAllElectric   (Planregio) := ='add('+AsItemList(replace('sum(BebouwingsComponenten/@BC@/BebouwingsObject/nrWoonUnits_i * Allocatie/@BC@/Aandelen/AllElectric, BebouwingsComponenten/@BC@/PlanRegio_rel)', '@BC@',Classifications/BebouwingsComponent/name) )+')';
		attribute<float64>    R_all_electric  (Planregio) := makedefined(nrAllElectric / nrWoonUnits, 0.0);
		attribute<bool >      all_electric    (Planregio) := R_all_electric > 0.999, // tolereer afronding fouten en kleine afwijkingen
			IntegrityCheck = "!(heeft_gas && all_electric)";

		container aardgas
		{
			attribute<bool>   had_gas         (Planregio) := /TussenResultaten/StartJaar/Allocatie/heeft_gas; // false voor bestaande restwarmte
			attribute<bool>   verwijderd      (Planregio) := had_gas && !heeft_gas; // alleen true als hele planregio gasloos is geworden

			// inkomsten
			attribute<Eur_yr> netbijdrage     (Planregio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/name+'/gebouwen/Aardgas/netwerk'  )+')';
			attribute<Eur_yr> vastrecht       (Planregio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/gebouwen/AardGas/Vastrecht')+')';
			attribute<Eur_yr> Aansl_m         (Planregio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/gebouwen/AardGas/Aansl_m'  )+')';
			attribute<Eur_yr> Oj_totaal       (Planregio) := netbijdrage + vastrecht;

			// kosten
			attribute<m>      L_infra_nu      (PlanRegio) := float64(heeft_gas ) * StateNaAllocatie/KostenBaten/KostenD/L_infra_g;
			attribute<m>      L_infra_org     (PlanRegio) := StartJaar/netwerken/aardgas/L_infra_nu;
			attribute<m>      L_infra         (PlanRegio) := max_elem(L_infra_nu, L_infra_org);

			attribute<Eur_yr> kapitaallasten  (Planregio) := L_infra    * Kengetallen/Infra/Kj_g_m;
			attribute<Eur_yr> onderhoud       (Planregio) := L_infra_nu * Kengetallen/Infra/Ko_g_m;
			attribute<Eur_yr> verwijderkosten (Planregio) := float64(verwijderd) * L_infra_org * Kengetallen/Infra/Kj_g_m * (Kengetallen/Infra/VB_g + 1.0);
			attribute<Eur_yr> Kj_totaal       (Planregio) := kapitaallasten + verwijderkosten + onderhoud;
			attribute<Eur_yr> Kj_netto        (Planregio) := Kj_totaal - Oj_totaal - Aansl_m;
		}
		container elektriciteit
		{
			// inkomsten
			attribute<Eur_yr> netbijdrage     (Planregio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/name+'/gebouwen/Elektriciteit/netwerk'  )+')';
			attribute<Eur_yr> vastrecht       (Planregio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/gebouwen/Elektriciteit/Vastrecht')+')';
			attribute<Eur_yr> Aansl_m         (Planregio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/gebouwen/Elektriciteit/Aansl_m'  )+')';
			attribute<Eur_yr> Oj_totaal       (Planregio) := netbijdrage + vastrecht;

			// kosten
			attribute<m>      L_infra         (PlanRegio) := StateNaAllocatie/KostenBaten/KostenD/L_infra_e;

			attribute<Eur_yr> kapitaallasten  (Planregio) := L_infra * Kengetallen/Infra/Kj_e_m;
			attribute<Eur_yr> onderhoud       (Planregio) := L_infra * Kengetallen/Infra/Ko_e_m;
			attribute<Eur_yr> netverzwaring   (Planregio) := kapitaallasten * R_all_electric;
			attribute<Eur_yr> Kj_totaal       (Planregio) := kapitaallasten + netverzwaring + onderhoud;
			attribute<Eur_yr> Kj_netto        (Planregio) := Kj_totaal - Oj_totaal - Aansl_m;
		}
		
		container kosten //TO DO: verwijzing toevoegen
		{
			//TussenResultaten/R1_2020/StateNaAllocatie/Infra_per_buurt/Kosten
			
			// ook cumulatief maken
		
		}
	}
	
	container netwerken_RNBdata
	{
		attribute<bool> heeft_gas (PlanRegio) := TussenResultaten/Allocatie/heeft_gas;

		attribute<nrWoningen> nrWoonUnits     (Planregio) := ='add('+AsItemList(replace('sum(BebouwingsComponenten/@BC@/BebouwingsObject/nrWoonUnits_i, BebouwingsComponenten/@BC@/PlanRegio_rel)', '@BC@',Classifications/BebouwingsComponent/name) )+')';
		attribute<nrWoningen> nrAllElectric   (Planregio) := ='add('+AsItemList(replace('sum(BebouwingsComponenten/@BC@/BebouwingsObject/nrWoonUnits_i * Allocatie/@BC@/Aandelen/AllElectric, BebouwingsComponenten/@BC@/PlanRegio_rel)', '@BC@',Classifications/BebouwingsComponent/name) )+')';
		attribute<float64>    R_all_electric  (Planregio) := makedefined(nrAllElectric / nrWoonUnits, 0.0);
		attribute<bool >      all_electric    (Planregio) := R_all_electric > 0.999, // tolereer afronding fouten en kleine afwijkingen
			IntegrityCheck = "!(heeft_gas && all_electric)";

		container aardgas
		{
			attribute<bool>   had_gas         (Planregio) := /TussenResultaten/StartJaar/Allocatie/heeft_gas; // false voor bestaande restwarmte
			attribute<bool>   verwijderd      (Planregio) := had_gas && !heeft_gas; // alleen true als hele planregio gasloos is geworden

			// inkomsten
			attribute<Eur_yr> netbijdrage     (Planregio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/name+'/gebouwen/Aardgas/netwerk'  )+')';
			attribute<Eur_yr> vastrecht       (Planregio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/gebouwen/AardGas/Vastrecht')+')';
			attribute<Eur_yr> Aansl_m         (Planregio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/gebouwen/AardGas/Aansl_m'  )+')';
			attribute<Eur_yr> Oj_totaal       (Planregio) := netbijdrage + vastrecht;

			// kosten
			attribute<m>      L_infra_nu      (PlanRegio) := float64(heeft_gas ) * StateNaAllocatie/KostenBaten/KostenD/L_infra_g;
			attribute<m>      L_infra_org     (PlanRegio) := StartJaar/netwerken/aardgas/L_infra_nu;
			attribute<m>      L_infra         (PlanRegio) := max_elem(L_infra_nu, L_infra_org);

			attribute<Eur_yr> kapitaallasten  (Planregio) := L_infra    * Kengetallen/Infra/Kj_g_m;
			attribute<Eur_yr> onderhoud       (Planregio) := L_infra_nu * Kengetallen/Infra/Ko_g_m;
			attribute<Eur_yr> verwijderkosten (Planregio) := float64(verwijderd) * L_infra_org * Kengetallen/Infra/Kj_g_m * (Kengetallen/Infra/VB_g + 1.0);
			attribute<Eur_yr> Kj_totaal       (Planregio) := kapitaallasten + verwijderkosten + onderhoud;
			attribute<Eur_yr> Kj_netto        (Planregio) := Kj_totaal - Oj_totaal - Aansl_m;
		}
		container elektriciteit
		{
			// inkomsten
			attribute<Eur_yr> netbijdrage     (Planregio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/name+'/gebouwen/Elektriciteit/netwerk'  )+')';
			attribute<Eur_yr> vastrecht       (Planregio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/gebouwen/Elektriciteit/Vastrecht')+')';
			attribute<Eur_yr> Aansl_m         (Planregio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/gebouwen/Elektriciteit/Aansl_m'  )+')';
			attribute<Eur_yr> Oj_totaal       (Planregio) := netbijdrage + vastrecht;

			// kosten
			attribute<m>      L_infra         (PlanRegio) := StateNaAllocatie/KostenBaten/KostenD/L_infra_e;

			attribute<Eur_yr> kapitaallasten  (Planregio) := L_infra * Kengetallen/Infra/Kj_e_m;
			attribute<Eur_yr> onderhoud       (Planregio) := L_infra * Kengetallen/Infra/Ko_e_m;
			attribute<Eur_yr> netverzwaring   (Planregio) := kapitaallasten * R_all_electric;
			attribute<Eur_yr> Kj_totaal       (Planregio) := kapitaallasten + netverzwaring + onderhoud;
			attribute<Eur_yr> Kj_netto        (Planregio) := Kj_totaal - Oj_totaal - Aansl_m;
		}
	}

	container MaatregelenRapportage
	{
		unit<uint32> RapportageRegio := SpecifiekeInstellingen/Rapportage/Regio;
		container BestaandeWoning :=
			for_each_ne(
				 RapportageRegio/Name
				, 'Rapportage/RegionaleRapportage(
					  RapportageRegio, '+string(id(RapportageRegio))+'[RapportageRegio]
					, Allocatie/BestaandeWoning, '+quote('BestaandeWoning')+'
					, '+Classifications/WoningBestaandRapGroep/ClassRef[SpecifiekeInstellingen/Rapportage/WoningBestaandRapGroep_rel]+'
					, '+quote('BebouwingsObject/'+Classifications/WoningBestaandRapGroep/AttrRef [ SpecifiekeInstellingen/Rapportage/WoningBestaandRapGroep_rel ])+'
					, Classifications/WoningBestaandRapGroep/ExportLabel [ SpecifiekeInstellingen/Rapportage/WoningBestaandRapGroep_rel ]
					, units/nrWoningen
				)'
			);

		container NieuwbouwWoning :=
			for_each_ne(
				 RapportageRegio/Name
				, 'Rapportage/RegionaleRapportage(
					  RapportageRegio, '+string(id(RapportageRegio))+'[RapportageRegio]
					, Allocatie/NieuwbouwWoning, '+quote('NieuwbouwWoning')+'
					, BebouwingsComponenten/NieuwbouwWoning/BebouwingsTypeDomein
					, '+quote('RuimtelijkeVraagComponent/BebouwingsType')+'
					, '+quote('WoningType')+'
					, units/nrWoningen
				)'
			);

		container BestaandeUtil:=
			for_each_ne(
				 RapportageRegio/Name
				, 'Rapportage/RegionaleRapportage(
					  RapportageRegio, '+string(id(RapportageRegio))+'[RapportageRegio]
					, Allocatie/BestaandeUtil, '+quote('BestaandeUtil')+'
					, BebouwingsComponenten/BestaandeUtil/BebouwingsTypeDomein
					, '+quote('RuimtelijkeVraagComponent/BebouwingsType')+'
					, '+quote('UtiliteitsType')+'
					, units/m2
				)'
			);

		container NieuwbouwUtil:=
			for_each_ne(
				 SpecifiekeInstellingen/Rapportage/Regio/Name
				, 'Rapportage/RegionaleRapportage(
					  RapportageRegio, '+string(id(RapportageRegio))+'[RapportageRegio]
					, Allocatie/NieuwbouwUtil, '+quote('NieuwbouwUtil')+'
					, BebouwingsComponenten/NieuwbouwUtil/BebouwingsTypeDomein
					, '+quote('RuimtelijkeVraagComponent/BebouwingsType')+'
					, '+quote('UtiliteitsType')+'
					, units/m2
				)'
			);

		container BestaandeGlTb :=
			for_each_ne(
				 SpecifiekeInstellingen/Rapportage/Regio/Name
				, 'Rapportage/RegionaleRapportage(
					  RapportageRegio, '+string(id(RapportageRegio))+'[RapportageRegio]
					, Allocatie/BestaandeGlTb, '+quote('BestaandeGlTb')+'
					, Classifications/GLTB
					, '+quote('RuimtelijkeVraagComponent/BebouwingsType')+'
					, '+quote('GlasTuinBouwType')+'
					, units/m2
				)'
			);

		container NieuwbouwGlTb:=
			for_each_ne(
				 SpecifiekeInstellingen/Rapportage/Regio/Name
				, 'Rapportage/RegionaleRapportage(
					  RapportageRegio, '+string(id(RapportageRegio))+'[RapportageRegio]
					, Allocatie/NieuwbouwGlTb, '+quote('NieuwbouwGlTb')+'
					, Classifications/GlTb
					, '+quote('RuimtelijkeVraagComponent/BebouwingsType')+'
					, '+quote('NieuwbouwGlTb')+'
					, units/m2
				)'
			);
	}

	container InputOutput :=
		for_each_ne(
			 SpecifiekeInstellingen/Rapportage/Regio/Name
		,	'Rapportage/Flow('
				'SpecifiekeInstellingen/Rapportage/Regio'
				','+Quote(SpecifiekeInstellingen/Rapportage/Regio/Name)+
				', value('+ string(id(SpecifiekeInstellingen/Rapportage/Regio)) + ',SpecifiekeInstellingen/Rapportage/Regio)'
			')'
		)
	{
		container Totaal := =(SpecifiekeInstellingen/Rapportage/Regio/Name != 'NL')
			? 'Rapportage/Flow(geography/RegioIndelingen/NL, ''NL'', value(0, geography/RegioIndelingen/NL) )'
			: '.';
	}
	
	container Generate: isHidden = "True"
	{
		parameter<String> All := 'Ready',
			ExplicitSuppliers =
				"= ';' + AsList('InputOutput/'+SpecifiekeInstellingen/Rapportage/Regio/Name+'/GenerateAll',';')"
				"+ (SpecifiekeInstellingen/Rapportage/Regio/Name != 'NL' ? ';InputOutput/Totaal/GenerateAll': '')"
				"+ ';' + AsList('MaatregelenRapportage/BestaandeWoning/'+SpecifiekeInstellingen/Rapportage/Regio/Name+'/GenerateAll',';')"
				"+ ';' + AsList('MaatregelenRapportage/NieuwbouwWoning/'+SpecifiekeInstellingen/Rapportage/Regio/Name+'/GenerateAll',';')"
				"+ ';' + AsList('MaatregelenRapportage/BestaandeUtil/'  +SpecifiekeInstellingen/Rapportage/Regio/Name+'/GenerateAll',';')"
				"+ ';' + AsList('MaatregelenRapportage/NieuwbouwUtil/'  +SpecifiekeInstellingen/Rapportage/Regio/Name+'/GenerateAll',';')"
				"+ ';' + AsList('MaatregelenRapportage/BestaandeGlTb/'  +SpecifiekeInstellingen/Rapportage/Regio/Name+'/GenerateAll',';')"
				"+ ';' + AsList('MaatregelenRapportage/NieuwbouwGlTb/'  +SpecifiekeInstellingen/Rapportage/Regio/Name+'/GenerateAll',';')"
	//			"+ ';' + 'AllocExports/GenerateThisToo'" // comment-out if you want to skip all exports
			;
	}
	#include <AllocExports.dms>

	container FolderInfo: isHidden = "True"
	{
		parameter<string> RunName      := Expand(., '%configName%');
		parameter<string> OutputFolder := '%localDataProjDir%/Results/'+ RunName +'/'+ RekenstapName;
	}
	
	container RegressieTest: isHidden = "True"
	{
		parameter<bool>   PotentiaalKaartLeesbaar : = all(Grid100m/totaal/Vraag/Potentiaal/totaal >= 0.0) && sum(Grid100m/totaal/Vraag/Potentiaal/totaal) > 0.0;
		parameter<bool>   All                     : = PotentiaalKaartLeesbaar;
		parameter<string> Generate                := 'Ready'
		,	IntegrityCheck    = "All"
		,	ExplicitSuppliers = "Generate/All"; // Generates All reports.
	}
	
	container PR_LD: isHidden = "True"
	{
		//statische indicatoren per buurt
		attribute<String>	I01_Buurtcode				(Planregio)	:=	Geography/RegioIndelingen/buurt/BU_CODE;
		attribute<String>	I02_Buurtnaam				(Planregio)	:=	Geography/RegioIndelingen/buurt/BU_NAAM;
		attribute<String>	I03_Wijkcode				(Planregio)	:=	Geography/RegioIndelingen/buurt/WK_CODE;
		attribute<String>	I04_Gemeentecode			(Planregio)	:=	Geography/RegioIndelingen/buurt/GM_CODE;
		attribute<String>	I05_Gemeentenaam			(Planregio)	:=	Geography/RegioIndelingen/buurt/GM_NAAM;
		attribute<String>	I06_Energieregio			(Planregio)	:=	Geography/RegioIndelingen/buurt/ER_NAAM;
		attribute<String>	I07_Provincienaam			(Planregio)	:=	Geography/RegioIndelingen/buurt/PROV_NAAM;
		attribute<Float64>	I08_Vbo_woningen			(Planregio)	:=	sum(BebouwingsComponenten/BestaandeWoning/RuimtelijkeVraagComponent/nrAansluitingen,	BebouwingsComponenten/BestaandeWoning/PlanRegio_rel);
		attribute<Float64>	I09_vbo_utiliteit			(Planregio)	:=	sum(BebouwingsComponenten/BestaandeUtil/RuimtelijkeVraagComponent/nrAansluitingen,		BebouwingsComponenten/BestaandeUtil/PlanRegio_rel);
		attribute<Float64>	I10_Woningequivalenten		(Planregio)	:=	netwerken/nrWoonunits;
				
		//dynamische indicatoren op hoofdlijnen
		attribute<GJ_yr>	I11_Vraag_aardgas			(Planregio)	:=	Groengas ? 0 [GJ_yr] : (subitem/BW_aardgas + subitem/BU_aardgas);
		attribute<GJ_yr>	I12_Vraag_groengas			(Planregio)	:=	Groengas ? (subitem/BW_aardgas + subitem/BU_aardgas): 0 [GJ_yr];
		attribute<GJ_yr>	I13_Vraag_MTwarmte			(Planregio)	:=	subitem/BW_MT_warmte + subitem/BU_MT_warmte;
		attribute<GJ_yr>	I14_Vraag_LTwarmte			(Planregio)	:=	subitem/BW_WKO_warmte + subitem/BU_WKO_warmte; //worden hier al de LT puntbronnen in opgenomen?
		attribute<GJ_yr>	I15_Vraag_elektotaal		(Planregio)	:=	subitem/BW_elek + subitem/BU_elek;
		attribute<GJ_yr>	I16_Vraag_elekwarmte		(Planregio)	:=	subitem/BW_ewP_warmte + subitem/BU_eWP_warmte;
		attribute<GJ_yr>	I17_Vraag_warmtetotaal		(Planregio)	:=	subitem/BW_totaal + subitem/BU_totaal;
		attribute<GJ_yr_ha>	I18_Warmte_hectare			(Planregio)	:=	I17_Vraag_warmtetotaal / subitem/Oppervlak; //dummywaarde
		attribute<GJ_yr>	I19_Vraag_koude				(Planregio)	:=	const(10 [GJ_yr], Planregio); //dummywaarde
		attribute<KG_yr>	I20_Uitstoot_CO2			(Planregio)	:=	AllocExports/PlanRegioResults/Table/CO2_totaal;
		attribute<Eur_yr>	I21_Nationale_kosten		(Planregio)	:=	maatschappelijk/Kmi_totaal - maatschappelijk/Om_comfort;
				
		//eindverbruikerskosten woningen
		attribute<EUR>		I22_W_eig_investering		(Planregio)	:=	subitem/W_Ki_aansl + subitem/W_Ki_gebouw + subitem/W_Ki_instal;
		attribute<EUR>		I23_W_eig_subsidie			(Planregio)	:=	const(0.0[Eur], Planregio); // voor nu geen subsidies opgenomen
		attribute<EUR_yr>	I24_W_eig_kapitaallast		(Planregio)	:=	subitem/W_KL_installatie + subitem/W_KL_gebouw + subitem/W_KL_aansl;
		attribute<EUR_yr>	I25_W_eig_splitincentive	(Planregio)	:=	const(0.0[Eur_yr], Planregio); //split incentive moet nog over besloten worden
		attribute<EUR>		I26_W_eig_BTWinvest			(Planregio)	:=	I22_W_eig_investering * 0.21; //voor nu constant, afhankelijk van beleidsscenario
		attribute<EUR_yr>	I27_W_geb_inkoopgas			(Planregio)	:=	(Groengas ? 3d : 1d) * sum(Allocatie/BestaandeWoning/Aardgas/eindgebruik_excl, BebouwingsComponenten/BestaandeWoning/PlanRegio_rel);
		attribute<EUR_yr>	I28_W_geb_inkoopwarmte		(Planregio)	:=	subitem/W_MT_rest + subitem/W_MT_bio + subitem/W_MT_geo;
		attribute<EUR_yr>	I29_W_geb_inkoopElek		(Planregio)	:=	Eindgebruiker/BestaandeWoning/gebouwen/Elektriciteit/Vraag_excl;
		attribute<EUR_yr>	I30_W_geb_splitincentive	(Planregio)	:=	const(0.0[Eur_yr], Planregio);//split incentive moet nog over besloten worden
		attribute<EUR_yr>	I31_W_geb_energiebelasting	(Planregio)	:=	subitem/W_EH_gas + subitem/W_EH_elek;
				
		//eindverbruikerskosten utiliteit
		attribute<EUR>		I32_U_eig_investering		(Planregio)	:=	subitem/U_Ki_aansl + subitem/U_Ki_gebouw + subitem/U_Ki_instal;
		attribute<EUR>		I33_U_eig_subsidie			(Planregio)	:=	const(0.0[Eur], Planregio); // voor nu geen subsidies opgenomen
		attribute<EUR_yr>	I34_U_eig_kapitaallast		(Planregio)	:=	subitem/U_KL_installatie + subitem/U_KL_gebouw + subitem/U_KL_aansl;
		attribute<EUR_yr>	I35_U_eig_splitincentive	(Planregio)	:=	const(0.0[Eur_yr], Planregio); //split incentive moet nog over besloten worden
		attribute<EUR_yr>	I36_U_geb_inkoopgas			(Planregio)	:=	(Groengas ? 3d : 1d) * sum(Allocatie/BestaandeUtil/Aardgas/eindgebruik_excl, BebouwingsComponenten/BestaandeUtil/PlanRegio_rel);
		attribute<EUR_yr>	I37_U_geb_inkoopwarmte		(Planregio)	:=	subitem/U_MT_rest + subitem/U_MT_bio + subitem/U_MT_geo;
		attribute<EUR_yr>	I38_U_geb_inkoopElek		(Planregio)	:=	Eindgebruiker/BestaandeUtil/gebouwen/Elektriciteit/Vraag_excl;
		attribute<EUR_yr>	I39_U_geb_splitincentive	(Planregio)	:=	const(0.0[Eur_yr], Planregio);//split incentive moet nog over besloten worden
		attribute<EUR_yr>	I40_U_geb_energiebelasting	(Planregio)	:=	subitem/U_EH_gas + subitem/U_EH_elek;
				
		//warmtebedrijf
		attribute<EUR>		I41_Wbedrijf_investering	(Planregio)	:=	StateNaAllocatie/Stock/Ki_Totaal;
		attribute<EUR>		I42_Wbedrijf_subsidie		(Planregio)	:=	const(0.0[Eur], Planregio); // voor nu geen subsidies opgenomen
		attribute<EUR_yr>	I43_Wbedrijf_kapitaallast	(Planregio)	:=	StateNaAllocatie/Flow/Kji_KL;
		attribute<EUR_yr>	I44_Wbedrijf_inkoopenergie	(Planregio)	:=	(Groengas ? 3d : 1d) * StateNaAllocatie/Flow/Kj_wd_verbruik;
		attribute<EUR_yr>	I45_Wbedrijf_inkomsten		(Planregio)	:=	StateNaAllocatie/Flow/Oj_totaal;
		attribute<EUR_yr>	I46_Wbedrijf_lopendekosten	(Planregio)	:=	StateNaAllocatie/Flow/Kj_lopendekosten;
		attribute<EUR_yr>	I47_Wbedrijf_energiebel		(Planregio)	:=	StateNaAllocatie/Flow/Kj_EH_bijstook;
		attribute<EUR_yr>	I48_Wbedrijf_resultaat		(Planregio)	:=	StateNaAllocatie/Flow/Kji_netto; //dit moet nog nader gecontroleerd worden
				
		//maatschappelijk
		attribute<EUR>		I49_Gnet_verwijderen		(Planregio)	:=	(I11_Vraag_aardgas + I12_Vraag_groengas > 10 [GJ_yr]) ? const(0.0[EUR], Planregio) : const(100.0[EUR],Planregio); // nog niet geimplementeerd
		attribute<EUR>		I51_Gnet_vervangen			(Planregio)	:=	(I11_Vraag_aardgas + I12_Vraag_groengas < 10 [GJ_yr]) ? const(0.0[EUR], Planregio) : const(100.0[EUR],Planregio); // nog niet geimplementeerd
		attribute<EUR_yr>	I50_Gnet_kapitaallast		(Planregio)	:=	maatschappelijk/net/Aardgas;
		attribute<EUR>		I52_Enet_verzwaren			(Planregio)	:=	const(333.0[EUR], Planregio); // nog niet geimplementeerd
		attribute<EUR_yr>	I53_Enet_kapitaallast		(Planregio)	:=	maatschappelijk/net/Elektriciteit;
		attribute<EUR_ton>	I54_Eur_vermeden_CO2		(Planregio)	:=	makedefined((subitem/Delta_nat_kosten / subitem/Delta_CO2_ton), 0.0[EUR_ton]);

		parameter<bool>	Groengas := true;
				
		container subitem
			{
			//energieverbruik
			attribute<GJ_yr>	BW_eWP_warmte		(Planregio)		:=	sum(Allocatie/BestaandeWoning/Metervraag/eWP,				BebouwingsComponenten/BestaandeWoning/PlanRegio_rel);
			attribute<GJ_yr>	BU_eWP_warmte		(Planregio)		:=	sum(Allocatie/BestaandeUtil/Metervraag/eWP, 				BebouwingsComponenten/BestaandeUtil/PlanRegio_rel);
			attribute<GJ_yr>	BW_WKO_warmte		(Planregio)		:=	sum(Allocatie/BestaandeWoning/Metervraag/WKO,				BebouwingsComponenten/BestaandeWoning/PlanRegio_rel);
			attribute<GJ_yr>	BU_WKO_warmte		(Planregio)		:=	sum(Allocatie/BestaandeUtil/Metervraag/WKO,					BebouwingsComponenten/BestaandeUtil/PlanRegio_rel);
			attribute<GJ_yr>	BU_aardgas			(Planregio)		:=	sum(Allocatie/BestaandeUtil/Metervraag/aardgas,				BebouwingsComponenten/BestaandeUtil/PlanRegio_rel);
			attribute<GJ_yr>	BW_aardgas			(Planregio)		:=	sum(Allocatie/BestaandeWoning/Metervraag/aardgas,			BebouwingsComponenten/BestaandeWoning/PlanRegio_rel);
			attribute<GJ_yr>	BU_MT_warmte		(Planregio)		:=	sum(Allocatie/BestaandeUtil/Metervraag/warmteMT,			BebouwingsComponenten/BestaandeUtil/PlanRegio_rel);
			attribute<GJ_yr>	BW_MT_warmte		(Planregio)		:=	sum(Allocatie/BestaandeWoning/Metervraag/warmteMT,			BebouwingsComponenten/BestaandeWoning/PlanRegio_rel);
			attribute<GJ_yr>	BU_totaal			(Planregio)		:=	sum(Allocatie/BestaandeUtil/Metervraag/warmtetotaal,		BebouwingsComponenten/BestaandeUtil/PlanRegio_rel);
			attribute<GJ_yr>	BW_totaal			(Planregio)		:=	sum(Allocatie/BestaandeWoning/Metervraag/warmtetotaal,		BebouwingsComponenten/BestaandeWoning/PlanRegio_rel);
			attribute<GJ_yr>	BU_elek				(Planregio)		:=	sum(Allocatie/BestaandeUtil/Metervraag/elektriciteit,		BebouwingsComponenten/BestaandeUtil/PlanRegio_rel);
			attribute<GJ_yr>	BW_elek				(Planregio)		:=	sum(Allocatie/BestaandeWoning/Metervraag/elektriciteit,		BebouwingsComponenten/BestaandeWoning/PlanRegio_rel);
			
			attribute<ha>		Oppervlak			(Planregio)		:=	Geography/RegioIndelingen/buurt/Overigen/a_lan_ha, Descr = "landoppervlakte van de buurt";
			attribute<kg_yr>	Orig_CO2			(Planregio)		:=	StartJaar/AllocExports/PlanRegioResults/Table/CO2_totaal;
			attribute<kg_yr>	Delta_CO2			(Planregio)		:=	Orig_CO2 - I20_Uitstoot_CO2;
			attribute<ton_yr>	Delta_CO2_ton		(Planregio)		:=	Delta_CO2 / 1000.0[kg_yr / ton_yr];
			
			attribute<EUR_yr>	Orig_nat_kosten		(Planregio)		:=	StartJaar/maatschappelijk/Kmi_totaal - StartJaar/maatschappelijk/Om_comfort;
			attribute<EUR_yr>	Delta_nat_kosten	(Planregio)		:=	I21_Nationale_kosten - Orig_nat_kosten;
					
			//energieverbruik kosten heffing
			attribute<EUR_yr>	U_EH_gas	(Planregio)	:=	sum(Allocatie/BestaandeUtil/Aardgas/EnergieHeffing,				BebouwingsComponenten/BestaandeUtil/PlanRegio_rel);
			attribute<EUR_yr>	U_EH_elek	(Planregio)	:=	sum(Allocatie/BestaandeUtil/Elektriciteit/EnergieHeffing,		BebouwingsComponenten/BestaandeUtil/PlanRegio_rel);
			attribute<EUR_yr>	W_EH_gas	(Planregio)	:=	sum(Allocatie/BestaandeWoning/Aardgas/EnergieHeffing,			BebouwingsComponenten/BestaandeWoning/PlanRegio_rel);
			attribute<EUR_yr>	W_EH_elek	(Planregio)	:=	sum(Allocatie/BestaandeWoning/Elektriciteit/EnergieHeffing,		BebouwingsComponenten/BestaandeWoning/PlanRegio_rel);					
					
			//energieverbruik kosten mt warmte
			attribute<EUR_yr>	W_MT_rest	(Planregio)	:=	Eindgebruiker/BestaandeWoning/gebouwen/RestWarmte/Vraag_excl;
			attribute<EUR_yr>	W_MT_bio	(Planregio)	:=	Eindgebruiker/BestaandeWoning/gebouwen/BioWKK/Vraag_excl;
			attribute<EUR_yr>	W_MT_geo	(Planregio)	:=	Eindgebruiker/BestaandeWoning/gebouwen/GeoThermie/Vraag_excl;
			attribute<EUR_yr>	U_MT_rest	(Planregio)	:=	Eindgebruiker/BestaandeUtil/gebouwen/RestWarmte/Vraag_excl;
			attribute<EUR_yr>	U_MT_bio	(Planregio)	:=	Eindgebruiker/BestaandeUtil/gebouwen/BioWKK/Vraag_excl;
			attribute<EUR_yr>	U_MT_geo	(Planregio)	:=	Eindgebruiker/BestaandeUtil/gebouwen/GeoThermie/Vraag_excl;
					
			//aansluitbijdrages
			attribute<EUR>	W_MT_aansl_rest	(Planregio)	:=	Eindgebruiker/BestaandeWoning/gebouwen/RestWarmte/Oi_aanslD;
			attribute<EUR>	W_MT_aansl_bio	(Planregio)	:=	Eindgebruiker/BestaandeWoning/gebouwen/BioWKK/Oi_aanslD;
			attribute<EUR>	W_MT_aansl_geo	(Planregio)	:=	Eindgebruiker/BestaandeWoning/gebouwen/GeoThermie/Oi_aanslD;
			attribute<EUR>	W_Lt_aansl	(Planregio)	:=	Eindgebruiker/BestaandeWoning/gebouwen/WKO/aansl_i;
			attribute<EUR>	W_Ki_aansl	(Planregio)	:=	W_MT_aansl_rest + W_MT_aansl_bio + W_MT_aansl_geo + W_Lt_aansl;
			attribute<EUR>	U_MT_aansl_rest		(Planregio)	:=	Eindgebruiker/BestaandeUtil/gebouwen/RestWarmte/Oi_aanslD;
			attribute<EUR>	U_MT_aansl_bio	(Planregio)	:=	Eindgebruiker/BestaandeUtil/gebouwen/BioWKK/Oi_aanslD;
			attribute<EUR>	U_MT_aansl_geo	(Planregio)	:=	Eindgebruiker/BestaandeUtil/gebouwen/GeoThermie/Oi_aanslD;
			attribute<EUR>	U_Lt_aansl	(Planregio)	:=	Eindgebruiker/BestaandeUtil/gebouwen/WKO/aansl_i;
			attribute<EUR>	U_ki_aansl	(Planregio)	:=	U_MT_aansl_rest + U_MT_aansl_bio + U_MT_aansl_geo + U_Lt_aansl;
					
			//kapitaallasten aansluitbijdrages
			attribute<EUR_yr>	W_KL_aansl_lt		(Planregio)	:=	Eindgebruiker/BestaandeWoning/gebouwen/WKO/Aansl_j;
			attribute<EUR_yr>	W_KL_aansl_bio		(Planregio)	:=	Eindgebruiker/BestaandeWoning/gebouwen/BioWKK/Aansl_j;
			attribute<EUR_yr>	W_KL_aansl_geo		(Planregio)	:=	Eindgebruiker/BestaandeWoning/gebouwen/GeoThermie/Aansl_j;
			attribute<EUR_yr>	W_KL_aansl_rest		(Planregio)	:=	Eindgebruiker/BestaandeWoning/gebouwen/RestWarmte/Aansl_j;
			attribute<EUR_yr>	U_KL_aansl_lt		(Planregio)	:=	Eindgebruiker/BestaandeUtil/gebouwen/WKO/Aansl_j;
			attribute<EUR_yr>	U_KL_aansl_bio		(Planregio)	:=	Eindgebruiker/BestaandeUtil/gebouwen/BioWKK/Aansl_j;
			attribute<EUR_yr>	U_KL_aansl_geo		(Planregio)	:=	Eindgebruiker/BestaandeUtil/gebouwen/GeoThermie/Aansl_j;
			attribute<EUR_yr>	U_KL_aansl_rest		(Planregio)	:=	Eindgebruiker/BestaandeUtil/gebouwen/RestWarmte/Aansl_j;
										
			//kapitaallasten
			attribute<EUR_yr>	W_KL_installatie	(Planregio)	:=	Eindgebruiker/BestaandeWoning/LokaleEnergieProductie/Kw_Totaal_excl;
			attribute<EUR_yr>	U_KL_installatie	(Planregio)	:=	Eindgebruiker/BestaandeUtil/LokaleEnergieProductie/Kw_Totaal_excl;
			attribute<EUR_yr>	W_KL_gebouw	(Planregio)	:=	Eindgebruiker/BestaandeWoning/gebouwen/Kw_GebouwVerbetering_excl;
			attribute<EUR_yr>	U_KL_gebouw	(Planregio)	:=	Eindgebruiker/BestaandeUtil/gebouwen/Kw_GebouwVerbetering_excl;
			attribute<EUR_yr>	W_KL_aansl	(Planregio)	:=	W_KL_aansl_lt + W_KL_aansl_bio + W_KL_aansl_geo + W_KL_aansl_rest;
			attribute<EUR_yr>	U_KL_aansl	(Planregio)	:=	U_KL_aansl_lt + U_KL_aansl_bio + U_KL_aansl_geo + U_KL_aansl_rest;
					
			//investeringen
			attribute<EUR>	W_Ki_gebouw		(Planregio)	:=	Eindgebruiker/BestaandeWoning/gebouwen/Ki_GebouwVerbetering_excl;
			attribute<EUR>	U_Ki_gebouw		(Planregio)	:=	Eindgebruiker/BestaandeUtil/gebouwen/Ki_GebouwVerbetering_excl;
			attribute<EUR>	W_Ki_instal30	(Planregio)	:=	Eindgebruiker/BestaandeWoning/LokaleEnergieProductie/Ki_Totaal30;
			attribute<EUR>	W_Ki_instal15	(Planregio)	:=	Eindgebruiker/BestaandeWoning/LokaleEnergieProductie/Ki_Totaal15;
			attribute<EUR>	U_Ki_instal30	(Planregio)	:=	Eindgebruiker/BestaandeUtil/LokaleEnergieProductie/Ki_Totaal30;
			attribute<EUR>	U_Ki_instal15	(Planregio)	:=	Eindgebruiker/BestaandeUtil/LokaleEnergieProductie/Ki_Totaal15;
			attribute<EUR>	W_Ki_instal		(Planregio)	:=	W_Ki_instal30 + W_Ki_instal15;
			attribute<EUR>	U_Ki_instal		(Planregio)	:=	U_Ki_instal30 + U_Ki_instal15;
					
			}
	}
}