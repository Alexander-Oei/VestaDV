//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                                  (C) VESTA 2018                                      //
//        Het doel van het model is om beleidsinformatie te genereren met betrekking    //
//        tot de energievoorziening van de gebouwde omgeving.                           //
//                                                                                      //
//                         Planbureau voor de Leefomgeving                              //
//                                    JUNI 2018                                         //
//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//     * In het geval van externe publicatie van onderzoeksresultaten waarbij gebruik   //
//       is gemaakt van de verstrekte gegevens dient de gebruiker deze resultaten aan   //
//       het PBL mede te delen middels de toezending van 2 exemplaren van de betref-    //
//       fende publicatie aan het hoofd van het team ROL van het PBL.                   //
//                                                                                      //
//     * In elke publicatie waarin de gegevens van het PBL in enige vorm zijn opgenomen,//
//       zal de gebruiker de vermelding opnemen: "Bron: Planbureau voor de Leefomgeving"//
//       Indien het een bewerking van de gegevens betreft, dient te worden opgenomen:   //
//       "Gebaseerd op Planbureau voor de Leefomgeving gegevens".                       //
//                                                                                      //
//     * Het PBL is onder geen enkele voorwaarde aansprakelijk tegenover gebruiker voor //
//       enige schade, mede begrepen winstderving, verlies van gegevens, schade         //
//       tengevolge van fouten of onjuistheden in gegevens, schade tengevolge van fouten//
//       of onvolkomenheden in de documentatie of andere incidentele of gevolgschade    //
//       welke voortvloeit uit of in verband staat met gebruik of het niet kunnen ge-   //
//       bruiken van gegevens.                                                          //
//                                                                                      //
//     * Het PBL staat niet in voor de juistheid van de resultaten die worden verkregen //
//       door combinatie van diverse, op zichzelf staande PBL-gegevens onderling,       //
//       of met die uit andere bronnen.                                                 //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////


container indicatoren
{
	unit<uint32> PlanRegio := SpecifiekeInstellingen/PlanRegio;
	container CO2: Source = "FO v7a C5"
	{
		parameter<KG_GJ> CO2_GJe := VestaRunData/Prijzen/Elektriciteit/CO2_GJ;
		parameter<KG_GJ> CO2_GJg := VestaRunData/Prijzen/AardGas/CO2_GJ;
		
		container gebouwen
		{
			container Elektriciteit := for_each_nedv(Classifications/BebouwingsComponent/Name, 'sum(AllocatieResultaat/'+Classifications/BebouwingsComponent/Name+'/CO2/Elektriciteit, VestaRunData/'+Classifications/BebouwingsComponent/Name+'/PlanRegio_rel)', PlanRegio, KG_yr)
			{
				attribute<KG_yr> Totaal(PlanRegio) := ='add('+AsItemList(Classifications/BebouwingsComponent/Name)+')';
			}

			container AardGas := for_each_nedv(Classifications/BebouwingsComponent/Name, 'sum(AllocatieResultaat/'+Classifications/BebouwingsComponent/Name+'/CO2/AardGas, VestaRunData/'+Classifications/BebouwingsComponent/Name+'/PlanRegio_rel)', PlanRegio, KG_yr)
			{
				attribute<KG_yr> Totaal(PlanRegio) := ='add('+AsItemList(Classifications/BebouwingsComponent/Name)+')';
			}
			attribute<KG_yr> Totaal(PlanRegio) := Elektriciteit/Totaal + AardGas/Totaal;
		}
		container impl
		{
			attribute<String> BronExpr (Classifications/AanbodOptie) := union_data(Classifications/AanbodOptie, 
				'EndState/AanbodOpties/PlanRegioWarmteAllocatie == Classifications/AanbodOptie/V/RestWarmte ? GebiedsAllocatie/RestWarmte/CO2   : 0.0[KG_yr]',
				'EndState/AanbodOpties/PlanRegioWarmteAllocatie == Classifications/AanbodOptie/V/GeoThermie ? GebiedsAllocatie/GeoThermie/CO2   : 0.0[KG_yr]',
				'EndState/AanbodOpties/PlanRegioWarmteAllocatie == Classifications/AanbodOptie/V/WijkWKK    ? GebiedsAllocatie/WijkWKK/impl/CO2 : 0.0[KG_yr]',
				'EndState/AanbodOpties/PlanRegioWarmteAllocatie == Classifications/AanbodOptie/V/BioWKK     ? GebiedsAllocatie/BioWKK/impl/CO2  : 0.0[KG_yr]',
				'EndState/WKO/CO2'
			);
		}
		container CentraleWarmteBronnen := for_each_nedv(Voorkeuren/WarmteOpties/Name, impl/BronExpr[Voorkeuren/WarmteOpties/AanbodOptie], PlanRegio, KG_yr)
		{
			attribute<KG_yr> Totaal(PlanRegio) := =iif(#Voorkeuren/WarmteOpties=0, 'const(0, PlanRegio, KG_yr)', 'add('+AsItemList(Voorkeuren/WarmteOpties/Name)+')');
		}
		attribute<KG_yr> Totaal(PlanRegio) := Gebouwen/Totaal + CentraleWarmteBronnen/Totaal;
	}

	container Metervraag: Source = "Rapportagebehoefte cijfers per buurt --> Geodan 15 maart 2018"
	{
		container gebouwen
		{
			container GebiedsOptie := for_each_nedv(Classifications/BebouwingsComponent/Name, 'sum(AllocatieResultaat/'+Classifications/BebouwingsComponent/Name+'/MeterVraag/GebiedsOptie, VestaRunData/'+Classifications/BebouwingsComponent/Name+'/PlanRegio_rel)', PlanRegio, GJ_yr)
			{
				attribute<GJ_yr> Totaal(PlanRegio) := ='add('+AsItemList(Classifications/BebouwingsComponent/Name)+')';
			}

			container Elektriciteit := for_each_nedv(Classifications/BebouwingsComponent/Name, 'sum(AllocatieResultaat/'+Classifications/BebouwingsComponent/Name+'/MeterVraag/Elektriciteit, VestaRunData/'+Classifications/BebouwingsComponent/Name+'/PlanRegio_rel)', PlanRegio, GJ_yr)
			{
				attribute<GJ_yr> Totaal(PlanRegio) := ='add('+AsItemList(Classifications/BebouwingsComponent/Name)+')';
			}

			container AardGas := for_each_nedv(Classifications/BebouwingsComponent/Name, 'sum(AllocatieResultaat/'+Classifications/BebouwingsComponent/Name+'/MeterVraag/AardGas, VestaRunData/'+Classifications/BebouwingsComponent/Name+'/PlanRegio_rel)', PlanRegio, GJ_yr)
			{
				attribute<GJ_yr> Totaal(PlanRegio) := ='add('+AsItemList(Classifications/BebouwingsComponent/Name)+')';
			}
		}
	}

	// *** Bij vervanging van AardGas door warmte, of eWP, EWV, of WKO,
	// valt de distributievergoeding weg tbv het dekken van de afschrijving van het bestaande aardgasnet
	attribute<bool> heeft_gas           (PlanRegio) := TussenResultaten/heeft_gas;
	
	container Maatschappelijk: Source = "FO v7a C6"
	{
		container impl: IsHidden = "true"
		{
			attribute<String> BronExpr(Classifications/AanbodOptie) :=
				union_data(Classifications/AanbodOptie,
					'EndState/Flow/Km_Total * float64(EndState/AanbodOpties/PlanRegioWarmteAllocatie == Classifications/AanbodOptie/V/RestWarmte)',
					'EndState/Flow/Km_Total * float64(EndState/AanbodOpties/PlanRegioWarmteAllocatie == Classifications/AanbodOptie/V/GeoThermie)',
					'EndState/Flow/Km_Total * float64(EndState/AanbodOpties/PlanRegioWarmteAllocatie == Classifications/AanbodOptie/V/WijkWKK)',
					'EndState/Flow/Km_Total * float64(EndState/AanbodOpties/PlanRegioWarmteAllocatie == Classifications/AanbodOptie/V/BioWKK)',
					'(EndState/WKO/Ki_Totaal *NCW/mr28/AnnualisationFactor + EndState/WKO/Km_Totaal)'
				);
		}

		container gebouwen {
			container ElektriciteitsVraag := // excl WKO vraag
					for_each_nedv(Classifications/BebouwingsComponent/Name, 
						replace(
							'sum(AllocatieResultaat/@BC@/Elektriciteit/maatschappelijk, VestaRunData/@BC@/PlanRegio_rel)'
						,	'@BC@', Classifications/BebouwingsComponent/Name
						)
					, PlanRegio, Eur_yr
					)
			{
				attribute<Eur_yr> Km_Totaal(PlanRegio) := ='add('+AsItemList(Classifications/BebouwingsComponent/Name)+')';
			}

			container AardGasVerbruik := // incl mWKK excl Wijk_wkk en kosten bijstook WOS
					for_each_nedv(Classifications/BebouwingsComponent/Name,
						replace(
							'sum(AllocatieResultaat/@BC@/AardGas/maatschappelijk, VestaRunData/@BC@/PlanRegio_rel)'
						,	'@BC@', Classifications/BebouwingsComponent/Name
						)
					, PlanRegio, Eur_yr
					)
			{
				attribute<Eur_yr> Km_Totaal(PlanRegio) := ='add('+AsItemList(Classifications/BebouwingsComponent/Name)+')';
			}

			attribute<Eur>    Ki_GebouwVerbetering (PlanRegio) := ='add('+AsItemList('sum(VestaRunData/'+Classifications/BebouwingsComponent/Name+'/Kosten/Ki_GebouwVerbetering, VestaRunData/'+Classifications/BebouwingsComponent/Name+'/PlanRegio_rel)')+')';
			attribute<Eur_yr> Km_GebouwVerbetering (PlanRegio) :=  Ki_GebouwVerbetering *NCW/mr30/AnnualisationFactor;

			attribute<Eur_yr> Kj_Onderhoud         (PlanRegio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/gebouwen/Kj_Onderh_excl')+')';
			attribute<Eur_yr> Kj_Admin             (PlanRegio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/gebouwen/Kj_Admin_excl')+')';

			attribute<Eur_yr> Km_Totaal            (PlanRegio) := ElektriciteitsVraag/Km_Totaal + AardGasVerbruik/Km_Totaal + Km_GebouwVerbetering + Kj_Onderhoud + Kj_Admin;
		}
		container LokaleEnergieProductie
		{
			attribute<Eur>    Ki_ZonneBoiler (PlanRegio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/LokaleEnergieProductie/Ki_ZonneBoiler')+')';
			attribute<Eur>    Ki_Zon_PV      (PlanRegio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/LokaleEnergieProductie/Ki_Zon_PV')+')';
			attribute<Eur>    Ki_mWKK        (PlanRegio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/LokaleEnergieProductie/Ki_mWKK'  )+')';
			attribute<Eur>    Ki_eWP         (PlanRegio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/LokaleEnergieProductie/Ki_eWP'   )+')';
			attribute<Eur>    Ki_hWP         (PlanRegio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/LokaleEnergieProductie/Ki_hWP'   )+')';
			attribute<Eur>    Ki_EWV         (PlanRegio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/LokaleEnergieProductie/Ki_EWV'   )+')';
			attribute<Eur>    Ki_Totaal30    (PlanRegio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/LokaleEnergieProductie/Ki_Totaal30')+')';
			attribute<Eur>    Ki_Totaal15    (PlanRegio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/LokaleEnergieProductie/Ki_Totaal15')+')';
			attribute<Eur>    Ki_Totaal      (PlanRegio) := Ki_Totaal30+Ki_Totaal15;
			attribute<Eur_yr> Kj_Onderhoud   (PlanRegio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/LokaleEnergieProductie/Kj_Onderh_excl')+')';
			attribute<Eur_yr> Kj_Admin       (PlanRegio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/LokaleEnergieProductie/Kj_admin_excl')+')';
			attribute<Eur_yr> Km_Totaal      (PlanRegio) := Ki_Totaal30 * NCW/mr30/AnnualisationFactor + Ki_Totaal15 * NCW/mr15/AnnualisationFactor + Kj_Onderhoud + Kj_Admin;
		}
		container CentraleWarmteBronnen := for_each_nedv(Voorkeuren/WarmteOpties/Name, impl/BronExpr[Voorkeuren/WarmteOpties/AanbodOptie], PlanRegio, Eur_yr)  // overrule the following defaults
		{
			attribute<Eur_yr> RestWarmte (PlanRegio) := const(0[Eur_yr], PlanRegio);
			attribute<Eur_yr> GeoThermie (PlanRegio) := const(0[Eur_yr], PlanRegio);
			attribute<Eur_yr> WijkWKK    (PlanRegio) := const(0[Eur_yr], PlanRegio);
			attribute<Eur_yr> BioWKK     (PlanRegio) := const(0[Eur_yr], PlanRegio);
			attribute<Eur_yr> WKO        (PlanRegio) := const(0[Eur_yr], PlanRegio);
			attribute<Eur_yr> Km_Totaal  (PlanRegio) := =#Voorkeuren/WarmteOpties == 0 ? 'const(0, PlanRegio, Eur_yr)' : 'add('+AsItemList(Voorkeuren/WarmteOpties/Name)+')';
		}
		container net {
			attribute<Eur_yr> Aardgas       (PlanRegio) := netwerk/aardgas/Kj_totaal       - netwerk/aardgas/netbijdrage;
			attribute<Eur_yr> Elektriciteit (PlanRegio) := netwerk/elektriciteit/Kj_totaal - netwerk/elektriciteit/netbijdrage;
		}
		attribute<Eur_yr> Om_Comfort        (PlanRegio) := =IsDefined(RLookup(Classifications/AanbodOptie/V/WKO, Voorkeuren/WarmteOpties/AanbodOptie)) 
			? 'add('+AsItemList('EndState/WKO/Om_Comfort/'+Classifications/WkoComponent/name)+')'
			: 'CentraleWarmteBronnen/WKO';
		attribute<Eur_yr> Km_Totaal         (PlanRegio) := net/Aardgas + net/Elektriciteit + 
			gebouwen/Km_Totaal + LokaleEnergieProductie/Km_Totaal + CentraleWarmteBronnen/Km_Totaal - Om_Comfort;
	}

	container Eindgebruiker :=
			for_each_ne(Classifications/BebouwingsComponent/name,
				replace(
					'Rapportage/EindgebruikerComponentTempl(
						AllocatieResultaat/@BC@,
						NCW/@NC@15,
						NCW/@NC@30,
						NCW/@NC@50,
						sum(AllocatieResultaat/@BC@/AardGas/Vastrecht,              VestaRunData/@BC@/PlanRegio_rel),
						sum(AllocatieResultaat/@BC@/AardGas/AansluitBijdrage,       VestaRunData/@BC@/PlanRegio_rel), 
						sum(AllocatieResultaat/@BC@/Elektriciteit/Vastrecht,        VestaRunData/@BC@/PlanRegio_rel),
						sum(AllocatieResultaat/@BC@/Elektriciteit/AansluitBijdrage, VestaRunData/@BC@/PlanRegio_rel),
						EndState/KostenBaten/PlanRegioKaarten/@BC@
					)'
				,	'@BC@', Classifications/BebouwingsComponent/name
				,	'@NC@', Classifications/BebouwingsComponent/NcwRefBase
				)
			)
		,	Source = "FO v7a C6";
	container EnergieProducent
	{
		// *** Investeringen zijn niet over 30 jaar genomen maar over de voor de betreffende aanbodtechnologie geldende gebruiksperiode en tegen het voor de EP geldende rentevoet
		container impl: IsHidden = "True"
		{
			attribute<String> FlowExpr(Classifications/AanbodOptie) := union_data(Classifications/AanbodOptie,
				'(GebiedsAllocatie/RestWarmte/BeginStatus/KostenBaten/Oj_wd - EndState/Flow/Kj_Netto) * float64(EndState/AanbodOpties/PlanRegioWarmteAllocatie == Classifications/AanbodOptie/V/RestWarmte)',
				'(GebiedsAllocatie/GeoThermie/BeginStatus/KostenBaten/Oj_wd - EndState/Flow/Kj_Netto) * float64(EndState/AanbodOpties/PlanRegioWarmteAllocatie == Classifications/AanbodOptie/V/GeoThermie)',
				'(GebiedsAllocatie/WijkWKK/BeginStatus/KostenBaten/Oj_wd    - EndState/Flow/Kj_Netto) * float64(EndState/AanbodOpties/PlanRegioWarmteAllocatie == Classifications/AanbodOptie/V/WijkWKK)',
				'(GebiedsAllocatie/BioWKK/BeginStatus/KostenBaten/Oj_wd     - EndState/Flow/Kj_Netto) * float64(EndState/AanbodOpties/PlanRegioWarmteAllocatie == Classifications/AanbodOptie/V/BioWKK)',
				'EndState/WKO/Oj_totaal - EndState/WKO/Kj_Netto'
			);

			attribute<String> StockExpr(Classifications/AanbodOptie) :=
				'EndState/Stock/Ki_Totaal* float64(EndState/AanbodOpties/PlanRegioWarmteAllocatie == Classifications/AanbodOptie/V/'+Classifications/AanbodOptie/Name+')';
		}
		container CashFlow := for_each_nedv(Voorkeuren/WarmteOpties/Name
			,	impl/FlowExpr[Voorkeuren/WarmteOpties/AanbodOptie]
			,	PlanRegio, Eur_yr) // overrule the following defaults
		{
			attribute<Eur_yr> RestWarmte (PlanRegio):= const(0[Eur_yr], PlanRegio);
			attribute<Eur_yr> GeoThermie (PlanRegio):= const(0[Eur_yr], PlanRegio);
			attribute<Eur_yr> WijkWKK    (PlanRegio):= const(0[Eur_yr], PlanRegio);
			attribute<Eur_yr> WKO        (PlanRegio):= const(0[Eur_yr], PlanRegio);
			attribute<Eur_yr> BioWKK     (PlanRegio):= const(0[Eur_yr], PlanRegio);
			attribute<Eur_yr> Kj_Totaal  (PlanRegio):= RestWarmte + GeoThermie + WijkWKK + WKO + BioWKK;
		}

		attribute<Eur_yr> Kj_Totaal(PlanRegio):= CashFlow/Kj_Totaal;

		container Stock := for_each_nedv(Voorkeuren/WarmteOpties/Name
			,	impl/StockExpr[Voorkeuren/WarmteOpties/AanbodOptie]
			,	PlanRegio, Eur) // overrule the following defaults
		{
			attribute<Eur> RestWarmte (PlanRegio) := const(0[Eur], PlanRegio);
			attribute<Eur> GeoThermie (PlanRegio) := const(0[Eur], PlanRegio);
			attribute<Eur> WijkWKK    (PlanRegio) := const(0[Eur], PlanRegio);
			attribute<Eur> WKO        (PlanRegio) := const(0[Eur], PlanRegio);
			attribute<Eur> BioWKK     (PlanRegio) := const(0[Eur], PlanRegio);
			attribute<Eur> Ki_Totaal  (PlanRegio) := RestWarmte + GeoThermie + WijkWKK + WKO + BioWKK;
		}
		attribute<Eur> Ki_Totaal      (PlanRegio) := Stock/Ki_Totaal; // totale investeringen zonder aftrek van de te ontvangen aansluitbijdrage.
	}
	container netwerk {
		attribute<nrWoningen> nrWoonUnits     (Planregio) := ='add('+AsItemList(replace('sum(VestaRunData/@BC@/BebouwingsObject/nrWoonUnits_i, VestaRunData/@BC@/PlanRegio_rel)', '@BC@',Classifications/BebouwingsComponent/name) )+')';
		attribute<nrWoningen> nrAllElectric   (Planregio) := ='add('+AsItemList(replace('sum(VestaRunData/@BC@/BebouwingsObject/nrWoonUnits_i * AllocatieResultaat/@BC@/Aandelen/AllElectric, VestaRunData/@BC@/PlanRegio_rel)', '@BC@',Classifications/BebouwingsComponent/name) )+')';
		attribute<float64>    R_all_electric  (Planregio) := makedefined(nrAllElectric / nrWoonUnits, 0.0);
		attribute<bool >      all_electric    (Planregio) := R_all_electric > 0.999, // tolereer afronding fouten en kleine afwijkingen
			IntegrityCheck = "!(heeft_gas && all_electric)";

		container aardgas {
			attribute<bool>   had_gas         (Planregio) := /TussenResultaten/J2010/heeft_gas; // false voor bestaande restwarmte
			attribute<bool>   verwijderd      (Planregio) := had_gas && !heeft_gas; // alleen true als hele planregio gasloos is geworden

			// inkomsten
			attribute<Eur_yr> netbijdrage     (Planregio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/name+'/gebouwen/Aardgas/netwerk'  )+')';
			attribute<Eur_yr> vastrecht       (Planregio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/gebouwen/AardGas/Vastrecht')+')';
			attribute<Eur_yr> Aansl_m         (Planregio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/gebouwen/AardGas/Aansl_m'  )+')';
			attribute<Eur_yr> Oj_totaal       (Planregio) := netbijdrage + vastrecht;

			// kosten
			attribute<m>      L_infra_nu      (PlanRegio) := float64(heeft_gas ) * EndState/KostenBaten/KostenD/L_infra_g;
			attribute<m>      L_infra_org     (PlanRegio) := J2010/indicatoren/netwerk/aardgas/L_infra_nu;
			attribute<m>      L_infra         (PlanRegio) := max_elem(L_infra_nu, L_infra_org);

			attribute<Eur_yr> kapitaallasten  (Planregio) := L_infra    * Kengetallen/Infra/Kj_g_m;
			attribute<Eur_yr> onderhoud       (Planregio) := L_infra_nu * Kengetallen/Infra/Ko_g_m;
			attribute<Eur_yr> verwijderkosten (Planregio) := float64(verwijderd) * L_infra_org * Kengetallen/Infra/Kj_g_m * (Kengetallen/Infra/VB_g + 1.0);
			attribute<Eur_yr> Kj_totaal       (Planregio) := kapitaallasten + verwijderkosten + onderhoud;
			attribute<Eur_yr> Kj_netto        (Planregio) := Kj_totaal - Oj_totaal - Aansl_m;
		}
		container elektriciteit {
			// inkomsten
			attribute<Eur_yr> netbijdrage     (Planregio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/name+'/gebouwen/Elektriciteit/netwerk'  )+')';
			attribute<Eur_yr> vastrecht       (Planregio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/gebouwen/Elektriciteit/Vastrecht')+')';
			attribute<Eur_yr> Aansl_m         (Planregio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/gebouwen/Elektriciteit/Aansl_m'  )+')';
			attribute<Eur_yr> Oj_totaal       (Planregio) := netbijdrage + vastrecht;

			// kosten
			attribute<m>      L_infra         (PlanRegio) := EndState/KostenBaten/KostenD/L_infra_e;

			attribute<Eur_yr> kapitaallasten  (Planregio) := L_infra * Kengetallen/Infra/Kj_e_m;
			attribute<Eur_yr> onderhoud       (Planregio) := L_infra * Kengetallen/Infra/Ko_e_m;
			attribute<Eur_yr> netverzwaring   (Planregio) := kapitaallasten * R_all_electric;
			attribute<Eur_yr> Kj_totaal       (Planregio) := kapitaallasten + netverzwaring + onderhoud;
			attribute<Eur_yr> Kj_netto        (Planregio) := Kj_totaal - Oj_totaal - Aansl_m;
		}
	}

	container MaatregelenRapportage
	{
		unit<uint32> RapportageRegio := SpecifiekeInstellingen/ReportOpties/RapportageRegio;
		container BestaandeWoning :=
			for_each_ne(
				 RapportageRegio/Name
				, 'Rapportage/RegionaleRapportageTempl(
					  RapportageRegio, '+string(id(RapportageRegio))+'[RapportageRegio]
					, AllocatieResultaat/BestaandeWoning, '+quote('BestaandeWoning')+'
					, '+Classifications/WoningBestaandRapGroep/ClassRef[SpecifiekeInstellingen/ReportOpties/WoningBestaandRapGroep]+'
					, '+quote('BebouwingsObject/'+Classifications/WoningBestaandRapGroep/AttrRef [ SpecifiekeInstellingen/ReportOpties/WoningBestaandRapGroep ])+'
					, Classifications/WoningBestaandRapGroep/ExportLabel [ SpecifiekeInstellingen/ReportOpties/WoningBestaandRapGroep ]
					, units/nrWoningen
				)'
			);
		container NieuwbouwWoning :=
			for_each_ne(
				 RapportageRegio/Name
				, 'Rapportage/RegionaleRapportageTempl(
					  RapportageRegio, '+string(id(RapportageRegio))+'[RapportageRegio]
					, AllocatieResultaat/NieuwbouwWoning, '+quote('NieuwbouwWoning')+'
					, VestaRunData/NieuwbouwWoning/BebouwingsTypeDomein
					, '+quote('RuimtelijkeVraagComponent/BebouwingsType')+'
					, '+quote('WoningType')+'
					, units/nrWoningen
				)'
			);
		container BestaandeUtil:=
			for_each_ne(
				 RapportageRegio/Name
				, 'Rapportage/RegionaleRapportageTempl(
					  RapportageRegio, '+string(id(RapportageRegio))+'[RapportageRegio]
					, AllocatieResultaat/BestaandeUtil, '+quote('BestaandeUtil')+'
					, VestaRunData/BestaandeUtil/BebouwingsTypeDomein
					, '+quote('RuimtelijkeVraagComponent/BebouwingsType')+'
					, '+quote('UtiliteitsType')+'
					, units/m2
				)'
			);
		container NieuwbouwUtil:=
			for_each_ne(
				 SpecifiekeInstellingen/ReportOpties/RapportageRegio/Name
				, 'Rapportage/RegionaleRapportageTempl(
					  RapportageRegio, '+string(id(RapportageRegio))+'[RapportageRegio]
					, AllocatieResultaat/NieuwbouwUtil, '+quote('NieuwbouwUtil')+'
					, VestaRunData/NieuwbouwUtil/BebouwingsTypeDomein
					, '+quote('RuimtelijkeVraagComponent/BebouwingsType')+'
					, '+quote('UtiliteitsType')+'
					, units/m2
				)'
			);
		container BestaandeGlTb :=
			for_each_ne(
				 SpecifiekeInstellingen/ReportOpties/RapportageRegio/Name
				, 'Rapportage/RegionaleRapportageTempl(
					  RapportageRegio, '+string(id(RapportageRegio))+'[RapportageRegio]
					, AllocatieResultaat/BestaandeGlTb, '+quote('BestaandeGlTb')+'
					, Classifications/GLTB
					, '+quote('RuimtelijkeVraagComponent/BebouwingsType')+'
					, '+quote('GlasTuinBouwType')+'
					, units/m2
				)'
			);
		container NieuwbouwGlTb:=
			for_each_ne(
				 SpecifiekeInstellingen/ReportOpties/RapportageRegio/Name
				, 'Rapportage/RegionaleRapportageTempl(
					  RapportageRegio, '+string(id(RapportageRegio))+'[RapportageRegio]
					, AllocatieResultaat/NieuwbouwGlTb, '+quote('NieuwbouwGlTb')+'
					, Classifications/GlTb
					, '+quote('RuimtelijkeVraagComponent/BebouwingsType')+'
					, '+quote('NieuwbouwGlTb')+'
					, units/m2
				)'
			);
	}

	container InputOutput :=
		for_each_ne(
			 SpecifiekeInstellingen/ReportOpties/RapportageRegio/Name
		,	'Rapportage/FlowTempl('
				'SpecifiekeInstellingen/ReportOpties/RapportageRegio'
				','+Quote(SpecifiekeInstellingen/ReportOpties/RapportageRegio/Name)+
				', value('+ string(id(SpecifiekeInstellingen/ReportOpties/RapportageRegio)) + ',SpecifiekeInstellingen/ReportOpties/RapportageRegio)'
			')'
		)
	{
		container Totaal := =(SpecifiekeInstellingen/ReportOpties/RapportageRegio/Name != 'NL')
			? 'Rapportage/FlowTempl(geography/RegioIndelingen/NL, ''NL'', value(0, geography/RegioIndelingen/NL) )'
			: '.';
	}

	parameter<String> GenerateAll := 'Ready',
		ExplicitSuppliers =
			"= ';' + AsList('InputOutput/'+SpecifiekeInstellingen/ReportOpties/RapportageRegio/Name+'/GenerateAll',';')"
			"+ (SpecifiekeInstellingen/ReportOpties/RapportageRegio/Name != 'NL' ? ';InputOutput/Totaal/GenerateAll': '')"
			"+ ';' + AsList('MaatregelenRapportage/BestaandeWoning/'+SpecifiekeInstellingen/ReportOpties/RapportageRegio/Name+'/GenerateAll',';')"
			"+ ';' + AsList('MaatregelenRapportage/NieuwbouwWoning/'+SpecifiekeInstellingen/ReportOpties/RapportageRegio/Name+'/GenerateAll',';')"
			"+ ';' + AsList('MaatregelenRapportage/BestaandeUtil/'  +SpecifiekeInstellingen/ReportOpties/RapportageRegio/Name+'/GenerateAll',';')"
			"+ ';' + AsList('MaatregelenRapportage/NieuwbouwUtil/'  +SpecifiekeInstellingen/ReportOpties/RapportageRegio/Name+'/GenerateAll',';')"
			"+ ';' + AsList('MaatregelenRapportage/BestaandeGlTb/'  +SpecifiekeInstellingen/ReportOpties/RapportageRegio/Name+'/GenerateAll',';')"
			"+ ';' + AsList('MaatregelenRapportage/NieuwbouwGlTb/'  +SpecifiekeInstellingen/ReportOpties/RapportageRegio/Name+'/GenerateAll',';')"
//			"+ ';' + 'AllocExports/GenerateThisToo'" // comment-out if you want to skip all exports
		;
}
