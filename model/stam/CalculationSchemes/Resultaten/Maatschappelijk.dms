//////////////////////////////////////////////////////////////////////////////////////////
//                                                                                      //
//                   (C) VESTA 2019 - Planbureau voor de Leefomgeving                   //
//                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////

container Maatschappelijk: Source = "FO v7a C6"
{
	container impl: IsHidden = "true"
	{
		attribute<String> BronExpr(Classifications/AanbodOptie) :=
				+replace(
					Classifications/AanbodOptie/IsPlanregioOptie
					? 'StateNaAllocatie/Flow/Kmi_Total * float64(StateNaAllocatie/PlanRegioWarmteAllocatie_REMOVE == Classifications/AanbodOptie/V/@RO@)'
					: 'StateNaAllocatie/@RO@/Kmi_Totaal'
				, '@RO@', Classifications/AanbodOptie/Name);
	}

	container gebouwen
	{
		container ElektriciteitsVraag := // excl WKO vraag
				for_each_nedv(Classifications/BebouwingsComponent/Name, 
					replace(
						'sum(Allocatie/@BC@/Elektriciteit/maatschappelijk, BebouwingsComponenten/@BC@/PlanRegio_rel)'
					,	'@BC@', Classifications/BebouwingsComponent/Name
					)
				,PlanRegio
				,Eur_yr
				)
		{
			attribute<Eur_yr> Km_Totaal(PlanRegio) := ='add('+AsItemList(Classifications/BebouwingsComponent/Name)+')';
		}

		container AardGasVerbruik := // incl mWKK excl Wijk_wkk en kosten bijstook WOS
				for_each_nedv(Classifications/BebouwingsComponent/Name,
					replace(
						'sum(Allocatie/@BC@/AardGas/maatschappelijk, BebouwingsComponenten/@BC@/PlanRegio_rel)'
					,	'@BC@', Classifications/BebouwingsComponent/Name
					)
				,PlanRegio
				,Eur_yr
				)
		{
			attribute<Eur_yr> Km_Totaal (PlanRegio) := ='add('+AsItemList(Classifications/BebouwingsComponent/Name)+')';
		}

		attribute<Eur>    Ki_GebouwVerbetering (PlanRegio) := ='add('+AsItemList('sum(BebouwingsComponenten/'+Classifications/BebouwingsComponent/Name+'/Kosten/Ki_GebouwVerbetering, BebouwingsComponenten/'+Classifications/BebouwingsComponent/Name+'/PlanRegio_rel)')+')';
		attribute<Eur_yr> Kmi_GebouwVerbetering(PlanRegio) :=  Ki_GebouwVerbetering *NCW/mr30/AnnualisationFactor;

		attribute<Eur_yr> Kj_Onderhoud         (PlanRegio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/gebouwen/Kj_Onderh_excl')+')';
		attribute<Eur_yr> Kj_Admin             (PlanRegio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/gebouwen/Kj_Admin_excl')+')';

		attribute<Eur_yr> Kmi_Totaal            (PlanRegio) := ElektriciteitsVraag/Km_Totaal + AardGasVerbruik/Km_Totaal + Kmi_GebouwVerbetering + Kj_Onderhoud + Kj_Admin;
	}
	container LokaleEnergieProductie
	{
		attribute<Eur>    Ki_ZonneBoiler (PlanRegio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/LokaleEnergieProductie/Ki_ZonneBoiler')+')';
		attribute<Eur>    Ki_Zon_PV      (PlanRegio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/LokaleEnergieProductie/Ki_Zon_PV')+')';
		attribute<Eur>    Ki_mWKK        (PlanRegio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/LokaleEnergieProductie/Ki_mWKK'  )+')';
		attribute<Eur>    Ki_eWP         (PlanRegio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/LokaleEnergieProductie/Ki_eWP'   )+')';
		attribute<Eur>    Ki_hWP         (PlanRegio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/LokaleEnergieProductie/Ki_hWP'   )+')';
		attribute<Eur>    Ki_EWV         (PlanRegio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/LokaleEnergieProductie/Ki_EWV'   )+')';
		attribute<Eur>    Ki_Totaal30    (PlanRegio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/LokaleEnergieProductie/Ki_Totaal30')+')';
		attribute<Eur>    Ki_Totaal15    (PlanRegio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/LokaleEnergieProductie/Ki_Totaal15')+')';
		attribute<Eur>    Ki_Totaal      (PlanRegio) := Ki_Totaal30 + Ki_Totaal15;
		attribute<Eur_yr> Kj_Onderhoud   (PlanRegio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/LokaleEnergieProductie/Kj_Onderh_excl')+')';
		attribute<Eur_yr> Kj_Admin       (PlanRegio) := ='add('+AsItemList('Eindgebruiker/'+Classifications/BebouwingsComponent/Name+'/LokaleEnergieProductie/Kj_admin_excl')+')';
		attribute<Eur_yr> Kmi_Totaal     (PlanRegio) := Ki_Totaal30 * NCW/mr30/AnnualisationFactor + Ki_Totaal15 * NCW/mr15/AnnualisationFactor + Kj_Onderhoud + Kj_Admin;
	}
	container CentraleWarmteBronnen := for_each_nedv(Voorkeuren/WarmteOpties/Name, impl/BronExpr[Voorkeuren/WarmteOpties/AanbodOptie], PlanRegio, Eur_yr)
	{
		attribute<Eur_yr> Kmi_Totaal  (PlanRegio) := =#Voorkeuren/WarmteOpties == 0 ? 'const(0, PlanRegio, Eur_yr)' : 'add('+AsItemList(Voorkeuren/WarmteOpties/Name)+')';
	}
	container net
	{
		attribute<Eur_yr> Aardgas       (PlanRegio) := netwerken/aardgas/Kj_totaal       - netwerken/aardgas/netbijdrage;
		attribute<Eur_yr> Elektriciteit (PlanRegio) := netwerken/elektriciteit/Kj_totaal - netwerken/elektriciteit/netbijdrage;
	}
	attribute<Eur_yr> Om_Comfort        (PlanRegio) := ='add('+AsItemList('StateNaAllocatie/' + Voorkeuren/WarmteOpties/Name + '/Om_ComfortT')+')';
	attribute<Eur_yr> Kmi_Totaal         (PlanRegio) := net/Aardgas + net/Elektriciteit + gebouwen/Kmi_Totaal + LokaleEnergieProductie/Kmi_Totaal + CentraleWarmteBronnen/Kmi_Totaal;
}